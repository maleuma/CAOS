
                
  0200           
  0200          ;*********************************
  0200          ;**                             **
  0200          ;**     CAOS 4.1 reassembliert  **
  0200          ;**     von MLsoft              **
  0200          ;**     13.06.92 bis 05.07.92   **
  0200          ;**                             **
  0200          ;*********************************
  0200           
  0200          ;Achtung: mind. FREE 68FF erforderlich!
  0200           
  0200          ;**     Speicher}bersicht       **
  0200           
  0200          ;C000   MODUL, SYSTEM
  0200          ;C0F7   KEYL, SQR, MULT, WININ, COLOR
  0200          ;C17C   LINE, PUDE
  0200          ;C206   DISPLAY, MODIFY
  0200          ;C2B8   V24OUT
  0200          ;C3B2   ShCLR-Reaktionsprogramm
  0200          ;C408   Hard- und Screencopyroutinen
  0200          ;C632   V24DUP und UMINIT
  0200          ;C6F1   GARG, MBIN, MBOUT
  0200          ;C887   frei *)
  0200          ;--------------------------------
  0200          ;E000   RESET und BASIC-Verteiler
  0200          ;E040   CRT-Treiber mit PADR
  0200          ;E339   KBD-Treiber mit BRKT
  0200          ;E3EF   SWITCH, JUMP
  0200          ;E4BB   TAPE-Treiber
  0200          ;E65C   BASIC-Extension *)
  0200          ;EDE8   frei *)
  0200          ;EE00   Zeichenbildtab. Gro~buchst. *)
  0200          ;--------------------------------
  0200          ;F000   PWRON und Programmverteiler
  0200          ;F12A   Kommandointerpreter (MENU)
  0200          ;F238   KBD, IRM-Default, INLIN
  0200          ;F354   Hexzahlen-Verarbeitung
  0200          ;F3D0   Systeminit, SIXD, Portinittab.
  0200          ;F46D   SAVE, VERIFY, LOAD
  0200          ;F5F2   COLOR, CSTBT, DISPLAY, MODIFY,
  0200          ;       WINDOW-Routinen, KEY, KEYLIST
  0200          ;F723   CUCP, Grafikroutinen, CIRCLE
  0200          ;F864   MBOUT,MBIN, MODUL, SYSTEM
  0200          ;F8A0   BASIC-I/O-Verteiler
  0200          ;F915   ZKOUT, TON, ESC-Progr{mmchen
  0200          ;F9BF   V24OUT, V24DUP, ISR SIO
  0200          ;FB5A   ROMC-Schalter, MULT, SQR
  0200          ;FB84   ESC/P->IBM-Codewandeltabelle
  0200          ;FB92   I/O-UP-Nummer-Tabelle
  0200          ;FB9A   ESC-Tabelle
  0200          ;FBB0   Unterprogrammtab. f}r SUTAB
  0200          ;FC42   Tastaturtabelle KTAB
  0200          ;FCC2   Interruptvektortabelle
  0200          ;FCD0   Steuerfunktionstabelle f}r CTAB
  0200          ;FD10   BASIC-Token-u.Zeiger-Tabelle *)
  0200          ;FDF6   frei *)
  0200          ;FDF8   Bittabelle f}r Punktroutinen
  0200          ;FE00   Zeichenbildtab. Kleinbuchst. *)
  0200          ;--------------------------------
  0200          ;*)     ist nicht in diesem Listing
  0200          ;       aufgef}hrt
  0200           
  0200           
  0200          ;**     EQU-Liste               **
  0200           
  0200          ARGC    EQU     0B780H
  0200          ARGN    EQU     0B781H
  0200          ARG1    EQU     0B782H
  0200          ARG2    EQU     0B784H
  0200          ARG3    EQU     0B786H
  0200          ARG49   EQU     0B788H
  0200          ARG10   EQU     0B794H
  0200          NUMNX   EQU     0B796H
  0200          NUMVX   EQU     0B797H
  0200          HCADR   EQU     0B799H
  0200          WINNR   EQU     0B79BH
  0200          WINON   EQU     0B79CH
  0200          WINLG   EQU     0B79EH
  0200          CURSO   EQU     0B7A0H
  0200          STBT    EQU     0B7A2H
  0200          COLOR   EQU     0B7A3H
  0200          WEND    EQU     0B7A4H
  0200          CCTL0   EQU     0B7A6H
  0200          SYSP    EQU     0B7AEH
  0200          SUTAB   EQU     0B7B0H
  0200          CTAB    EQU     0B7B2H
  0200          BJUMP   EQU     0B7B4H
  0200          OUTAB   EQU     0B7B9H
  0200          INTAB   EQU     0B7BBH
  0200          UOUT1   EQU     0B7BDH
  0200          UIN1    EQU     0B7C0H
  0200          UOUT2   EQU     0B7C3H
  0200          UIN2    EQU     0B7C6H
  0200          IOERR   EQU     0B7C9H
  0200          VRAM    EQU     0B7CBH
  0200          ZOTAB   EQU     0B7CDH
  0200          ZWEND   EQU     0B7CFH
  0200          FTAST   EQU     0B7D1H
  0200          HOR     EQU     0B7D3H
  0200          VERT    EQU     0B7D5H
  0200          FARB    EQU     0B7D6H
  0200          MIXIT   EQU     0B7D7H
  0200          VORTN   EQU     0B7D8H
  0200          DTADR   EQU     0B7DAH
  0200          DTNR    EQU     0B7DCH
  0200          L3TAB   EQU     0B7DDH
  0200          L3SIZ   EQU     0B7DFH
  0200          COUNT   EQU     0B7E0H
  0200          HCPZ    EQU     0B7E1H
  0200          INTV1   EQU     0B7E2H
  0200          INTV1L  EQU     0B7E4H
  0200          INTV2   EQU     0B7E5H
  0200          INTV2L  EQU     0B7E7H
  0200          HCPZ2   EQU     0B7E8H
  0200          
  0200          WNDFN   EQU     0B99CH  ; Fenstervektoren
  0200          ;--------------------------------
  0200           
  0200           
  0200          ;**     CAOS 4.1 ROM C          **
  0200           
  0200                  ORG     0C000H
  C000           
  C000           
  C000          MODULC  ;Modulkontrollanzeige
  C000 D5               PUSH    DE
  C001 2E08             LD      L,8     ;ab Platz 8
  C003 E5       MODC1   PUSH    HL
  C004 3E01             LD      A,1     ;lesen
  C006 CD0BE4           CALL    MODU
  C009 7C               LD      A,H
  C00A 3C               INC     A       ;Kennbyte=FFh?
  C00B 280F             JR      Z,MODC2 ;kein Modul
  C00D 7D               LD      A,L
  C00E CD59F3           CALL    AHSPC   ;Platz
  C011 7C               LD      A,H
  C012 CD59F3           CALL    AHSPC   ;Kennbyte
  C015 7A               LD      A,D
  C016 CD76F3           CALL    AHEX    ;Steuerbyte
  C019 CD6BF3           CALL    CRLF
  C01C E1       MODC2   POP     HL
  C01D 7D               LD      A,L
  C01E C604             ADD     4       ;Viererschritte
  C020 6F               LD      L,A
  C021 30E0             JR      NC,MODC1;bis FCh
  C023 CD6BF3           CALL    CRLF
  C026 D1               POP     DE
  C027 C9               RET
  C028           
  C028          SYSTC   ;Systemcheck, umst{ndlichst!!
  C028 D5               PUSH    DE
  C029 CDEEF1           CALL    OSTR
  C02C 43414F53         DEFM    'CAOS'
  C030 00               NOP
  C031 3E45             LD      A,'E'
  C033 CD29E3           CALL    CRT
  C036 DB88             IN      A,88H
  C038 0F               RRCA            ;Bit 0
  C039 3805             JR      C,SYSC1
  C03B CDECC0           CALL    WON     ;on/off ausg.
  C03E 1803             JR      SYSC2
  C040           
  C040 CDE2C0   SYSC1   CALL    WOFF
  C043 CDEEF1   SYSC2   CALL    OSTR
  C046 524F4D43         DEFM    'ROMC '
  C04B 00               NOP
  C04C DD7E04           LD      A,(IX+4)
  C04F CB3F             SRL     A       ;Bits 5,6
  C051 CB3F             SRL     A
  C053 CB3F             SRL     A
  C055 CB3F             SRL     A
  C057 CB3F             SRL     A       ;nach Bits 0,1
  C059 2F               CPL
  C05A E633             AND     '3'
  C05C CD29E3           CALL    CRT     ;Ebene ausgeben
  C05F DB88             IN      A,88H
  C061 5F               LD      E,A
  C062 07               RLCA            ;Bit 7
  C063 3805             JR      C,SYSC3
  C065 CDECC0           CALL    WON     ;on/off ausg.
  C068 1803             JR      SYSC4
  C06A           
  C06A CDE2C0   SYSC3   CALL    WOFF
  C06D CDDAC0   SYSC4   CALL    WRAM    ;'RAM' ausg.
  C070 3E30             LD      A,'0'
  C072 CD29E3           CALL    CRT     ;RAM0
  C075 CB4B             BIT     1,E     ;von 88h
  C077 2005             JR      NZ,SYSC5
  C079 CDECC0           CALL    WON
  C07C 1803             JR      SYSC6
  C07E           
  C07E CDE2C0   SYSC5   CALL    WOFF
  C081 CDDAC0   SYSC6   CALL    WRAM
  C084 3E34             LD      A,'4'   ;RAM4
  C086 CD29E3           CALL    CRT
  C089 DDCB0446         BIT     0,(IX+4)
  C08D 2005             JR      NZ,SYSC7
  C08F CDECC0           CALL    WON     ;on/off ausg.
  C092 1803             JR      SYSC8
  C094           
  C094 CDE2C0   SYSC7   CALL    WOFF
  C097 CDDAC0   SYSC8   CALL    WRAM
  C09A CDEEF1           CALL    OSTR
  C09D 3820             DEFM    '8 '    ;RAM8
  C09F 00               NOP
  C0A0 DD7E01           LD      A,(IX+1)
  C0A3 CB3F             SRL     A       ;Bits 4,5,6,7
  C0A5 CB3F             SRL     A
  C0A7 CB3F             SRL     A
  C0A9 CB3F             SRL     A       ;nach 0,1,2,3
  C0AB C62E             ADD     '0'-2
  C0AD CD29E3           CALL    CRT     ;Ebene ausg.
  C0B0 DB89             IN      A,89H
  C0B2 CB6F             BIT     5,A
  C0B4 2005             JR      NZ,SYSC9
  C0B6 CDECC0           CALL    WON     ;on/off ausg.
  C0B9 1803             JR      SYSC10
  C0BB           
  C0BB CDE2C0   SYSC9   CALL    WOFF
  C0BE DD7E01   SYSC10  LD      A,(IX+1)
  C0C1 5F               LD      E,A
  C0C2 CDEEF1           CALL    OSTR
  C0C5 42494C44         DEFM    'BILD '
  C0CA 00               NOP
  C0CB CB53             BIT     2,E     ;Zugriffs-Bit
  C0CD 3E30             LD      A,'0'
  C0CF 2801             JR      Z,SYSC11
  C0D1 3C               INC     A
  C0D2 CD29E3   SYSC11  CALL    CRT     ;Nummer ausg.
  C0D5 CD6BF3           CALL    CRLF
  C0D8 D1               POP     DE
  C0D9 C9               RET
  C0DA           
  C0DA CDEEF1   WRAM    CALL    OSTR
  C0DD 52414D           DEFM    'RAM'
  C0E0 00               NOP
  C0E1 C9               RET
  C0E2           
  C0E2 CDEEF1   WOFF    CALL    OSTR
  C0E5 204F4E           DEFM    ' ON'
  C0E8 0D0A             DEFW    0A0DH
  C0EA 00               NOP
  C0EB C9               RET
  C0EC           
  C0EC CDEEF1   WON     CALL    OSTR
  C0EF 204F4646         DEFM    ' OFF'
  C0F3 0D0A             DEFW    0A0DH
  C0F5 00               NOP
  C0F6 C9               RET
  C0F7           
  C0F7          KEYLIC  ;Anzeige Belegung F-Tasten
  C0F7 2100B9           LD      HL,0B900H
  C0FA 01F00C           LD      BC,0CF0H
  C0FD 0C       KEYLC1  INC     C
  C0FE 79               LD      A,C
  C0FF CDEBF1           CALL    AHOSTR  ;Fn ausgeben
  C102 203A             DEFM    ' :'
  C104 00               NOP
  C105 CDA8F6           CALL    NCHAR   ;Belegung
  C108 CD6BF3           CALL    CRLF
  C10B 10F0             DJNZ    KEYLC1  ;12*
  C10D C9               RET
  C10E           
  C10E          SQRC    ;Quadratwurzel A=SQR(HL)
  C10E AF               XOR     A
  C10F 3EFF             LD      A,0FFH
  C111 5F               LD      E,A
  C112 57               LD      D,A
  C113 3C       SQRC1   INC     A
  C114 13               INC     DE
  C115 13               INC     DE
  C116 ED52             SBC     HL,DE
  C118 F213C1           JP      P,SQRC1
  C11B C9               RET
  C11C           
  C11C          MULC    ;Multiplikation BA=C*D
  C11C 0600             LD      B,0
  C11E 1E09             LD      E,9
  C120 7A       MULC1   LD      A,D
  C121 1F               RRA
  C122 1D               DEC     E
  C123 C8               RET     Z
  C124 57               LD      D,A
  C125 78               LD      A,B
  C126 3001             JR      NC,MULC2
  C128 81               ADD     C
  C129 1F       MULC2   RRA
  C12A 47               LD      B,A
  C12B 18F3             JR      MULC1   ;8*
  C12D           
  C12D          WININC  ;Initialisierung eines Fensters
  C12D F5               PUSH    AF
  C12E FE0A             CP      0AH     ;No.
  C130 302E             JR      NC,WINIE
  C132 7A               LD      A,D     ;Zeilen
  C133 A7               AND     A
  C134 282A             JR      Z,WINIE
  C136 84               ADD     H
  C137 3827             JR      C,WINIE
  C139 FE21             CP      21H
  C13B 3023             JR      NC,WINIE
  C13D 7B               LD      A,E     ;Spalten
  C13E A7               AND     A
  C13F 281F             JR      Z,WINIE
  C141 81               ADD     C       ;richtig: ADD L!
  C142 381C             JR      C,WINIE
  C144 FE29             CP      29H
  C146 3018             JR      NC,WINIE
  C148 F1               POP     AF
  C149 D5               PUSH    DE
  C14A E5               PUSH    HL
  C14B CD94F6           CALL    WCOPY
  C14E E1               POP     HL
  C14F D1               POP     DE
  C150 229CB7           LD      (WINON),HL
  C153 ED539EB7         LD      (WINLG),DE
  C157 ED43A0B7         LD      (CURSO),BC
  C15B 329BB7           LD      (WINNR),A
  C15E A7               AND     A
  C15F C9               RET
  C160           
  C160 F1       WINIE   POP     AF
  C161 37               SCF
  C162 C9               RET
  C163           
  C163          COLRC   ;Farbe setzen (aus L und E)
  C163 3E1F             LD      A,1FH
  C165 A5               AND     L
  C166 17               RLA
  C167 17               RLA
  C168 17               RLA
  C169 6F               LD      L,A
  C16A 3A81B7           LD      A,(ARGN)
  C16D FE02             CP      2
  C16F 7B               LD      A,E
  C170 3003             JR      NC,COLRC1
  C172 3AA3B7           LD      A,(COLOR)
  C175 E607     COLRC1  AND     7
  C177 B5               OR      L
  C178 32A3B7           LD      (COLOR),A
  C17B C9               RET
  C17C           
  C17C          LINEC   ;Zeichnen Linie von
  C17C          ;       (ARG1)/(ARG2) nach (ARG3)/(ARG4)
  C17C 2A84B7           LD      HL,(ARG2) ;YANF
  C17F ED5B88B7         LD      DE,(ARG49);YEND
  C183 D9               EXX
  C184 2A82B7           LD      HL,(ARG1) ;XANF
  C187 ED5B86B7         LD      DE,(ARG3) ;XEND
  C18B AF       LINC1   XOR     A       ;Bit-Merker=0
  C18C ED52             SBC     HL,DE
  C18E 3007             JR      NC,LINC2;wenn XANF>=XEND
  C190 19               ADD     HL,DE   ;r}ckg{ngig
  C191 EB               EX      DE,HL
  C192 D9               EXX
  C193 EB               EX      DE,HL
  C194 D9               EXX
  C195 18F4             JR      LINC1   ;HL=XSTEPS
  C197           
  C197 D9       LINC2   EXX
  C198 D5               PUSH    DE      ;YEND retten
  C199 ED52     LINC3   SBC     HL,DE
  C19B 3006             JR      NC,LINC4;steigende Linie
  C19D 19               ADD     HL,DE   ;r}ckg{ngig
  C19E EB               EX      DE,HL
  C19F F601             OR      1       ;merken
  C1A1 18F6             JR      LINC3   ;fallende Linie
  C1A3 E5       LINC4   PUSH    HL      ;YSTEPS retten
  C1A4 D9               EXX             ;HL=XSTEPS
  C1A5 C1               POP     BC      ;BC=YSTEPS
  C1A6 E5               PUSH    HL
  C1A7 ED42             SBC     HL,BC   ;Vergleich
  C1A9 E1               POP     HL
  C1AA C5               PUSH    BC
  C1AB 3003             JR      NC,LINC5;HL>=BC, flache
  C1AD E3               EX      (SP),HL ;HL<BC, steile
  C1AE F602             OR      2       ;Linie merken
  C1B0 E5       LINC5   PUSH    HL      ;HL=MAX(XSTEPS,
  C1B1 44               LD      B,H     ; YSTEPS)
  C1B2 4D               LD      C,L     ;=Punktezahl-1
  C1B3 D9               EXX
  C1B4 C1               POP     BC      ;BC=MAX(...)
  C1B5 D1               POP     DE      ;DE=MIN(XSTEPS,
  C1B6 60               LD      H,B     ; YSTEPS)
  C1B7 69               LD      L,C
  C1B8 CB3C             SRL     H       ;Slave mit 1/2
  C1BA CB1D             RR      L       ;+Master laden
  C1BC D9               EXX
  C1BD E1               POP     HL
  C1BE EB               EX      DE,HL
  C1BF           
  C1BF                  ;Hauptschleife
  C1BF          ;AF Bitregister
  C1BF          ;HL lfd. X-Koordinate   HL' Slave
  C1BF          ;DE lfd. Y-Koordinate   DE' -Master
  C1BF          ;BC Punktez{hler        BC' +Master
  C1BF           
  C1BF CDE1F7   LINCS   CALL    SHLDE   ;Punkt setzen
  C1C2 D9               EXX
  C1C3 A7               AND     A
  C1C4 ED52             SBC     HL,DE   ;-Master subtr
  C1C6 3001             JR      NC,LINC6;kein ]bertrag-
  C1C8                                  ;kein Sprung!
  C1C8 09               ADD     HL,BC   ;+Master add.
  C1C9                                  ;CY bleibt =1!
  C1C9 D9       LINC6   EXX
  C1CA CB4F             BIT     1,A     ;steil?
  C1CC 2004             JR      NZ,LINC7
  C1CE 23               INC     HL
  C1CF 300B             JR      NC,LINC9
  C1D1 A7               AND     A
  C1D2 CB47     LINC7   BIT     0,A     ;fallend?
  C1D4 13               INC     DE
  C1D5 2802             JR      Z,LINC8
  C1D7 1B               DEC     DE
  C1D8 1B               DEC     DE
  C1D9 3001     LINC8   JR      NC,LINC9
  C1DB 23               INC     HL
  C1DC 08       LINC9   EX      AF
  C1DD 78               LD      A,B
  C1DE B1               OR      C       ;letzter Punkt?
  C1DF C8               RET     Z
  C1E0 0B               DEC     BC
  C1E1 08               EX      AF
  C1E2 18DB             JR      LINCS
  C1E4           
  C1E4          PUDEC   ;Punkt l|schen, PA: A=Farbbyte
  C1E4 E5               PUSH    HL
  C1E5 D5               PUSH    DE
  C1E6 C5               PUSH    BC
  C1E7 CD39F7           CALL    BITT    ;aus BITTAB ber.
  C1EA 3816             JR      C,PUDEC1        ;au~erhb
  C1EC 47               LD      B,A     ;Bit
  C1ED 4E               LD      C,M
  C1EE 2F               CPL
  C1EF A1               AND     C
  C1F0 77               LD      M,A     ;l|schen
  C1F1 DD7E01           LD      A,(IX+1)
  C1F4 5F               LD      E,A
  C1F5 EE02             XOR     2
  C1F7 F3               DI
  C1F8 D384             OUT     84H
  C1FA 56               LD      D,M     ;Farbbyte
  C1FB 7B               LD      A,E
  C1FC D384             OUT     84H
  C1FE FB               EI
  C1FF 78               LD      A,B     ;Z=1: Punkt
  C200 A1               AND     C       ;war gel|scht
  C201 7A               LD      A,D     ;Frabbyte
  C202 C1       PUDEC1  POP     BC
  C203 D1               POP     DE
  C204 E1               POP     HL
  C205 C9               RET
  C206           
  C206          DPMEMO  ;Ausgabe einer Displayzeile
  C206                  ;HL=Adr, C=Anzahl Bytes
  C206 CD54F3           CALL    HLHX    ;Adresse
  C209 E5               PUSH    HL
  C20A 41               LD      B,C
  C20B CD23F6   DPME1   CALL    LDAME   ;Byte holen
  C20E 23               INC     HL
  C20F CD59F3           CALL    AHSPC   ;anzeigen
  C212 10F7             DJNZ    DPME1   ;C*
  C214 E1               POP     HL
  C215 41               LD      B,C
  C216 3E09             LD      A,9     ;CUR
  C218 CD1DF2           CALL    OCHR
  C21B CD23F6   DPME2   CALL    LDAME   ;Byte holen
  C21E 23               INC     HL
  C21F CD03F6           CALL    CSTBT   ;als ASCII
  C222 10F7             DJNZ    DPME2
  C224 C9               RET
  C225           
  C225          DISPC   ;Anzeige Speicherbereich
  C225                  ;HL=Adr, E=Listblockl{nge
  C225                  ;C=Anzahl Bytes pro Zeile
  C225                  ;A=Anzahl Arergumente
  C225 FE03             CP      3
  C227 3008             JR      NC,DISP1
  C229 0E08             LD      C,8     ;default
  C22B FE02             CP      2
  C22D 3002             JR      NC,DISP1
  C22F 1E04             LD      E,4     ;default
  C231 D5       DISP1   PUSH    DE
  C232 CD06C2   DISP2   CALL    DPMEMO  ;1 Zeile
  C235 CD6BF3           CALL    CRLF
  C238 CDE5E3           CALL    BRKT
  C23B 3806             JR      C,DISP3
  C23D 1D               DEC     E
  C23E 20F2             JR      NZ,DISP2;E*
  C240 CD17F2           CALL    INTB    ;Warten Taste
  C243 D1       DISP3   POP     DE
  C244 FE03             CP      3       ;BRK?
  C246 C8               RET     Z
  C247 FE13             CP      13H     ;STOP?
  C249 20E6             JR      NZ,DISP1
  C24B 1802             JR      MODI1
  C24D           
  C24D          MODIC   ;Anzeige/Ver{ndern Speicher-
  C24D                  ;bereich, Par. wie DISPC
  C24D 0E01             LD      C,1
  C24F CD06C2   MODI1   CALL    DPMEMO
  C252 3E05             LD      A,5
  C254 32A0B7           LD      (CURSO),A
  C257 CD34F3           CALL    INLIN
  C25A E5               PUSH    HL
  C25B CD92F3           CALL    RHEX    ;Adresse
  C25E 7E               LD      A,M
  C25F E1               POP     HL
  C260 3851             JR      C,MODI9 ;Fehler
  C262 A7               AND     A
  C263 28EA             JR      Z,MODI1
  C265 2A97B7           LD      HL,(NUMVX)
  C268 E5       MODI2   PUSH    HL
  C269 CD92F3           CALL    RHEX    ;Datenbyte
  C26C 7E               LD      A,M
  C26D A7               AND     A       ;kein Zeichen
  C26E 23               INC     HL      ;einlesbar?
  C26F 7E               LD      A,M
  C270 E1               POP     HL
  C271 2804             JR      Z,MODI4
  C273 CD3BF6   MODI3   CALL    LDMAE   ;eintragen
  C276 23               INC     HL
  C277 1A       MODI4   LD      A,(DE)
  C278 FE2E             CP      '.'     ;Abschlu~?
  C27A C8               RET     Z
  C27B FE2C             CP      ','     ;1 Zeichen?
  C27D 2005             JR      NZ,MODI5
  C27F 13               INC     DE
  C280 1A               LD      A,(DE)  ;vom VRAM
  C281 13               INC     DE
  C282 18EF             JR      MODI3   ;eintragen
  C284           
  C284 FE3A     MODI5   CP      ':'     ;R}ckschritt?
  C286 2003             JR      NZ,MODI6
  C288 2B               DEC     HL
  C289 18C4             JR      MODI1
  C28B           
  C28B FE2F     MODI6   CP      '/'     ;neue Adresse?
  C28D 200B             JR      NZ,MODI0
  C28F 13               INC     DE
  C290 CD92F3           CALL    RHEX    ;Fehler
  C293 381E             JR      C,MODI9
  C295 2A97B7           LD      HL,(NUMVX)
  C298 18B5             JR      MODI1
  C29A           
  C29A FE27     MODI0   CP      27H     ;Zeichenkette?
  C29C 200E             JR      NZ,MODI8
  C29E 13               INC     DE
  C29F 1A       MODI7   LD      A,(DE)  ;Zeichen holen
  C2A0 13               INC     DE
  C2A1 A7               AND     A       ;Dummy-Ende?
  C2A2 28AB             JR      Z,MODI1
  C2A4 FE27             CP      27H     ;Ende Kette?
  C2A6 28CF             JR      Z,MODI4
  C2A8 77               LD      M,A     ;PROGRAMMFEHLER
  C2A9 23               INC     HL      ;(richtig LDMAE)
  C2AA 18F3             JR      MODI7
  C2AC           
  C2AC A7       MODI8   AND     A       ;Ende Zeile?
  C2AD 28A0             JR      Z,MODI1
  C2AF FE20             CP      ' '     ;Leerzeichen?
  C2B1 28B5             JR      Z,MODI2
  C2B3 CD61F3   MODI9   CALL    ERRM    ;sonst ERROR
  C2B6 1897             JR      MODI1
  C2B8           
  C2B8          V24OC   ;Initialisieren Druckerausgabe
  C2B8                  ;PE: Argumente
  C2B8 2181B7           LD      HL,ARGN
  C2BB 7E               LD      A,M
  C2BC FE03             CP      3
  C2BE 3024             JR      NC,V24O3;mehr als 2 Arg
  C2C0 0608             LD      B,8     ;ab Platz 8
  C2C2 CD94C3           CALL    V24SU   ;M003 suchen
  C2C5 3856             JR      C,V24O6 ;nicht gef.
  C2C7 21DBF9           LD      HL,HCPGM
  C2CA 2299B7           LD      (HCADR),HL;ShCLR belegen
  C2CD 11FFF9           LD      DE,PRINT
  C2D0 21E1B7           LD      HL,HCPZ
  C2D3 CB4E             BIT     1,M     ;UOUT1 oder 2?
  C2D5 2006             JR      NZ,V24O1
  C2D7 ED53BEB7         LD      (UOUT1+1),DE
  C2DB 1804             JR      V24O2
  C2DD           
  C2DD ED53C4B7 V24O1   LD      (UOUT2+1),DE
  C2E1 C372C3   V24O2   JP      V24O12
  C2E4           
  C2E4 23       V24O3   INC     HL
  C2E5 46               LD      B,M     ;ab Platz (ARG1)
  C2E6 CD94C3           CALL    V24SU   ;M003 suchen
  C2E9 3832             JR      C,V24O6 ;nicht gef.
  C2EB 23               INC     HL
  C2EC 23               INC     HL
  C2ED 7E               LD      A,M     ;SIO A oder B?
  C2EE 3D               DEC     A
  C2EF 200A             JR      NZ,V24O4
  C2F1 3AE1B7           LD      A,(HCPZ)
  C2F4 CB97             RES     2,A     ;merken SIO-A
  C2F6 32E1B7           LD      (HCPZ),A
  C2F9 180B             JR      V24O5
  C2FB           
  C2FB 3D       V24O4   DEC     A
  C2FC 201F             JR      NZ,V24O6;Fehler
  C2FE 3AE1B7           LD      A,(HCPZ)
  C301 CBD7             SET     2,A     ;merken SIO-B
  C303 32E1B7           LD      (HCPZ),A
  C306 23       V24O5   INC     HL
  C307 23               INC     HL
  C308 11FFF9           LD      DE,PRINT
  C30B 7E               LD      A,M     ;USER1 oder 2?
  C30C 3D               DEC     A
  C30D 3D               DEC     A
  C30E 200F             JR      NZ,V24O7
  C310 ED53BEB7         LD      (UOUT1+1),DE
  C314 11E1B7           LD      DE,HCPZ
  C317 1A               LD      A,(DE)
  C318 CB8F             RES     1,A     ;merken USER1
  C31A 12               LD      (DE),A
  C31B 1810             JR      V24O8
  C31D           
  C31D 1872     V24O6   JR      V24O13  ;Fehlersprung
  C31F           
  C31F 3D       V24O7   DEC     A
  C320 20FB             JR      NZ,V24O6;Fehler
  C322 ED53C4B7         LD      (UOUT2+1),DE
  C326 11E1B7           LD      DE,HCPZ
  C329 1A               LD      A,(DE)
  C32A CBCF             SET     1,A     ;merken USER2
  C32C 12               LD      (DE),A
  C32D 3A81B7   V24O8   LD      A,(ARGN)
  C330 FE04             CP      4
  C332 23               INC     HL
  C333 23               INC     HL
  C334 383C             JR      C,V24O12;weniger als 4
  C336 7E               LD      A,M
  C337 A7               AND     A       ;keine Fkt von
  C338 281C             JR      Z,V24O11;ShCLR?-ja
  C33A 3D               DEC     A       ;Protokoll?
  C33B 280A             JR      Z,V24O9
  C33D 3D               DEC     A       ;Hardcopy?
  C33E 2051             JR      NZ,V24O13
  C340 3AE1B7           LD      A,(HCPZ)
  C343 CBC7             SET     0,A     ;Hcopy merken
  C345 1805             JR      V24O10
  C347           
  C347 3AE1B7   V24O9   LD      A,(HCPZ)
  C34A CB87             RES     0,A     ;Hcopy aus
  C34C 32E1B7   V24O10  LD      (HCPZ),A
  C34F 11DBF9           LD      DE,HCPGM
  C352 ED5399B7         LD      (HCADR),DE
  C356 3A81B7   V24O11  LD      A,(ARGN)
  C359 FE05             CP      5       ;5 Agrumente?
  C35B 2015             JR      NZ,V24O12
  C35D 23               INC     HL
  C35E 23               INC     HL
  C35F 7E               LD      A,M     ;Druckertyp
  C360 CB27             SLA     A
  C362 CB27             SLA     A
  C364 CB27             SLA     A       ;nach obere
  C366 CB27             SLA     A       ;Tetrade
  C368 4F               LD      C,A
  C369 3AE1B7           LD      A,(HCPZ)
  C36C E60F             AND     0FH     ;in (HCPZ)
  C36E B1               OR      C       ;einbauen
  C36F 32E1B7           LD      (HCPZ),A
  C372 CDCDFA   V24O12  CALL    LCSO
  C375 3E0C             LD      A,0CH   ;CTC-Adresse
  C377 C5               PUSH    BC
  C378 81               ADD     C
  C379 0602             LD      B,2     ;2 Steuerbytes
  C37B 4F               LD      C,A
  C37C 2AE2B7           LD      HL,(INTV1)
  C37F F3               DI
  C380 EDB3             OTIR            ;CTC init.
  C382 C1               POP     BC
  C383 3AE4B7           LD      A,(INTV1L)
  C386 3D               DEC     A
  C387 3D               DEC     A       ;-2
  C388 47               LD      B,A
  C389 3E0A             LD      A,0AH   ;SIO-Adresse
  C38B 81               ADD     C
  C38C 4F               LD      C,A
  C38D EDB3             OTIR            ;SIO init.
  C38F FB               EI
  C390 C9               RET
  C391           
  C391 C361F3   V24O13  JP      ERRM
  C394           
  C394          V24SU   ;Suche 1. V24-Modul
  C394 163C             LD      D,3CH
  C396 0E80             LD      C,80H
  C398 ED78     V24S1   IN      A,(C)
  C39A FEEE             CP      0EEH
  C39C 2809             JR      Z,V24S2
  C39E 04               INC     B       ;bl|dsinniger-
  C39F 04               INC     B       ;weise in
  C3A0 04               INC     B       ;Viererschritten
  C3A1 04               INC     B
  C3A2 15               DEC     D
  C3A3 20F3             JR      NZ,V24S1
  C3A5 37               SCF             ;nicht gef.
  C3A6 C9               RET
  C3A7           
  C3A7 E5       V24S2   PUSH    HL
  C3A8 68               LD      L,B
  C3A9 3E02             LD      A,2
  C3AB 1601             LD      D,1
  C3AD CD0BE4           CALL    MODU    ;einschalten
  C3B0 E1               POP     HL
  C3B1 C9               RET
  C3B2           
  C3B2          HCPGMC  ;ShCLR-Reaktionsprogramm
  C3B2 E5               PUSH    HL
  C3B3 D5               PUSH    DE
  C3B4 3AE1B7           LD      A,(HCPZ)
  C3B7 CB47             BIT     0,A     ;Hardcopy?
  C3B9 C208C4           JP      NZ,COPIES
  C3BC           
  C3BC                  ;Protokollfunktion
  C3BC 2AB9B7           LD      HL,(OUTAB)
  C3BF AF               XOR     A
  C3C0 B6               OR      M       ;Null?
  C3C1 201A             JR      NZ,PROTO2
  C3C3 11FCF9           LD      DE,ECHO
  C3C6 3D               DEC     A       ;PROGRAMMFEHLER
  C3C7 3D               DEC     A       ;(A ist Null!)
  C3C8 2805             JR      Z,PROTO1
  C3CA 2195FB           LD      HL,ZEI3 ;nein
  C3CD 1818             JR      PROTO3
  C3CF           
  C3CF 2194FB   PROTO1  LD      HL,ZEI2
  C3D2 1819             JR      PROTO4
  C3D4           
  C3D4 ED53BEB7         LD      (UOUT1+1),DE;RUDIMENT
  C3D8 2194FB           LD      HL,ZEI2     ;RUDIMENT
  C3DB 1814             JR      PROTO5      ;RUDIMENT
  C3DD           
  C3DD          PROTO2  ;Protokoll deaktivieren
  C3DD 3D               DEC     A
  C3DE 11FFF9           LD      DE,PRINT
  C3E1 2192FB           LD      HL,ZEI0
  C3E4 3D               DEC     A
  C3E5 2806             JR      Z,PROTO4
  C3E7 ED53C4B7 PROTO3  LD      (UOUT2+1),DE;r}cksetzen
  C3EB 1804             JR      PROTO5
  C3ED           
  C3ED ED53BEB7 PROTO4  LD      (UOUT1+1),DE;r}cksetzen
  C3F1 22B9B7   PROTO5  LD      (OUTAB),HL
  C3F4 D1               POP     DE
  C3F5 E1               POP     HL
  C3F6 C9               RET
  C3F7           
  C3F7                  ;undefinierbarer Rest
  C3F7 1B5B             DEFW    5B1BH
  C3F9 3131             DEFW    3131H
  C3FB 615B             DEFW    5B61H
  C3FD 3031             DEFW    3130H
  C3FF 651B             DEFW    1B65H
  C401 5B37             DEFW    375BH
  C403 3871             DEFW    7138H
  C405 4B40             DEFW    404BH
  C407 02               DEFB    2
  C408           
  C408          COPIES  ;Hard- oder Screencopy
  C408 C5               PUSH    BC
  C409 F5               PUSH    AF
  C40A 3AE1B7           LD      A,(HCPZ)
  C40D CB7F             BIT     7,A
  C40F 282A             JR      Z,HCOPYC
  C411           
  C411                  ;Screencopy
  C411 2ACBB7           LD      HL,(VRAM)
  C414 0620             LD      B,20H
  C416 0E28     SCOPY1  LD      C,28H
  C418 7E       SCOPY2  LD      A,M
  C419 23               INC     HL
  C41A A7               AND     A
  C41B 2002             JR      NZ,SCOPY3
  C41D 3E20             LD      A,20H   ;doppelt
  C41F FE20     SCOPY3  CP      20H
  C421 3002             JR      NC,SCOPY4
  C423 3E20             LD      A,20H   ;gemoppelt
  C425 CDFFF9   SCOPY4  CALL    PRINT
  C428 0D               DEC     C       ;40 Spalten
  C429 20ED             JR      NZ,SCOPY2
  C42B 3E0D             LD      A,0DH   ;CR
  C42D CD31FA           CALL    PRINT4
  C430 3E0A             LD      A,0AH   ;LF
  C432 CD31FA           CALL    PRINT4
  C435 10DF             DJNZ    SCOPY1  ;32 Zeilen
  C437 F1       HCEND   POP     AF
  C438 C334F2           JP      POP3
  C43B           
  C43B          HCOPYC  ;..nach Typ verzweigen
  C43B E6F0             AND     0F0H
  C43D 2815             JR      Z,K6313 ;EPSON-komp.
  C43F FE10             CP      10H
  C441 285C             JR      Z,K6314 ;dto. breit
  C443 FE20             CP      20H
  C445 CA15C5           JP      Z,K6311 ;mit ANSI
  C448 FE30             CP      30H
  C44A CA9AC5           JP      Z,K6312 ;dto breit
  C44D FE50             CP      50H
  C44F CADDC5           JP      Z,K6304 ;Thermodrucker
  C452 18E3             JR      HCEND
  C454                  ;K6303 u.a.: keine Hardcopy!
  C454           
  C454 2600     K6313   LD      H,0
  C456 E5       K6313a  PUSH    HL
  C457 21F9C5           LD      HL,C6313;Tabelle
  C45A 060A             LD      B,10    ;L{nge
  C45C CDF1C5           CALL    PRZK
  C45F E1               POP     HL
  C460 CD72C4           CALL    HCLINE  ;1 Zeile
  C463 30F1             JR      NC,K6313a ;n{chste
  C465 3E0D     HCCRLF  LD      A,0DH
  C467 CD4AFA           CALL    OFILT
  C46A 3E0A             LD      A,0AH
  C46C CD4AFA           CALL    OFILT   ;CRLF
  C46F C337C4           JP      HCEND
  C472           
  C472 2E00     HCLINE  LD      L,0
  C474 010800   HCLIN1  LD      BC,8
  C477 E5               PUSH    HL
  C478 1100B7           LD      DE,0B700H ;Kassetten-
  C47B CD4EE0           CALL    PADR      ;puffer
  C47E EDB0             LDIR
  C480 0E08             LD      C,8
  C482 0608     HCLIN2  LD      B,8
  C484 2100B7           LD      HL,0B700H
  C487 CB16     HCLIN3  RL      M
  C489 17               RLA
  C48A 23               INC     HL
  C48B 10FA             DJNZ    HCLIN3
  C48D CD4AFA           CALL    OFILT
  C490 0D               DEC     C
  C491 20EF             JR      NZ,HCLIN2 ;n{chster
  C493 E1               POP     HL        ;Cursor
  C494 2C               INC     L
  C495 3E28             LD      A,28H
  C497 BD               CP      L
  C498 20DA             JR      NZ,HCLIN1
  C49A 7C               LD      A,H
  C49B C608             ADD     8       ;n{chste Zeile
  C49D 67               LD      H,A
  C49E C9               RET
  C49F           
  C49F 2600     K6314   LD      H,0
  C4A1 E5       K6314a  PUSH    HL
  C4A2 2103C6           LD      HL,C6314;Tabelle
  C4A5 060A             LD      B,10    ;L{nge
  C4A7 CDF1C5           CALL    PRZK
  C4AA E1               POP     HL
  C4AB 2E00             LD      L,0
  C4AD 0604     K6314b  LD      B,4
  C4AF E5               PUSH    HL
  C4B0 1100B7           LD      DE,0B700H
  C4B3 CD4EE0           CALL    PADR
  C4B6 7E       K6314c  LD      A,M
  C4B7 12               LD      (DE),A
  C4B8 13               INC     DE
  C4B9 12               LD      (DE),A
  C4BA 13               INC     DE
  C4BB 2C               INC     L
  C4BC 10F8             DJNZ    K6314c  ;4*
  C4BE 0E08             LD      C,8
  C4C0 0608     K6314d  LD      B,8
  C4C2 2100B7           LD      HL,0B700H
  C4C5 CB16     K6314e  RL      M
  C4C7 17               RLA
  C4C8 23               INC     HL
  C4C9 10FA             DJNZ    K6314e
  C4CB CD4AFA           CALL    OFILT
  C4CE CD4AFA           CALL    OFILT
  C4D1 0D               DEC     C
  C4D2 20EC             JR      NZ,K6314d
  C4D4 E1               POP     HL
  C4D5 2C               INC     L
  C4D6 3E28             LD      A,28H
  C4D8 BD               CP      L
  C4D9 20D2             JR      NZ,K6314b
  C4DB 7C               LD      A,H
  C4DC C604             ADD     4
  C4DE 67               LD      H,A
  C4DF 30C0             JR      NC,K6314a
  C4E1 1882             JR      HCCRLF
  C4E3           
  C4E3          CBYTES  ;C Bytes nach Kass-Puffer
  C4E3 E5               PUSH    HL
  C4E4 C5               PUSH    BC
  C4E5 CD4EE0           CALL    PADR
  C4E8 0600             LD      B,0
  C4EA EDB0             LDIR
  C4EC C1               POP     BC
  C4ED E1               POP     HL
  C4EE C9               RET
  C4EF           
  C4EF          SPRS    ;8* 6-Nadel-Sprosse ausgeben
  C4EF E5               PUSH    HL
  C4F0 C5               PUSH    BC
  C4F1 2E80             LD      L,80H
  C4F3 0600     SPRS1   LD      B,0
  C4F5 1100B7           LD      DE,0B700H
  C4F8 1A       SPRS2   LD      A,(DE)
  C4F9 A5               AND     L       ;CY=0
  C4FA 2801             JR      Z,SPRS3
  C4FC 37               SCF             ;CY=1
  C4FD CB10     SPRS3   RL      B
  C4FF 13               INC     DE
  C500 3E06             LD      A,6     ;f}r 6 Bit
  C502 BB               CP      E
  C503 20F3             JR      NZ,SPRS2;oberste Nadel
  C505                                  ;war 0
  C505 A7               AND     A       ;unterste Nadel
  C506 CB10             RL      B       ;=0
  C508 78               LD      A,B
  C509 CD4AFA           CALL    OFILT
  C50C CB0D             RRC     L
  C50E CB7D             BIT     7,L
  C510 28E1             JR      Z,SPRS1
  C512 C1               POP     BC
  C513 E1               POP     HL
  C514 C9               RET
  C515           
  C515 210DC6   K6311   LD      HL,C6311;Tabelle
  C518 060E             LD      B,14    ;L{nge
  C51A CDF1C5           CALL    PRZK
  C51D 062A             LD      B,42    ;256/6=42 Rest4
  C51F 210000           LD      HL,0
  C522 1100B7   K6311a  LD      DE,0B700H
  C525 0E06             LD      C,6
  C527 CDE3C4           CALL    CBYTES
  C52A CDEFC4           CALL    SPRS    ;1 6*8-Feld
  C52D 2C               INC     L       ;ausgeben
  C52E 3E28             LD      A,28H
  C530 BD               CP      L
  C531 20EF             JR      NZ,K6311a
  C533 3E06             LD      A,6     ;6 Pixel tiefer
  C535 84               ADD     H
  C536 67               LD      H,A
  C537 2E00             LD      L,0
  C539 E5               PUSH    HL
  C53A C5               PUSH    BC
  C53B 210DC6           LD      HL,C6311;wieder Tabelle
  C53E 060E             LD      B,14    ;L{nge
  C540 CDF1C5           CALL    PRZK
  C543 C1               POP     BC
  C544 E1               POP     HL
  C545 05               DEC     B
  C546 20DA             JR      NZ,K6311a
  C548 1100B7   K6311b  LD      DE,0B700H
  C54B 0E04             LD      C,4     ;den Rest4
  C54D CDE3C4           CALL    CBYTES
  C550 AF               XOR     A
  C551 12               LD      (DE),A  ;letzten beiden
  C552 13               INC     DE      ;Zeilen leeren
  C553 12               LD      (DE),A
  C554 CDEFC4           CALL    SPRS
  C557 2C               INC     L
  C558 3E28             LD      A,28H
  C55A BD               CP      L
  C55B 20EB             JR      NZ,K6311b
  C55D C365C4           JP      HCCRLF
  C560           
  C560          CBYTS2  ;C Bytes nach Puffer & doppeln
  C560 E5               PUSH    HL
  C561 1100B7           LD      DE,0B700H
  C564 CD4EE0           CALL    PADR
  C567 7E       C2BYTL  LD      A,M
  C568 12               LD      (DE),A
  C569 13               INC     DE
  C56A 12               LD      (DE),A  ;doppelt
  C56B 13               INC     DE
  C56C 2C               INC     L
  C56D 0D               DEC     C
  C56E 20F7             JR      NZ,C2BYTL
  C570 E1               POP     HL
  C571 C9               RET
  C572           
  C572          S2PRS   ;8* 6-Nadel-Sprosse doppelt out
  C572 E5               PUSH    HL
  C573 C5               PUSH    BC
  C574 2E80             LD      L,80H
  C576 0600     S2PR1   LD      B,0
  C578 1100B7           LD      DE,0B700H
  C57B 1A       S2PR2   LD      A,(DE)
  C57C A5               AND     L
  C57D 2801             JR      Z,S2PR3
  C57F 37               SCF
  C580 CB10     S2PR3   RL      B
  C582 13               INC     DE
  C583 3E06             LD      A,6
  C585 BB               CP      E
  C586 20F3             JR      NZ,S2PR2
  C588 78               LD      A,B
  C589 17               RLA             ;untere beiden
  C58A 17               RLA             ;Nadeln tot
  C58B CD4AFA           CALL    OFILT
  C58E CD4AFA           CALL    OFILT
  C591 CB0D             RRC     L
  C593 CB7D             BIT     7,L
  C595 28DF             JR      Z,S2PR1
  C597 C1               POP     BC
  C598 E1               POP     HL
  C599 C9               RET
  C59A           
  C59A 211BC6   K6312   LD      HL,C6312;Tabelle
  C59D 060E             LD      B,14    ;L{nge
  C59F CDF1C5           CALL    PRZK
  C5A2 0655             LD      B,85    ;256/3=85 Rest1
  C5A4 210000           LD      HL,0
  C5A7 0E03     K6312a  LD      C,3
  C5A9 CD60C5           CALL    CBYTS2
  C5AC CD72C5           CALL    S2PRS
  C5AF 2C               INC     L
  C5B0 3E28             LD      A,28H
  C5B2 BD               CP      L
  C5B3 20F2             JR      NZ,K6312a
  C5B5 24               INC     H
  C5B6 24               INC     H
  C5B7 24               INC     H
  C5B8 2E00             LD      L,0
  C5BA E5               PUSH    HL
  C5BB C5               PUSH    BC
  C5BC 211BC6           LD      HL,C6312
  C5BF 060E             LD      B,14
  C5C1 CDF1C5           CALL    PRZK
  C5C4 C1               POP     BC
  C5C5 E1               POP     HL
  C5C6 10DF             DJNZ    K6312a
  C5C8 0E01     K6312b  LD      C,1     ;Rest1
  C5CA CD60C5           CALL    CBYTS2
  C5CD AF               XOR     A
  C5CE 12               LD      (DE),A
  C5CF 13               INC     DE
  C5D0 12               LD      (DE),A
  C5D1 CD72C5           CALL    S2PRS
  C5D4 2C               INC     L
  C5D5 3E28             LD      A,28H
  C5D7 BD               CP      L
  C5D8 20EE             JR      NZ,K6312b
  C5DA C365C4           JP      HCCRLF
  C5DD           
  C5DD 2600     K6304   LD      H,0
  C5DF E5       K6304a  PUSH    HL
  C5E0 2129C6           LD      HL,C6304;Tabelle
  C5E3 0609             LD      B,9     ;L{nge
  C5E5 CDF1C5           CALL    PRZK
  C5E8 E1               POP     HL
  C5E9 CD72C4           CALL    HCLINE  ;1 Zeile
  C5EC 30F1             JR      NC,K6304a
  C5EE C365C4           JP      HCCRLF
  C5F1           
  C5F1          PRZK    ;Durch (HL) adressierte
  C5F1                  ;Zeichenkette ausgeben, B lang
  C5F1 7E               LD      A,M
  C5F2 23               INC     HL
  C5F3 CD4AFA           CALL    OFILT
  C5F6 10F9             DJNZ    PRZK
  C5F8 C9               RET
  C5F9           
  C5F9                  ;Grafiksequenzen f}r
  C5F9                  ;die Druckertypen
  C5F9 0D09     C6313   DEFW    90DH
  C5FB 1B               DEFB    1BH
  C5FC 4A               DEFB    'J'
  C5FD 18               DEFB    24
  C5FE 1B               DEFB    1BH
  C5FF 2A               DEFB    '*'
  C600 05               DEFB    5
  C601 4001             DEFW    140H
  C603           
  C603 0D09     C6314   DEFW    90DH
  C605 1B               DEFB    1BH
  C606 4A               DEFB    'J'
  C607 18               DEFB    24
  C608 1B               DEFB    1BH
  C609 2A               DEFB    '*'
  C60A 05               DEFB    5
  C60B 8002             DEFW    280H
  C60D           
  C60D 1B       C6311   DEFB    1BH
  C60E 5B303165         DEFM    '[01e'
  C612 1B               DEFB    1BH
  C613 5B313260         DEFM    '[12`'
  C617 1B               DEFB    1BH
  C618 4B               DEFB    'K'
  C619 4001             DEFW    140H
  C61B           
  C61B 1B       C6312   DEFB    1BH
  C61C 5B303165         DEFM    '[01e'
  C620 1B               DEFB    1BH
  C621 5B313260         DEFM    '[12`'
  C625 1B               DEFB    1BH
  C626 4B               DEFB    'K'
  C627 8002             DEFW    280H
  C629           
  C629 0D09     C6304   DEFW    90DH
  C62B 1B               DEFB    1BH
  C62C 4A               DEFB    'J'
  C62D 18               DEFB    24
  C62E 1B               DEFB    1BH
  C62F 4B               DEFB    'K'
  C630 4001             DEFW    140H
  C632           
  C632          V24DC   ;Duplex-Init
  C632                  ;PE: Argumente
  C632 3A81B7           LD      A,(ARGN)
  C635 FE03             CP      3
  C637 3829             JR      C,V24DC2;<3 Argumente
  C639 2182B7           LD      HL,ARG1
  C63C 46               LD      B,M     ;ab Platz B
  C63D CD94C3           CALL    V24SU   ;M003 suchen
  C640 DA61F3           JP      C,ERRM
  C643 23               INC     HL
  C644 23               INC     HL
  C645 3AE8B7           LD      A,(HCPZ2)
  C648 CB46             BIT     0,M     ;SIO A oder B?
  C64A CB97             RES     2,A
  C64C 2002             JR      NZ,V24DC1
  C64E CBD7             SET     2,A
  C650 47       V24DC1  LD      B,A
  C651 23               INC     HL
  C652 23               INC     HL
  C653 7E               LD      A,M
  C654 D602             SUB     2       ;USER1 oder 2?
  C656 CB88             RES     1,B
  C658 2802             JR      Z,V24DC6
  C65A CBC8             SET     1,B
  C65C 78       V24DC6  LD      A,B
  C65D 32E8B7           LD      (HCPZ2),A
  C660 1806             JR      V24DC3
  C662           
  C662 CD94C3   V24DC2  CALL    V24SU   ;PROGRAMMFEHLER
  C665 DA61F3           JP      C,ERRM  ;(B nicht def.)
  C668 1171FA   V24DC3  LD      DE,V24OT
  C66B 0191FA           LD      BC,V24IN
  C66E 3AE8B7           LD      A,(HCPZ2)
  C671 CB4F             BIT     1,A
  C673 200A             JR      NZ,V24DC4
  C675 ED53BEB7         LD      (UOUT1+1),DE
  C679 ED43C1B7         LD      (UIN1+1),BC
  C67D 1808             JR      V24DC5
  C67F           
  C67F ED53C4B7 V24DC4  LD      (UOUT2+1),DE
  C683 ED43C7B7         LD      (UIN2+1),BC
  C687 CDDAFA   V24DC5  CALL    LCSOD
  C68A 3E0C             LD      A,0CH
  C68C C5               PUSH    BC
  C68D 81               ADD     C
  C68E 0602             LD      B,2     ;2 Initbytes
  C690 4F               LD      C,A
  C691 2AE5B7           LD      HL,(INTV2)
  C694 F3               DI
  C695 EDB3             OTIR            ;CTC init.
  C697 C1               POP     BC
  C698 3AE7B7           LD      A,(INTV2L)
  C69B 3D               DEC     A
  C69C 3D               DEC     A
  C69D 47               LD      B,A
  C69E 3E0A             LD      A,0AH
  C6A0 81               ADD     C
  C6A1 4F               LD      C,A
  C6A2 EDB3     UMINI1  OTIR            ;SIO init.
  C6A4 FB               EI
  C6A5 C9               RET
  C6A6           
  C6A6          UMINIT  ;SIO B auf Interruptbetrieb
  C6A6 2171FA           LD      HL,V24OT
  C6A9 22C4B7           LD      (UOUT2+1),HL
  C6AC 0E0D             LD      C,0DH
  C6AE 21E4C6           LD      HL,V24UMT
  C6B1 0602             LD      B,2     ;2 Initbytes
  C6B3 F3               DI
  C6B4 EDB3             OTIR            ;CTC init.
  C6B6 060B             LD      B,11    ;11 Initbytes
  C6B8 0E0B             LD      C,0BH
  C6BA 18E6             JR      UMINI1
  C6BC           
  C6BC          V24INI  ;-Tabellen in RAM kopieren
  C6BC                  ;-M003 suchen und einschalten
  C6BC                  ;-M003-Status in A800h merken
  C6BC 21D3C6           LD      HL,V24TAB
  C6BF 1101A8           LD      DE,0A801H
  C6C2 011100           LD      BC,17
  C6C5 EDB0             LDIR
  C6C7 0608             LD      B,8
  C6C9 CD94C3           CALL    V24SU
  C6CC D8               RET     C
  C6CD 3E01             LD      A,1
  C6CF 3200A8           LD      (0A800H),A
  C6D2 C9               RET
  C6D3           
  C6D3 47       V24TAB  DEFB    47H     ;f}r IRM
  C6D4 5B               DEFB    5BH
  C6D5 0404             DEFW    0404H
  C6D7 0320             DEFW    2003H
  C6D9 056A             DEFW    6A05H
  C6DB           
  C6DB 47               DEFB    47H
  C6DC 2E               DEFB    2EH
  C6DD 18               DEFB    18H
  C6DE 0444             DEFW    4404H
  C6E0 03E1             DEFW    0E103H
  C6E2 056A             DEFW    6A05H
  C6E4           
  C6E4 47       V24UMT  DEFB    47H     ;f}r Interrupt-
  C6E5 2E               DEFB    2EH     ;betrieb
  C6E6 18               DEFB    18H
  C6E7 02E2             DEFW    0E202H
  C6E9 1444             DEFW    4414H
  C6EB 03E1             DEFW    0E103H
  C6ED 05EA             DEFW    0EA05H
  C6EF 1118             DEFW    1811H
  C6F1           
  C6F1          GARGC   ;bis 10 Argumente erfassen
  C6F1 0181B7           LD      BC,ARGN
  C6F4 AF               XOR     A
  C6F5 02               LD      (BC),A  ;(ARGN)=0
  C6F6 CD92F3   GARG1   CALL    RHEX
  C6F9 D8               RET     C       ;Fehler
  C6FA 7E               LD      A,M
  C6FB B7               OR      A       ;Ende-Dummy?
  C6FC C8               RET     Z
  C6FD 23               INC     HL
  C6FE 03               INC     BC
  C6FF 7E               LD      A,M     ;L(NUMVX)
  C700 02               LD      (BC),A  ;L(Argument)
  C701 23               INC     HL
  C702 03               INC     BC
  C703 7E               LD      A,M     ;H(NUMVX)
  C704 02               LD      (BC),A  ;H(Argument)
  C705 2E81             LD      L,81H   ;HL=ARGN
  C707 34               INC     M
  C708 7E               LD      A,M
  C709 C6F5             ADD     0F5H    ;10 Argumente?
  C70B 30E9             JR      NC,GARG1
  C70D C9               RET             ;Fehler: zu viel
  C70E           
  C70E          MBOC    ;Byteweise MB-Ausgabe
  C70E 5A               LD      E,D     ;Steuerbyte
  C70F 57               LD      D,A     ;Datenbyte
  C710 D5               PUSH    DE
  C711 C5               PUSH    BC
  C712 CB5B             BIT     3,E
  C714 2829             JR      Z,NOINIT
  C716 DDCB07CE         SET     1,(IX+7);merken INIT
  C71A 3ED5             LD      A,'U'+80H
  C71C BE               CP      M
  C71D D5               PUSH    DE
  C71E 11A000           LD      DE,0A0H ;Vortonl{nge
  C721 2003             JR      NZ,NOHIU;bei 'U'
  C723 110005           LD      DE,500H ;gedehnt
  C726 ED53D8B7 NOHIU   LD      (VORTN),DE
  C72A 1100B7           LD      DE,0B700H
  C72D 010B00           LD      BC,11
  C730 EDB0             LDIR            ;Name
  C732 EB               EX      DE,HL
  C733 D1               POP     DE
  C734 72               LD      M,D     ;1. Byte
  C735 3E74             LD      A,74H   ;noch-Bytes
  C737 CD7FC8   MBOC1   CALL    SETDT
  C73A C1       MBOCE   POP     BC
  C73B D1               POP     DE
  C73C CB9B             RES     3,E
  C73E C9               RET
  C73F           
  C73F 2ADAB7   NOINIT  LD      HL,(DTADR)
  C742 72               LD      M,D     ;Byte eintragen
  C743 CB73             BIT     6,E
  C745 2023             JR      NZ,MCLOS
  C747 3ADCB7           LD      A,(DTNR);kein Close
  C74A 3D               DEC     A
  C74B 20EA             JR      NZ,MBOC1
  C74D DDCB074E         BIT     1,(IX+7);schon Init?
  C751 2008             JR      NZ,MBOC2;nein
  C753 CD64C8           CALL    MBLO    ;ja-Blockout
  C756 CD78C8           CALL    SETDTN
  C759 18DF             JR      MBOCE
  C75B           
  C75B DDCB078E MBOC2   RES     1,(IX+7);r}cksetzen
  C75F CD20E5           CALL    ISRO    ;Init.
  C762 CD6BC8           CALL    MBLNR   ;Blocknr.
  C765 CD78C8           CALL    SETDTN
  C768 18D0     JMBOCE  JR      MBOCE
  C76A           
  C76A DDCB074E MCLOS   BIT     1,(IX+7);Init?
  C76E 2805             JR      Z,MBOC3
  C770 CD20E5           CALL    ISRO    ;nein?-aber
  C773                                  ;jetzt!
  C773 1803             JR      MBOC4
  C775           
  C775 CD64C8   MBOC3   CALL    MBLO    ;Blockout m.Nr.
  C778 ED4BD8B7 MBOC4   LD      BC,(VORTN)
  C77C CDDFE4           CALL    CSRO
  C77F D5       MBOC5   PUSH    DE
  C780 CDD8F5           CALL    CSRI
  C783 DDCB07AE         RES     5,(IX+7);'U'-Merker
  C787 D1               POP     DE      ;r}cksetzen
  C788 18DE             JR      JMBOCE
  C78A           
  C78A          MBIC    ;Byteweise MB-Eingabe
  C78A 5A               LD      E,D     ;Steuerbyte
  C78B 57               LD      D,A     ;Datenbyte (PA!)
  C78C D5               PUSH    DE
  C78D C5               PUSH    BC
  C78E CB73             BIT     6,E     ;Close?
  C790 20ED             JR      NZ,MBOC5
  C792 CB5B             BIT     3,E     ;Init?
  C794 2866             JR      Z,MBIU
  C796 3E01             LD      A,1     ;erwarteter Blk
  C798 DD7703           LD      (IX+3),A
  C79B E5               PUSH    HL
  C79C CD92E5           CALL    ISRI
  C79F E1       MBIC1   POP     HL
  C7A0 DA54C8           JP      C,JBLERR;Fehler: '*'
  C7A3 DD7E02           LD      A,(IX+2);gelesener Blk.
  C7A6 FE01             CP      1
  C7A8 C254C8           JP      NZ,JBLERR;Fehler: '*'
  C7AB DDCB07BE         RES     7,(IX+7);Kopfblk geladen
  C7AF DD3403           INC     (IX+3)  ;erw. Blk
  C7B2 1100B7           LD      DE,0B700H
  C7B5 060B             LD      B,11
  C7B7 1A               LD      A,(DE)
  C7B8 FED5             CP      'U'+80H ;hohes 'U'?
  C7BA 2004             JR      NZ,MBIC2
  C7BC DDCB07EE         SET     5,(IX+7);merken zur
  C7C0           ;Blocknummernausgabeunterdr}ckung
  C7C0 C629     MBIC2   ADD     29H     ;gesch}tzte
  C7C2 3012             JR      NC,MBIC3;BASIC-
  C7C4 1A               LD      A,(DE)  ;Programme
  C7C5 D604             SUB     4       ;als un-
  C7C7 12               LD      (DE),A  ;gesch}tzt
  C7C8 13               INC     DE      ;anzeigen
  C7C9 12               LD      (DE),A  ;und Setzen
  C7CA 13               INC     DE      ;LIST/EDIT/BYE-
  C7CB 12               LD      (DE),A  ;Schutz
  C7CC 325E03           LD      (35EH),A;<=hier
  C7CF DDCB07F6         SET     6,(IX+7);merken
  C7D3 1100B7           LD      DE,0B700H ;(f}r wen?)
  C7D6 1A       MBIC3   LD      A,(DE)  ;Name
  C7D7 CD29E3           CALL    CRT     ;anzeigen
  C7DA BE               CP      M       ;stimmt Name?
  C7DB 2804             JR      Z,MBIC4
  C7DD DDCB07FE         SET     7,(IX+7);nein-merken
  C7E1 23       MBIC4   INC     HL
  C7E2 13               INC     DE
  C7E3 10F1             DJNZ    MBIC3
  C7E5 CD6BF3           CALL    CRLF
  C7E8 DDCB077E         BIT     7,(IX+7)
  C7EC C274F8           JP      NZ,JIOERR ;zu ?IO ERROR
  C7EF EB               EX      DE,HL
  C7F0 3E75             LD      A,75H   ;noch-Bytes
  C7F2 56       MBIC5   LD      D,M     ;auslesen
  C7F3 CD7FC8           CALL    SETDT
  C7F6 7A               LD      A,D
  C7F7 C1               POP     BC
  C7F8 D1               POP     DE
  C7F9 CB9B             RES     3,E
  C7FB C9               RET
  C7FC           
  C7FC 2ADAB7   MBIU    LD      HL,(DTADR)
  C7FF 3ADCB7           LD      A,(DTNR)
  C802 3D               DEC     A
  C803 20ED             JR      NZ,MBIC5
  C805 CD95E5   MBIU1   CALL    MBI
  C808 3839             JR      C,MERR
  C80A CD78C8           CALL    SETDTN
  C80D 3EFF             LD      A,0FFH
  C80F DDBE02           CP      (IX+2)  ;Block FF?
  C812 2817             JR      Z,MBIU3
  C814 DD7E03           LD      A,(IX+3)
  C817 DDBE02           CP      (IX+2)  ;erwarteter?
  C81A 280F             JR      Z,MBIU3
  C81C 3E2A             LD      A,'*'
  C81E CD29E3           CALL    CRT     ;nicht
  C821 CDE8F1           CALL    BNROST  ;erwartet
  C824 19               DEFB    19H
  C825 00               NOP
  C826 CD71C8   MBIU2   CALL    JBRKT
  C829 18DA             JR      MBIU1
  C82B           
  C82B DD3403   MBIU3   INC     (IX+3)  ;n{chsten
  C82E 3D               DEC     A       ;erwarten
  C82F DDCB076E         BIT     5,(IX+7)
  C833 2006             JR      NZ,MBIU4
  C835 CDE8F1           CALL    BNROST  ;Blocknummer
  C838 3E               DEFB    '>'     ;unter Vorbe-
  C839 19               DEFB    19H     ;halt ausgeben
  C83A 00               NOP
  C83B CD78C8   MBIU4   CALL    SETDTN
  C83E 2ADAB7           LD      HL,(DTADR)
  C841 18AF             JR      MBIC5
  C843           
  C843 CDEEF1   MERR    CALL    OSTR    ;Fehler im
  C846 0909             DEFW    909H    ;Block
  C848 0909             DEFW    909H
  C84A 3F               DEFB    '?'
  C84B 00               NOP
  C84C CDE8F1           CALL    BNROST
  C84F 0D0A             DEFW    0A0DH
  C851 00               NOP
  C852 18D2             JR      MBIU2
  C854           
  C854 CDEEF1   JBLERR  CALL    OSTR    ;nicht erwarte-
  C857 2A               DEFM    '*'     ;ter 1. Block
  C858 08               DEFB    8
  C859 00               NOP
  C85A CD71C8           CALL    JBRKT
  C85D CD95E5           CALL    MBI
  C860 E5               PUSH    HL
  C861 C39FC7           JP      MBIC1
  C864           
  C864 ED4BD8B7 MBLO    LD      BC,(VORTN)
  C868 CD2FE5           CALL    MBO
  C86B CDE8F1   MBLNR   CALL    BNROST
  C86E 3E               DEFB    '>'
  C86F 19               DEFB    19H
  C870 00               NOP
  C871 CDE5E3   JBRKT   CALL    BRKT    ;bei BRK
  C874 D0               RET     NC      ;auch ?IO ERROR
  C875 C374F8           JP      JIOERR
  C878           
  C878          SETDTN  ;Setzen Datenzeiger neu
  C878 3E80             LD      A,80H
  C87A 2100B7           LD      HL,0B700H
  C87D 1801             JR      SETD1
  C87F           
  C87F          SETDT   ;N{chsten Datenzeiger setzen
  C87F 23               INC     HL
  C880 22DAB7   SETD1   LD      (DTADR),HL
  C883 32DCB7           LD      (DTNR),A
  C886 C9               RET
  C887           
  C887                  END

Errors: 0
End+1 : C887 
                
  0200           
  0200          ;**     CAOS 4.1 ROM E          **
  0200           
  0200                  ORG     0E000H
  E000           
  E000 C3F6F0           JP      BYE     ;Tasten-RESET
  E003 C35CE6           JP      BEXP1   ;BASIC-
  E006 C370E6           JP      BEXP2   ;Expansionen
  E009 C3E8E6           JP      BEXP3
  E00C 10FD             DW      TOKTAB
  E00E C3A0F8   BASIO   JP      BASPV
  E011           
  E011 7F7F             DW      7F7FH
  E013 42415349         DB      'BASIC',0
  E019 CD2FE0           CALL    BASON
  E01C C30DC0           JP      0C00DH
  E01F           
  E01F 7F7F             DW      7F7FH
  E021 52454241         DB      'REBASIC',0
  E029 CD2FE0           CALL    BASON
  E02C C38CC0           JP      0C08CH
  E02F           
  E02F          BASON   ;BASIC-ROM auftauen
  E02F DB88             IN      A,88H
  E031 F680             OR      80H
  E033 D388             OUT     88H
  E035 DD7E04           LD      A,(IX+4)
  E038 F660             OR      60H     ;Segment 3
  E03A D386             OUT     86H
  E03C DD7704           LD      (IX+4),A
  E03F C9               RET
  E040           
  E040          ;CRT-Treiber
  E040           
  E040 ED5BA0B7 PADR0   LD      DE,(CURSO)
  E044 2A9CB7   PADR1   LD      HL,(WINON)
  E047 19               ADD     HL,DE
  E048 CB24             SLA     H
  E04A CB24             SLA     H
  E04C CB24             SLA     H
  E04E                  ;
  E04E F5       PADR    PUSH    AF              ;**34**
  E04F 7D               LD      A,L     ;Spalte
  E050 6C               LD      L,H     ;Pixelzeile
  E051 FE28             CP      28H
  E053 3006             JR      NC,IAD2 ;zu gro~
  E055 F680             OR      80H
  E057 67               LD      H,A     ;HL=Pixeladr.
  E058 F1               POP     AF
  E059 A7               AND     A       ;CY=0
  E05A C9               RET
  E05B                  ;
  E05B F1       IAD2    POP     AF
  E05C 37               SCF             ;CY=1
  E05D C9               RET
  E05E           
  E05E          TCIF    ;Test Cursor im Fenster  **33**
  E05E 3A9EB7           LD      A,(WINLG)
  E061 3D               DEC     A
  E062 93               SUB     E       ;Cursor-Spalte
  E063 D8               RET     C
  E064 3A9FB7           LD      A,(WINLG+1)
  E067 3D               DEC     A
  E068 92               SUB     D       ;Cursor-Zeile
  E069 C9               RET
  E06A           
  E06A F5       DABR    PUSH    AF              ;**32**
  E06B CD5EE0           CALL    TCIF
  E06E 38EB             JR      C,IAD2
  E070 3A9CB7           LD      A,(WINON)
  E073 83               ADD     E       ;Cursor-Spalte
  E074 D5               PUSH    DE
  E075 5F               LD      E,A     ;absolut
  E076 3A9DB7           LD      A,(WINON+1)
  E079 82               ADD     D       ;Cursor-Zeile
  E07A 87               ADD     A
  E07B 87               ADD     A
  E07C 87               ADD     A       ;*8
  E07D 6F               LD      L,A     ;Pixelzeile
  E07E 2600             LD      H,0
  E080 54               LD      D,H     ;D=0
  E081 29               ADD     HL,HL
  E082 29               ADD     HL,HL   ;*4
  E083 19               ADD     HL,DE   ;*5, zus. *40
  E084 ED5BCBB7         LD      DE,(VRAM)
  E088 5F               LD      E,A
  E089 19               ADD     HL,DE
  E08A D1               POP     DE
  E08B F1               POP     AF
  E08C A7               AND     A
  E08D C9               RET
  E08E           
  E08E          ESC81   ;Farbe compeln (V<=>H)
  E08E F5               PUSH    AF
  E08F E6C0             AND     0C0H
  E091 4F               LD      C,A     ;Bit 6,7
  E092 F1               POP     AF
  E093 F5               PUSH    AF
  E094 E607             AND     7       ;hFarbe
  E096 07               RLCA
  E097 07               RLCA
  E098 07               RLCA
  E099 B1               OR      C
  E09A 4F               LD      C,A
  E09B F1               POP     AF
  E09C 0F               RRCA
  E09D 0F               RRCA
  E09E 0F               RRCA
  E09F E607             AND     7       ;vFarbe
  E0A1 B1               OR      C
  E0A2 C9               RET
  E0A3           
  E0A3          WPIX    ;Zeichen auf Grafikbildschirm
  E0A3                  ;sichtbar machen, PE: DE,A
  E0A3 E5               PUSH    HL
  E0A4 D5               PUSH    DE
  E0A5 C5               PUSH    BC
  E0A6 F5               PUSH    AF
  E0A7 2A9CB7           LD      HL,(WINON)
  E0AA 19               ADD     HL,DE
  E0AB EB               EX      DE,HL   ;DE:Absolut-Curs
  E0AC 21A6B7           LD      HL,CCTL0
  E0AF 87               ADD     A
  E0B0 3002             JR      NC,WPIX1
  E0B2 2EAA             LD      L,0AAH  ;CCTL2
  E0B4 D640     WPIX1   SUB     40H
  E0B6 3804             JR      C,WPIX2 ;0..1F o.80..9F
  E0B8 FE80             CP      80H
  E0BA 3804             JR      C,WPIX3 ;20.40 o.A0..C0
  E0BC C640     WPIX2   ADD     40H
  E0BE 2C               INC     L       ;um 1 CCTL
  E0BF 2C               INC     L       ;h|her
  E0C0 4E       WPIX3   LD      C,M
  E0C1 2C               INC     L       ;BC=(CCTL)
  E0C2 46               LD      B,M
  E0C3 87               ADD     A       ;*2
  E0C4 6F               LD      L,A
  E0C5 2600             LD      H,0
  E0C7 29               ADD     HL,HL   ;*4
  E0C8 09               ADD     HL,BC   ;*8
  E0C9 7B               LD      A,E     ;Absolutspalte
  E0CA FE28             CP      28H
  E0CC 3056             JR      NC,POPS4;au~erhalb
  E0CE F680             OR      80H
  E0D0 42               LD      B,D
  E0D1 57               LD      D,A
  E0D2 78               LD      A,B     ;A:=:D
  E0D3 87               ADD     A
  E0D4 87               ADD     A
  E0D5 87               ADD     A       ;*8
  E0D6 5F               LD      E,A     ;DE=Pixeladr.
  E0D7 3AA2B7           LD      A,(STBT)
  E0DA CB57             BIT     2,A     ;invers?
  E0DC 280F             JR      Z,WPIX5
  E0DE 4F               LD      C,A     ;ja, per
  E0DF 0608             LD      B,8     ;Schleife
  E0E1 D5               PUSH    DE      ;zeichnen
  E0E2 7E       WPIX4   LD      A,M
  E0E3 2F               CPL             ;(Hier kein
  E0E4 12               LD      (DE),A  ;Test auf
  E0E5 23               INC     HL      ;Schreiben
  E0E6 13               INC     DE      ;Pixel aus!)
  E0E7 10F9             DJNZ    WPIX4   ;*8
  E0E9 79               LD      A,C
  E0EA 0F               RRCA
  E0EB 1814             JR      WPIX6
  E0ED           
  E0ED 0F       WPIX5   RRCA            ;Schreiben Pix?
  E0EE 3812             JR      C,WPIX7
  E0F0 D5               PUSH    DE      ;ja
  E0F1 EDA0             LDI             ;sehr schnell
  E0F3 EDA0             LDI             ;per LDI
  E0F5 EDA0             LDI
  E0F7 EDA0             LDI
  E0F9 EDA0             LDI
  E0FB EDA0             LDI
  E0FD EDA0             LDI
  E0FF EDA0             LDI
  E101 D1       WPIX6   POP     DE
  E102 0F       WPIX7   RRCA            ;Schreiben Farb?
  E103 381F             JR      C,POPS4
  E105 DD7E01           LD      A,(IX+1);ja
  E108 67               LD      H,A
  E109 EE02             XOR     2
  E10B F3               DI
  E10C D384             OUT     84H     ;Farbebene
  E10E 3AA3B7           LD      A,(COLOR)
  E111 12               LD      (DE),A  ;sehr schnell
  E112 13               INC     DE      ;ohne Schleife
  E113 12               LD      (DE),A
  E114 13               INC     DE      ;alles im DI
  E115 12               LD      (DE),A
  E116 13               INC     DE
  E117 12               LD      (DE),A
  E118 13               INC     DE
  E119 12               LD      (DE),A
  E11A 13               INC     DE
  E11B 12               LD      (DE),A
  E11C 13               INC     DE
  E11D 12               LD      (DE),A
  E11E 13               INC     DE
  E11F 12               LD      (DE),A
  E120 7C               LD      A,H
  E121 D384             OUT     84H,A
  E123 FB               EI
  E124 F1       POPS4   POP     AF
  E125 C1               POP     BC
  E126 D1               POP     DE
  E127 E1               POP     HL
  E128 C9               RET
  E129           
  E129          MOVELN  ;Rolle Fenster f}r Scrolling
  E129 E5               PUSH    HL
  E12A D5               PUSH    DE
  E12B C5               PUSH    BC
  E12C F5               PUSH    AF
  E12D 08               EX      AF,AF'
  E12E F5               PUSH    AF
  E12F 79               LD      A,C     ;BC:Pixelzeilen
  E130 08               EX      AF,AF'
  E131 3A9EB7           LD      A,(WINLG) ;Spalten
  E134 E5       MOLN1   PUSH    HL
  E135 D5               PUSH    DE
  E136 EDA0     MOLN2   LDI             ;zeichenweise
  E138 EDA0             LDI
  E13A EDA0             LDI
  E13C EDA0             LDI
  E13E EDA0             LDI
  E140 EDA0             LDI
  E142 EDA0             LDI
  E144 EDA0             LDI
  E146 EA36E1           JP      PE,MOLN2
  E149 D1               POP     DE
  E14A E1               POP     HL
  E14B 24               INC     H       ;neue Spalte
  E14C 14               INC     D
  E14D 08               EX      AF,AF'
  E14E 4F               LD      C,A
  E14F 08               EX      AF,AF'
  E150 3D               DEC     A       ;Spaltenz{hler
  E151 20E1             JR      NZ,MOLN1
  E153 08               EX      AF,AF'
  E154 F1               POP     AF
  E155 08               EX      AF,AF'
  E156 18CC             JR      POPS4
  E158           
  E158          PCHR    ;CRT-Grundprogramm
  E158 ED5BA0B7         LD      DE,(CURSO)
  E15C 21A2B7           LD      HL,STBT
  E15F CB66             BIT     4,M     ;ESC aktiv?
  E161 2822             JR      Z,PCHR3
  E163 CBA6     CRT1    RES     4,M
  E165 FE30             CP      '0'
  E167 D8               RET     C
  E168 FE3A             CP      '9'+1
  E16A 3004             JR      NC,PCHR1
  E16C D630             SUB     '0'
  E16E 180A             JR      PCHR2
  E170                  ;
  E170 FE41     PCHR1   CP      'A'
  E172 D8               RET     C
  E173 CBAF             RES     5,A
  E175 FE5B             CP      'Z'+1
  E177 D0               RET     NC
  E178 D637             SUB     'A'-0AH
  E17A 21DFB7   PCHR2   LD      HL,L3SIZ;Tabelle
  E17D BE               CP      M       ;lang genug?
  E17E D0               RET     NC
  E17F 87               ADD     A       ;*2
  E180 2ADDB7           LD      HL,(L3TAB) ;klar zum
  E183 180C             JR      PCHR4   ;Ansprung
  E185                  ;
  E185 FE20     PCHR3   CP      20H     ;Steuerfunktion?
  E187 3019             JR      NC,PCHR5
  E189 CB5E             BIT     3,M     ;darstellen?
  E18B 2015             JR      NZ,PCHR5
  E18D 87               ADD     A       ;*2
  E18E 2AB2B7           LD      HL,(CTAB)
  E191 4F       PCHR4   LD      C,A
  E192 0600             LD      B,0
  E194 09               ADD     HL,BC
  E195 7E               LD      A,M
  E196 23               INC     HL
  E197 66               LD      H,M
  E198 6F               LD      L,A
  E199 E9       JPHL    JP      (HL)    ;anspringen
  E19A           
  E19A 7B       ESC0    LD      A,E     ;Tabulator
  E19B E6F8             AND     0F8H    ;8er Step
  E19D C608             ADD     8
  E19F 5F               LD      E,A
  E1A0 1809             JR      CUR1
  E1A2                  ;
  E1A2 CD6AE0   PCHR5   CALL    DABR
  E1A5 D8               RET     C       ;au~erhalb
  E1A6 77               LD      M,A     ;ASCII eintragen
  E1A7 CDA3E0           CALL    WPIX    ;darstellen
  E1AA 1C       CUR     INC     E
  E1AB 3A9EB7   CUR1    LD      A,(WINLG)
  E1AE 3D               DEC     A
  E1AF BB               CP      E
  E1B0 D0               RET     NC
  E1B1 1E00     NL      LD      E,0     ;NL f}r 1E
  E1B3 14       CUD     INC     D
  E1B4 3A9FB7           LD      A,(WINLG+1)
  E1B7 BA               CP      D
  E1B8 C0               RET     NZ
  E1B9 2AA4B7           LD      HL,(WEND)
  E1BC E9               JP      (HL)    ;PAGE o. SCROLL
  E1BD           
  E1BD 2A99B7   HCOPY   LD      HL,(HCADR) ;ShCLR
  E1C0 E9               JP      (HL)
  E1C1           
  E1C1 7B       CUL     LD      A,E
  E1C2 A7               AND     A
  E1C3 2802             JR      Z,CUL1
  E1C5 1D               DEC     E
  E1C6 C9               RET
  E1C7           
  E1C7 7A       CUL1    LD      A,D
  E1C8 A7               AND     A
  E1C9 C8               RET     Z
  E1CA 15               DEC     D
  E1CB 3A9EB7   CEL     LD      A,(WINLG)
  E1CE 3D               DEC     A
  E1CF 5F               LD      E,A
  E1D0 C9               RET
  E1D1           
  E1D1 7A       CUU     LD      A,D
  E1D2 A7               AND     A
  E1D3 C8               RET     Z
  E1D4 15               DEC     D
  E1D5 C9               RET
  E1D6           
  E1D6 2145E2   PAGE    LD      HL,HOMEPG ;ShCUU
  E1D9 22A4B7   WADR    LD      (WEND),HL
  E1DC C9               RET
  E1DD           
  E1DD 2160E2   SCROL   LD      HL,SCRLPG ;ShCUD
  E1E0 18F7             JR      WADR
  E1E2           
  E1E2 CDC1E1   CLR     CALL    CUL
  E1E5 D5       DEL     PUSH    DE      ;Cursor retten
  E1E6 CD6AE0           CALL    DABR
  E1E9 7E               LD      A,M
  E1EA A7               AND     A       ;Ende?
  E1EB 2829             JR      Z,POPDE
  E1ED E5               PUSH    HL      ;VRAM
  E1EE D5       DEL1    PUSH    DE      ;Cursor
  E1EF 1C               INC     E       ;n{chste Spalte
  E1F0 CD6AE0           CALL    DABR    ;raus?
  E1F3 3008             JR      NC,DEL2
  E1F5 1E00             LD      E,0     ;ja, wie NL
  E1F7 14               INC     D
  E1F8 CD6AE0           CALL    DABR    ;raus?
  E1FB 3810             JR      C,DEL3  ;ja, wie Ende
  E1FD 7E       DEL2    LD      A,M
  E1FE A7               AND     A       ;Ende?
  E1FF 280C             JR      Z,DEL3
  E201 42               LD      B,D
  E202 4B               LD      C,E     ;BC:=DE
  E203 D1               POP     DE      ;DE=links davon
  E204 E3               EX      (SP),HL ;HL=VRAM links
  E205 77               LD      M,A     ;eintragen
  E206 CDA3E0           CALL    WPIX    ;zeichnen
  E209 50               LD      D,B
  E20A 59               LD      E,C     ;DE:=BC
  E20B 18E1             JR      DEL1    ;von vorn
  E20D           
  E20D D1       DEL3    POP     DE
  E20E E1               POP     HL
  E20F 3600             LD      M,0
  E211 3E20             LD      A,' '
  E213 CDA3E0           CALL    WPIX
  E216 D1       POPDE   POP     DE
  E217 C9               RET
  E218           
  E218 D5       INS     PUSH    DE      ;Cursor
  E219 3E20             LD      A,' '
  E21B CD6AE0           CALL    DABR
  E21E 46       INS1    LD      B,M     ;VRAM
  E21F 77               LD      M,A     ;neues Zeichen
  E220 CDA3E0           CALL    WPIX    ;zeichnen
  E223 78               LD      A,B
  E224 A7               AND     A       ;Dummy?
  E225 280E             JR      Z,INS2
  E227 1C               INC     E       ;CUR
  E228 CD6AE0           CALL    DABR    ;raus?
  E22B 30F1             JR      NC,INS1
  E22D 1E00             LD      E,0     ;wie NL
  E22F 14               INC     D
  E230 CD6AE0           CALL    DABR    ;raus?
  E233 30E9             JR      NC,INS1
  E235 D1       INS2    POP     DE      ;wenn ja Schlu~
  E236 C9               RET
  E237           
  E237 3A9FB7   CLS     LD      A,(WINLG+1) ;ShHOME
  E23A 1600             LD      D,0
  E23C F5       CLS1    PUSH    AF
  E23D CDAAE2           CALL    CLLN
  E240 F1               POP     AF
  E241 14               INC     D
  E242 3D               DEC     A
  E243 20F7             JR      NZ,CLS1 ;A Zeilen
  E245 1600     HOMEPG  LD      D,0
  E247 1E00     CBL     LD      E,0
  E249 C9               RET
  E24A           
  E24A          CLLINE  ;L|schen einer Zeile
  E24A                  ;HL: Adr. C: L{nge, A: Byte
  E24A 41               LD      B,C
  E24B 55       CLL1    LD      D,L     ;L retten
  E24C 77               LD      M,A     ;zeichenweise
  E24D 2C               INC     L       ;ohne Schleife
  E24E 77               LD      M,A
  E24F 2C               INC     L
  E250 77               LD      M,A
  E251 2C               INC     L
  E252 77               LD      M,A
  E253 2C               INC     L
  E254 77               LD      M,A
  E255 2C               INC     L
  E256 77               LD      M,A
  E257 2C               INC     L
  E258 77               LD      M,A
  E259 2C               INC     L
  E25A 77               LD      M,A
  E25B 6A               LD      L,D     ;L holen
  E25C 24               INC     H
  E25D 10EC             DJNZ    CLL1
  E25F C9               RET
  E260           
  E260 3A9EB7   SCRLPG  LD      A,(WINLG) ;Spalten
  E263 4F               LD      C,A
  E264 0600             LD      B,0
  E266 3A9FB7           LD      A,(WINLG+1) ;Zeilen
  E269 3D               DEC     A       ;nur 1 Zeile?
  E26A 283D             JR      Z,SCRL3 ;nur CLLN
  E26C D5               PUSH    DE
  E26D F5               PUSH    AF
  E26E 110000           LD      DE,0    ;Cursor li/oben
  E271 CD6AE0           CALL    DABR
  E274 EB               EX      DE,HL
  E275 212800   SCRL1   LD      HL,40   ;1 Zeile tiefer
  E278 19               ADD     HL,DE
  E279 E5               PUSH    HL
  E27A C5               PUSH    BC
  E27B EDB0             LDIR            ;im VRAM eine
  E27D C1               POP     BC      ;Zeile rollen
  E27E D1               POP     DE
  E27F 3D               DEC     A
  E280 20F3             JR      NZ,SCRL1;A*
  E282 F1               POP     AF
  E283 87               ADD     A
  E284 87               ADD     A
  E285 87               ADD     A       ;*8: Pixelzeile
  E286 4F               LD      C,A
  E287 110000           LD      DE,0
  E28A 42               LD      B,D
  E28B CD44E0           CALL    PADR1
  E28E EB               EX      DE,HL   ;DE=PixAdr.
  E28F 2E08             LD      L,8     ;H war 0
  E291 19               ADD     HL,DE
  E292 3AA2B7           LD      A,(STBT)
  E295 1F               RRA
  E296 1F               RRA             ;Schreiben Farb?
  E297 380B             JR      C,SCRL2
  E299 F5               PUSH    AF
  E29A CDDEE2           CALL    FARBEA
  E29D CD29E1           CALL    MOVELN
  E2A0 CDDEE2           CALL    FARBEA
  E2A3 F1               POP     AF
  E2A4 17       SCRL2   RLA             ;Schreiben Pix?
  E2A5 D429E1           CALL    NC,MOVELN
  E2A8 D1               POP     DE
  E2A9 15       SCRL3   DEC     D
  E2AA                  ;
  E2AA 1E00     CLLN    LD      E,0     ;ShDEL
  E2AC CD6AE0           CALL    DABR    ;au~erhalb?
  E2AF D8               RET     C       ;(Selbstschutz)
  E2B0 3A9EB7           LD      A,(WINLG)
  E2B3 47               LD      B,A     ;Spalten
  E2B4 4F               LD      C,A
  E2B5 AF               XOR     A
  E2B6 77       CLLN1   LD      M,A     ;VRAM l|schen
  E2B7 23               INC     HL
  E2B8 10FC             DJNZ    CLLN1
  E2BA CD44E0           CALL    PADR1
  E2BD D8               RET     C
  E2BE D5               PUSH    DE
  E2BF 3AA2B7           LD      A,(STBT)
  E2C2 0F               RRCA            ;Schreiben Pix?
  E2C3 5F               LD      E,A     ;nach E merken
  E2C4 3806             JR      C,CLLN2
  E2C6 AF               XOR     A
  E2C7 E5               PUSH    HL
  E2C8 CD4AE2           CALL    CLLINE  ;Pixel
  E2CB E1               POP     HL
  E2CC CB0B     CLLN2   RRC     E       ;Schreiben Farb?
  E2CE 380C             JR      C,CLLN3
  E2D0 CDDEE2           CALL    FARBEA
  E2D3 3AA3B7           LD      A,(COLOR)
  E2D6 CD4AE2           CALL    CLLINE  ;Farbe
  E2D9 CDDEE2           CALL    FARBEA
  E2DC D1       CLLN3   POP     DE
  E2DD C9               RET
  E2DE           
  E2DE DD7E01   FARBEA  LD      A,(IX+1) ;wie ESC9
  E2E1 EE02             XOR     2
  E2E3 F3               DI
  E2E4 DD7701           LD      (IX+1),A
  E2E7 D384             OUT     84H
  E2E9 FB               EI
  E2EA C9               RET
  E2EB           
  E2EB 010F0A   BEEP    LD      BC,0A0FH ;CHR(7)
  E2EE 213000           LD      HL,30H
  E2F1 D5               PUSH    DE
  E2F2 5C               LD      E,H     ;E=0
  E2F3 CD27F9           CALL    TON2
  E2F6 3E1E             LD      A,1EH
  E2F8 CDE0F1           CALL    WAIT
  E2FB 3E03             LD      A,3
  E2FD D38C             OUT     8CH
  E2FF 3E10             LD      A,10H
  E301 D1               POP     DE
  E302 C3E0F1           JP      WAIT
  E305           
  E305 213000   CLIK    LD      HL,30H
  E308 D5               PUSH    DE
  E309 5C               LD      E,H     ;E=0
  E30A CD27F9           CALL    TON2
  E30D D1               POP     DE
  E30E C9               RET
  E30F           
  E30F 21A3B7   ESC8    LD      HL,COLOR ;Farbtausch
  E312 7E               LD      A,M
  E313 CD8EE0           CALL    ESC81
  E316 77       LDMA    LD      M,A             ;**28**
  E317 C9               RET
  E318           
  E318 7E       LDAM    LD      A,M             ;**29**
  E319 C9               RET
  E31A           
  E31A DD7E08   CLICK   LD      A,(IX+8) ;ShINS
  E31D EE20             XOR     20H
  E31F DD7708           LD      (IX+8),A
  E322 C9       NOT     RET
  E323           
  E323 21A2B7   ESC     LD      HL,STBT ;ShSTOP
  E326 CBE6             SET     4,M
  E328 C9               RET
  E329           
  E329 E5       CRT     PUSH    HL              ;**00**
  E32A D5               PUSH    DE
  E32B C5               PUSH    BC
  E32C F5               PUSH    AF
  E32D CD58E1           CALL    PCHR
  E330 ED53A0B7         LD      (CURSO),DE
  E334 F1               POP     AF
  E335 C1               POP     BC
  E336 D1               POP     DE
  E337 E1               POP     HL
  E338 C9               RET
  E339           
  E339          ;KBD-Treiber
  E339           
  E339          ISRC3   ;ISR CTC Kanal 3 (Tastatur)
  E339 FB               EI
  E33A F5               PUSH    AF
  E33B 3E23             LD      A,23H   ;DI,ZG256,Res
  E33D D38F             OUT     8FH     ;CTC K3
  E33F DDCB08DE         SET     3,(IX+8)
  E343 DD360D00         LD      (IX+13),0 ;Zeichen tot
  E347 1861             JR      TST4
  E349           
  E349          ISRPB   ;ISR PIO Kanal B (Tastatur)
  E349 F5               PUSH    AF
  E34A DB8F             IN      A,8FH   ;gemessene
  E34C F5               PUSH    AF      ;Zeit retten
  E34D 3EA7             LD      A,0A7H  ;EI,ZG256,Res
  E34F D38F             OUT     8FH,A
  E351 3E8F             LD      A,8FH   ;Zeitkonstante
  E353 D38F             OUT     8FH
  E355 F1               POP     AF
  E356 FB               EI
  E357 DDCB085E         BIT     3,(IX+8)
  E35B DDCB089E         RES     3,(IX+8)
  E35F 2049             JR      NZ,TST4
  E361 FE78             CP      78H     ;zu kurz
  E363 3045             JR      NC,TST4 ;(z.B. St|rung)
  E365 FE65             CP      65H     ;Diskriminator
  E367 303D             JR      NC,TST3
  E369 C6BE             ADD     0BEH    ;Stop-Bit?
  E36B 3839             JR      C,TST3
  E36D E5               PUSH    HL
  E36E D5               PUSH    DE
  E36F DD7E0C           LD      A,(IX+12)
  E372 1F               RRA             ;7bit-Scancode
  E373 EE01             XOR     1       ;Startbit neg.
  E375 DD6E0E           LD      L,(IX+14)
  E378 DD660F           LD      H,(IX+15)
  E37B 1600             LD      D,0
  E37D 5F               LD      E,A
  E37E 19               ADD     HL,DE   ;Pos. in KTAB
  E37F 7E               LD      A,M     ;ASCII holen
  E380 D1               POP     DE
  E381 E1               POP     HL
  E382 DDCB087E         BIT     7,(IX+8);CAPS aktiv?
  E386 200A             JR      NZ,TST2 ;1=nein
  E388 FE40             CP      40H
  E38A 3806             JR      C,TST2
  E38C FE80             CP      80H
  E38E 3002             JR      NC,TST2
  E390 EE20             XOR     20H     ;Klein<=>Gro~
  E392 DDBE0D   TST2    CP      (IX+13) ;= letzter Code?
  E395 201D             JR      NZ,TST6
  E397 F5               PUSH    AF      ;ja
  E398 3AE0B7           LD      A,(COUNT)
  E39B DDBE0A           CP      (IX+10) ;FastRepeat?
  E39E 3811             JR      C,TST5  ;ja
  E3A0 F1               POP     AF      ;nein
  E3A1 DD340A           INC     (IX+10) ;Zeit abwarten
  E3A4 1804             JR      TST4
  E3A6           
  E3A6 DDCB0C1E TST3    RR      (IX+12) ;Bit rechts
  E3AA DB89     TST4    IN      A,89H   ;einschieben
  E3AC D389             OUT     89H     ;und PIO-Logik
  E3AE F1               POP     AF      ;freigeben
  E3AF ED4D             RETI
  E3B1           
  E3B1 F1       TST5    POP     AF
  E3B2 1808             JR      TST7
  E3B4           
  E3B4 DD360A00 TST6    LD      (IX+10),0 ;neuer Code
  E3B8 FE16             CP      16H     ;CAPS?
  E3BA 2809             JR      Z,TST8
  E3BC DD770D   TST7    LD      (IX+13),A ;eintragen
  E3BF DDCB08C6         SET     0,(IX+8)  ;g}ltig machen
  E3C3 18E5             JR      TST4
  E3C5           
  E3C5 DD7E08   TST8    LD      A,(IX+8) ;CAPS-Programm
  E3C8 EE80             XOR     80H
  E3CA DD7708           LD      (IX+8),A
  E3CD 3E16             LD      A,16H
  E3CF 18EB             JR      TST7
  E3D1           
  E3D1          KBDS    ;Abfrage ohne Quittung   **0C**
  E3D1 B7               OR      A
  E3D2 DDCB0846         BIT     0,(IX+8);Code g}ltig?
  E3D6 C8               RET     Z
  E3D7 DD7E0D           LD      A,(IX+13)
  E3DA 37               SCF             ;wenn ja
  E3DB C9               RET
  E3DC           
  E3DC          KBDZ    ;Abfrage mit Quittung    **0E**
  E3DC CDD1E3           CALL    KBDS
  E3DF D0               RET     NC
  E3E0 DDCB0886         RES     0,(IX+8)
  E3E4 C9               RET
  E3E5           
  E3E5          BRKT    ;Test auf BRK-Anforderung**2A**
  E3E5 CDD1E3           CALL    KBDS
  E3E8 D0               RET     NC
  E3E9 FE03             CP      3
  E3EB 37               SCF
  E3EC C8               RET     Z
  E3ED A7               AND     A
  E3EE C9               RET
  E3EF           
  E3EF          ;SWITCH und JUMP
  E3EF           
  E3EF 7F7F             DW      7F7FH
  E3F1 53574954         DB      'SWITCH',1
  E3F8 53               LD      D,E
  E3F9 CD0BE4           CALL    MODU
  E3FC 7D               LD      A,L
  E3FD CD59F3           CALL    AHSPC   ;Platz
  E400 7C               LD      A,H
  E401 CD59F3           CALL    AHSPC   ;Kennbyte
  E404 7A               LD      A,D
  E405 CD76F3           CALL    AHEX    ;Steuerbyte
  E408 C36BF3           JP      CRLF
  E40B           
  E40B          MODU    ;Lesen und Schalten      **26**
  E40B 26B8             LD      H,0B8H  ;H(ModulStSp)
  E40D 0E80             LD      C,80H   ;I/O-Adr.
  E40F 45               LD      B,L
  E410 FE02             CP      2       ;Parameter?
  E412 3004             JR      NC,MODU1
  E414 56               LD      D,M     ;nur lesen
  E415 ED60     RSTRB   IN      H,(C)
  E417 C9               RET
  E418           
  E418 72       MODU1   LD      M,D     ;eintragen
  E419 7D               LD      A,L
  E41A FE05             CP      5       ;interne Module?
  E41C 3814             JR      C,MODU2
  E41E ED51             OUT     (C),D   ;Senden Steuerb.
  E420 18F3             JR      RSTRB
  E422           
  E422 E6F5     MODU6   AND     0F5H    ;f}r RAM0
  E424 CB42             BIT     0,D
  E426 285C             JR      Z,OUT88
  E428 CBCF             SET     1,A
  E42A CB4A             BIT     1,D
  E42C 2856             JR      Z,OUT88
  E42E CBDF             SET     3,A
  E430 1852             JR      OUT88
  E432           
  E432 FE04     MODU2   CP      4       ;RAM4?
  E434 3818             JR      C,MODU3
  E436 DD7E04           LD      A,(IX+4)
  E439 E6FC             AND     0FCH
  E43B CB42             BIT     0,D
  E43D 2808             JR      Z,SRAM4
  E43F CBC7             SET     0,A
  E441 CB4A             BIT     1,D
  E443 2802             JR      Z,SRAM4
  E445 CBCF             SET     1,A
  E447 DD7704   SRAM4   LD      (IX+4),A
  E44A D386             OUT     86H
  E44C 1838             JR      STBFF
  E44E           
  E44E FE03     MODU3   CP      3       ;RAM8?
  E450 3824             JR      C,MODU4
  E452 DB89             IN      A,89H
  E454 E69F             AND     9FH
  E456 CB42             BIT     0,D
  E458 2808             JR      Z,SRAM8 ;RAM8 aus
  E45A CBEF             SET     5,A     ;RAM8 ein
  E45C CB4A             BIT     1,D     ;Schreibschutz?
  E45E 2802             JR      Z,SRAM8
  E460 CBF7             SET     6,A     ;aus
  E462 D389     SRAM8   OUT     89H
  E464 DD7E01           LD      A,(IX+1)
  E467 F610             OR      10H
  E469 CB52             BIT     2,D     ;RAM8-Segment?
  E46B 2002             JR      NZ,SSEG8
  E46D EE10             XOR     10H     ;(nicht vorbe-
  E46F D384     SSEG8   OUT     84H     ;reitet f}r
  E471 DD7701           LD      (IX+1),A;256k-Variante)
  E474 1810             JR      STBFF
  E476           
  E476 FE02     MODU4   CP      2       ;BASIC-ROM?
  E478 380F             JR      C,MODU5
  E47A DB88             IN      A,88H   ;(keine Ebenen-
  E47C CB42             BIT     0,D     ;Umschaltung!)
  E47E CBBF             RES     7,A     ;aus
  E480 2802             JR      Z,OUT88
  E482 CBFF             SET     7,A     ;ein
  E484 D388     OUT88   OUT     88H
  E486 26FF     STBFF   LD      H,0FFH  ;Kennbyte
  E488 C9               RET
  E489           
  E489 FE01     MODU5   CP      1       ;IRM?
  E48B DB88             IN      A,88H
  E48D DA22E4           JP      C,MODU6 ;RAM0
  E490 CB42             BIT     0,D
  E492 CB97             RES     2,A
  E494 28EE             JR      Z,OUT88
  E496 CBD7             SET     2,A
  E498 18EA             JR      OUT88
  E49A           
  E49A 7F7F             DW      7F7FH
  E49C 4A554D50         DB      'JUMP',1
  E4A1 7D               LD      A,L
  E4A2 47       JUMP    LD      B,A             ;**27**
  E4A3 0E80             LD      C,80H
  E4A5 ED78             IN      A,(C)
  E4A7 3C               INC     A       ;Kennbyte FF?
  E4A8 CA61F3           JP      Z,ERRM  ;ja-Fehler
  E4AB 3EFF             LD      A,0FFH  ;Ausgabe FF
  E4AD 26B8             LD      H,0B8H
  E4AF 68               LD      L,B
  E4B0 77               LD      M,A     ;Eintrag
  E4B1 F3               DI
  E4B2 ED79             OUT     (C),A
  E4B4 DB88             IN      A,88H
  E4B6 E67E             AND     7EH
  E4B8 C3B4B7           JP      BJUMP   ;in IRM
  E4BB           
  E4BB          ;TAPE-Treiber
  E4BB           
  E4BB F3       ISRI1   DI
  E4BC DB88             IN      A,88H
  E4BE F640             OR      40H     ;Motor ein
  E4C0 E6DF             AND     0DFH    ;LED aus
  E4C2 D388     ISRO1   OUT     88H
  E4C4 FB               EI
  E4C5 2AA4B7           LD      HL,(WEND)
  E4C8 22CFB7           LD      (ZWEND),HL
  E4CB CDFDF1           CALL    NOUT    ;kein Drucker!
  E4CE 22CDB7           LD      (ZOTAB),HL
  E4D1 CD33F4           CALL    KPUFF
  E4D4 CDD6E1           CALL    PAGE    ;Page Mode
  E4D7 3E03     TOFF    LD      A,3     ;Ton aus
  E4D9 D38C             OUT     8CH     ;CTC0
  E4DB D38D             OUT     8DH     ;CTC1
  E4DD 1828             JR      CSRO2
  E4DF           
  E4DF          CSRO    ;Abschlu~ Bandausgabe    **09**
  E4DF DD3602FE         LD      (IX+2),0FEH
  E4E3 CD2FE5           CALL    MBO
  E4E6 2ACDB7   CSROI   LD      HL,(ZOTAB) ;intern
  E4E9 22B9B7           LD      (OUTAB),HL
  E4EC 2ACFB7           LD      HL,(ZWEND) ;gro~es
  E4EF 22A4B7           LD      (WEND),HL  ;R}cksetzen
  E4F2 2180B7           LD      HL,0B780H
  E4F5 AF               XOR     A
  E4F6 2D       CSRO1   DEC     L       ;KassPuff
  E4F7 77               LD      M,A     ;l|schen
  E4F8 20FC             JR      NZ,CSRO1
  E4FA DB88             IN      A,88H
  E4FC E69F             AND     9FH     ;Motor, LED aus
  E4FE D388             OUT     88H
  E500 CD6BF3           CALL    CRLF
  E503 DDCB0886         RES     0,(IX+8)
  E507 DDCB015E CSRO2   BIT     3,(IX+1);HiRes?
  E50B 2806             JR      Z,CSRO3
  E50D DB89             IN      A,89H   ;nein-
  E50F CBFF             SET     7,A     ;Blinken ein
  E511 D389             OUT     89H
  E513 DDCB088E CSRO3   RES     1,(IX+8)
  E517 3E47             LD      A,47H   ;CTC2 auf
  E519 D38E             OUT     8EH     ;Blinken
  E51B 3E0C             LD      A,0CH   ;stellen
  E51D D38E             OUT     8EH
  E51F C9               RET
  E520           
  E520          ISRO    ;Init. Bandausgabe       **08**
  E520 F3               DI
  E521 DB88             IN      A,88H
  E523 F660             OR      60H     ;Motor, LED ein
  E525 CDC2E4           CALL    ISRO1
  E528 DD360200         LD      (IX+2),0
  E52C 010010           LD      BC,1000H
  E52F                  ;
  E52F          MBO     ;Ausgabe 1 Block         **01**
  E52F DD3402           INC     (IX+2)
  E532 F3               DI
  E533 3E87             LD      A,87H   ;EI,ZG16,Res.
  E535 D38D             OUT     8DH
  E537 3E2F             LD      A,2FH   ;Vorton
  E539 D38D             OUT     8DH
  E53B FB               EI
  E53C 57               LD      D,A     ;D=2Fh
  E53D 5F               LD      E,A     ;E=2Fh
  E53E CD84E5   MBO1    CALL    BITOUT
  E541 EDA1             CPI
  E543 EA3EE5           JP      PE,MBO1 ;BC mal
  E546 CD82E5           CALL    ZTON    ;Trennzeichen
  E549 DD7E02           LD      A,(IX+2)
  E54C CD6FE5           CALL    BYTOT   ;Blocknummer
  E54F DD6E05           LD      L,(IX+5)
  E552 DD6606           LD      H,(IX+6)
  E555 0680             LD      B,80H
  E557 7E       BLKOT   LD      A,M
  E558 CD6FE5           CALL    BYTOT   ;Datenbyte
  E55B 79               LD      A,C
  E55C 86               ADD     M       ;Pr}fsumme
  E55D 4F               LD      C,A     ;nebenher
  E55E 23               INC     HL
  E55F 10F6             DJNZ    BLKOT
  E561 CD6FE5           CALL    BYTOT   ;und ausgeben
  E564 7A               LD      A,D
  E565 CD87E5           CALL    HBITOT  ;noch ein
  E568 5D               LD      E,L     ;Knacks
  E569 54               LD      D,H
  E56A 3E03             LD      A,3     ;CTC stoppen
  E56C D38D             OUT     8DH
  E56E C9               RET
  E56F           
  E56F          BYTOT   ;Ausgabe eines Bytes
  E56F C5               PUSH    BC
  E570 4F               LD      C,A
  E571 0608             LD      B,8
  E573 CB09     BYTOUT  RRC     C       ;mit Bit0
  E575 1E17             LD      E,17H   ;beginnend
  E577 D484E5           CALL    NC,BITOUT
  E57A 1E2F             LD      E,2FH   ;Zeitkonstanten
  E57C DC84E5           CALL    C,BITOUT
  E57F 10F2             DJNZ    BYTOUT  ;8*
  E581 C1               POP     BC
  E582 1E5D     ZTON    LD      E,5DH   ;ZK Trennz.
  E584 CD87E5   BITOUT  CALL    HBITOT  ;aufrufen und
  E587 DD7200   HBITOT  LD      (IX),D  ;reinlaufen
  E58A DD7E00   HBIT1   LD      A,(IX)  ;Warten auf
  E58D A7               AND     A       ;Interrupt
  E58E 20FA             JR      NZ,HBIT1
  E590 53               LD      D,E
  E591 C9               RET
  E592           
  E592          ISRI    ;Init. Bandeingabe       **0A**
  E592 CDBBE4           CALL    ISRI1
  E595                  ;
  E595          MBI     ;Einlesen 1 Block        **05**
  E595 3E83             LD      A,83H   ;EI
  E597 D38A             OUT     8AH     ;an PIO A
  E599 E5               PUSH    HL
  E59A D5               PUSH    DE
  E59B 0616     MBI1    LD      B,16H
  E59D DD360D00         LD      (IX+13),0 ;Pr}fsumme=0
  E5A1 CD1EE6   MBI2    CALL    STOP1
  E5A4 38F5             JR      C,MBI1
  E5A6 FEBA             CP      0BAH
  E5A8 CDF1E5           CALL    LEDOO
  E5AB 38EE             JR      C,MBI1  ;16h korrekte
  E5AD 10F2             DJNZ    MBI2    ;Schwingungen
  E5AF 0602     MBI3    LD      B,2     ;erkennen
  E5B1 AF       MBI4    XOR     A
  E5B2 4F               LD      C,A
  E5B3 DD7700           LD      (IX),A
  E5B6 CD31E6           CALL    STOP21  ;2 halbe Trenn-
  E5B9 FE5D             CP      5DH     ;zeichen er-
  E5BB 30F2             JR      NC,MBI3 ;kennen
  E5BD 10F2             DJNZ    MBI4
  E5BF CD3DE6           CALL    BYTIN   ;Blocknummer
  E5C2 3825             JR      C,MBI5  ;St|rung
  E5C4 DD7702           LD      (IX+2),A
  E5C7 0680             LD      B,80H
  E5C9 DD6E05           LD      L,(IX+5)
  E5CC DD6606           LD      H,(IX+6)
  E5CF CD3DE6   BLKIN   CALL    BYTIN   ;Datenbyte
  E5D2 3815             JR      C,MBI5
  E5D4 77               LD      M,A
  E5D5 DD7E0D           LD      A,(IX+13)
  E5D8 86               ADD     M       ;aufsummieren
  E5D9 DD770D           LD      (IX+13),A
  E5DC 23               INC     HL
  E5DD 10F0             DJNZ    BLKIN
  E5DF CD3DE6           CALL    BYTIN   ;Pr}fsumme
  E5E2 3805             JR      C,MBI5  ;St|rung
  E5E4 DD960D           SUB     (IX+13) ;gleich?
  E5E7 C6FF             ADD     0FFH    ;CY:=/Z
  E5E9 D1       MBI5    POP     DE
  E5EA E1               POP     HL
  E5EB F3               DI
  E5EC 3E03             LD      A,3     ;DI an PIO A
  E5EE D38A             OUT     8AH
  E5F0 FB               EI
  E5F1 DB88     LEDOO   IN      A,88H   ;LED ein/aus
  E5F3 CBEF             SET     5,A     ;je nach CY
  E5F5 3002             JR      NC,MBI6
  E5F7 CBAF             RES     5,A
  E5F9 D388     MBI6    OUT     88H
  E5FB C9               RET
  E5FC           
  E5FC          ISRC1   ;ISR CTC Kanal 1 (Kassette-Out)
  E5FC F5               PUSH    AF
  E5FD 3E87             LD      A,87H
  E5FF D38D             OUT     8DH
  E601 DD7E00           LD      A,(IX)  ;Uebergabezelle
  E604 D38D             OUT     8DH
  E606 DD360000         LD      (IX),0  ;Quittierung
  E60A 180E             JR      ISR1E
  E60C           
  E60C          ISRPA   ;ISR PIO Kanal A (Kassette-In)
  E60C F5               PUSH    AF
  E60D DB8E             IN      A,8EH
  E60F DD7700           LD      (IX),A  ;Uebergabezelle
  E612 3E07             LD      A,7
  E614 D38E             OUT     8EH
  E616 3EA3             LD      A,0A3H
  E618 D38E             OUT     8EH
  E61A F1       ISR1E   POP     AF
  E61B FB       INTEND  EI
  E61C ED4D             RETI
  E61E           
  E61E DD360000 STOP1   LD      (IX),0  ;1 Bit einlesen
  E622 DB88     STOP11  IN      A,88H   ;PIO-Logik
  E624 D388             OUT     88H     ;freigeben
  E626 DD7E00           LD      A,(IX)  ;Warten auf
  E629 B7               OR      A       ;Interrupt
  E62A 28F6             JR      Z,STOP11
  E62C 4F               LD      C,A
  E62D DD360000         LD      (IX),0
  E631 DB88     STOP21  IN      A,88H   ;dto.
  E633 D388             OUT     88H,A
  E635 DD7E00           LD      A,(IX)
  E638 B7               OR      A
  E639 28F6             JR      Z,STOP21
  E63B 81               ADD     C       ;beide 'Zeiten'
  E63C C9               RET             ;addieren
  E63D           
  E63D          BYTIN   ;Einlesen eines Bytes
  E63D                  ;PA: A:Byte, CY=1: St|rung
  E63D 110008           LD      DE,800H
  E640 CD1EE6   TRN1    CALL    STOP1
  E643 3803             JR      C,TRN2
  E645 FEBA             CP      0BAH    ;zu intolerant
  E647 D8               RET     C
  E648 3F       TRN2    CCF
  E649 CB1B             RR      E       ;Bit einschieben
  E64B 15               DEC     D
  E64C 20F2             JR      NZ,TRN1 ;8*
  E64E CD1EE6           CALL    STOP1   ;Trennzeichen
  E651 7B               LD      A,E     ;mit CY-
  E652 C9               RET             ;R}ckmeldung
  E653           
  E653          ISRC2   ;CTC2: Tonlg.
  E653 CD1BE6           CALL    INTEND
  E656 F5               PUSH    AF
  E657 CDD7E4           CALL    TOFF
  E65A F1               POP     AF
  E65B C9               RET
  E65C           
  E65C          ;BASIC:
  E65C           
  E65C          BEXP1   ;BASIC-Exp 1
  E65C 78               LD      A,B
  E65D D609             SUB     9
  E65F 384F             JR      C,SNERR1
  E661 FE1B             CP      27
  E663 304B             JR      NC,SNERR1       ;>26
  E665 07               RLCA            ;*2
  E666 4F               LD      C,A
  E667 0600             LD      B,0
  E669 EB               EX      DE,HL
  E66A 21C2FD           LD      HL,TOKJP+8
  E66D C3B7C8           JP      0C8B7H  ;R}cksprung
  E670           
  E670          BEXP2   ;BASIC-Exp 2
  E670 7E               LD      A,M
  E671 FEDF             CP      0DFH
  E673 D8               RET     C               ;<DFH
  E674 FEE3             CP      0E3H
  E676 D0               RET     NC              ;>E2H
  E677 FEE1             CP      0E1H    ;AT?
  E679 CAA4EA           JP      Z,PRAT
  E67C 3AFD03           LD      A,(3FDH)
  E67F A7               AND     A       ;PRINT-Erw.?
  E680 202E             JR      NZ,SNERR1
  E682 3C               INC     A
  E683 32FD03           LD      (3FDH),A
  E686 E5       PREX1   PUSH    HL
  E687 21A3B7           LD      HL,COLOR
  E68A CDA2EC           CALL    IRMRD
  E68D E1               POP     HL
  E68E 327E03           LD      (37EH),A ;Farbe merken
  E691 7E               LD      A,M
  E692 FEDF             CP      0DFH    ;INK?
  E694 281D             JR      Z,PRINK
  E696 FEE2             CP      0E2H    ;COLOR?
  E698 CAA8E6           JP      Z,PRCOL
  E69B CDBDC8           CALL    0C8BDH
  E69E CD21EB           CALL    PAPER
  E6A1 7E       PREX2   LD      A,M
  E6A2 FE3B             CP      ';'
  E6A4 283D             JR      Z,PREX5
  E6A6 1808             JR      SNERR1
  E6A8           
  E6A8 CDBDC8   PRCOL   CALL    0C8BDH
  E6AB CD3DEB           CALL    BCOLOR
  E6AE 18F1             JR      PREX2
  E6B0           
  E6B0 C348C3   SNERR1  JP      0C348H  ;SN-ERROR
  E6B3           
  E6B3 CDBDC8   PRINK   CALL    0C8BDH
  E6B6 CD07EB           CALL    INK
  E6B9 7E               LD      A,M
  E6BA FE3B             CP      ';'
  E6BC 2825             JR      Z,PREX5
  E6BE CDCCC8           CALL    0C8CCH  ;Komma?
  E6C1 2C               DB      ','
  E6C2 FEE0             CP      0E0H    ;PAPER?
  E6C4 20EA             JR      NZ,SNERR1
  E6C6 CDBDC8           CALL    0C8BDH
  E6C9 CD21EB           CALL    PAPER
  E6CC CDCCC8           CALL    0C8CCH
  E6CF 3B               DB      ';'
  E6D0 CD03CB   PREX3   CALL    0CB03H
  E6D3 3A7E03           LD      A,(37EH)  ;Farbe
  E6D6 E5               PUSH    HL
  E6D7 21A3B7           LD      HL,COLOR
  E6DA CD9EEC           CALL    IRMWR     ;eintragen
  E6DD E1               POP     HL
  E6DE C1               POP     BC
  E6DF C9               RET
  E6E0 C5       PREX4   PUSH    BC
  E6E1 18A3             JR      PREX1
  E6E3 CDBDC8   PREX5   CALL    0C8BDH
  E6E6 18E8             JR      PREX3
  E6E8           
  E6E8          BEXP3   ;BASIC-Exp 3
  E6E8 79               LD      A,C
  E6E9 FE62             CP      62H
  E6EB CA58EA           JP      Z,VPEEK
  E6EE FE6E             CP      6EH
  E6F0 CAD0EC           JP      Z,PTEST
  E6F3 FE7C             CP      7CH
  E6F5 CAAEED           JP      Z,CSRLN
  E6F8 FE76             CP      76H
  E6FA CAD1ED           JP      Z,VGET
  E6FD D63E             SUB     3EH
  E6FF 38AF             JR      C,SNERR1
  E701 FE07             CP      7
  E703 30AB             JR      NC,SNERR1
  E705 EB               EX      DE,HL
  E706 01BAFD           LD      BC,TOKJP ;Tokentab.
  E709 E1               POP     HL
  E70A 6F               LD      L,A
  E70B 09               ADD     HL,BC
  E70C 4E               LD      C,M
  E70D 23               INC     HL
  E70E 66               LD      H,M
  E70F 69               LD      L,C
  E710 E5               PUSH    HL      ;Routine
  E711 EB               EX      DE,HL   ;anspringen
  E712 C9               RET
  E713           
  E713          STRING  ;String vervielf{ltigen
  E713 CDCCC8           CALL    0C8CCH
  E716 28               DB      '('     ;Klammer auf?
  E717 CD21D4           CALL    0D421H
  E71A F5               PUSH    AF
  E71B CDD6C8           CALL    0C8D6H  ;Komma?
  E71E CD3ACD           CALL    0CD3AH
  E721 CDDBC8           CALL    0C8DBH  ;Klammer zu?
  E724 F1               POP     AF
  E725 E5               PUSH    HL
  E726 F5               PUSH    AF
  E727 CD30D3           CALL    0D330H
  E72A 23               INC     HL
  E72B 23               INC     HL
  E72C 5E               LD      E,M
  E72D 23               INC     HL
  E72E 56               LD      D,M
  E72F C1               POP     BC
  E730 C5               PUSH    BC
  E731 F5               PUSH    AF
  E732 D5               PUSH    DE
  E733 4F               LD      C,A
  E734 AF               XOR     A
  E735 B9               CP      C
  E736 280C             JR      Z,STR2
  E738 B8               CP      B
  E739 2809             JR      Z,STR2
  E73B 79               LD      A,C
  E73C 05               DEC     B
  E73D 2805             JR      Z,STR2
  E73F 81       STR1    ADD     C
  E740 382B             JR      C,STR5
  E742 10FB             DJNZ    STR1
  E744 47       STR2    LD      B,A
  E745 0E00             LD      C,0
  E747 C5               PUSH    BC
  E748 CDE1D1           CALL    0D1E1H  ;Str.-Arithm.
  E74B C1               POP     BC
  E74C C1               POP     BC
  E74D C5               PUSH    BC
  E74E CD7ED1           CALL    0D17EH
  E751 E1               POP     HL
  E752 E3               EX      (SP),HL
  E753 7C               LD      A,H
  E754 E1               POP     HL
  E755 E3               EX      (SP),HL
  E756 6F               LD      L,A
  E757 24               INC     H
  E758 25       STR3    DEC     H
  E759 E5               PUSH    HL
  E75A C5               PUSH    BC
  E75B 2807             JR      Z,STR4
  E75D CDF2D2           CALL    0D2F2H
  E760 C1               POP     BC
  E761 E1               POP     HL
  E762 18F4             JR      STR3
  E764 C1       STR4    POP     BC
  E765 E1               POP     HL
  E766 D1               POP     DE
  E767 CD02D3           CALL    0D302H
  E76A C3A9D1           JP      0D1A9H
  E76D 1E1C     STR5    LD      E,1CH   ;ST-
  E76F C356C3           JP      0C356H  ;ERROR
  E772           
  E772          RENUM   ;neu nummerieren
  E772 E5               PUSH    HL
  E773 210A00           LD      HL,10
  E776 225403           LD      (354H),HL       ;DISTAN
  E779 2A5F03           LD      HL,(35FH)       ;Start
  E77C F5               PUSH    AF
  E77D E5               PUSH    HL
  E77E 23               INC     HL
  E77F 23               INC     HL
  E780 7E               LD      A,M
  E781 23               INC     HL
  E782 66               LD      H,M
  E783 6F               LD      L,A
  E784 224E03           LD      (34EH),HL       ;ZL-Nr
  E787 225203           LD      (352H),HL       ;NANF
  E78A ED5BD703         LD      DE,(3D7H)       ;Ende
  E78E 1B               DEC     DE
  E78F 1B               DEC     DE
  E790 E1       REN1    POP     HL
  E791 E5               PUSH    HL
  E792 7E               LD      A,M
  E793 23               INC     HL
  E794 66               LD      H,M
  E795 6F               LD      L,A
  E796 CD89C6           CALL    0C689H
  E799 E3               EX      (SP),HL
  E79A 20F4             JR      NZ,REN1
  E79C D1               POP     DE
  E79D 23               INC     HL
  E79E 23               INC     HL
  E79F 7E               LD      A,M
  E7A0 23               INC     HL
  E7A1 66               LD      H,M
  E7A2 6F               LD      L,A
  E7A3 225003           LD      (350H),HL ;ZL-Abst.
  E7A6 0604             LD      B,4
  E7A8 F1               POP     AF
  E7A9 214E03           LD      HL,34EH
  E7AC E3               EX      (SP),HL
  E7AD 2820     REN2    JR      Z,REN5
  E7AF CD86C9           CALL    0C986H
  E7B2 F5               PUSH    AF
  E7B3 7A               LD      A,D
  E7B4 B3               OR      E
  E7B5 CA67C9   REN3    JP      Z,0C967H  ;FC-ERROR
  E7B8 F1               POP     AF
  E7B9 E3               EX      (SP),HL
  E7BA 73               LD      M,E
  E7BB 23               INC     HL
  E7BC 72               LD      M,D
  E7BD 23               INC     HL
  E7BE 280F             JR      Z,REN5
  E7C0 F5               PUSH    AF
  E7C1 05               DEC     B
  E7C2 2807             JR      Z,REN4
  E7C4 F1               POP     AF
  E7C5 E3               EX      (SP),HL
  E7C6 CDD6C8           CALL    0C8D6H  ;Komma?
  E7C9 18E2             JR      REN2
  E7CB F1       REN4    POP     AF
  E7CC C276EA           JP      NZ,SNERR2
  E7CF 2A5003   REN5    LD      HL,(350H)
  E7D2 ED5B4E03         LD      DE,(34EH)
  E7D6 CD89C6           CALL    0C689H
  E7D9 DA67C9   REN6    JP      C,0C967H
  E7DC 2A5F03           LD      HL,(35FH)
  E7DF CDBEC4   REN7    CALL    0C4BEH
  E7E2 3804             JR      C,REN8
  E7E4 28CF             JR      Z,REN3
  E7E6 18F7             JR      REN7
  E7E8 E1       REN8    POP     HL
  E7E9 C5               PUSH    BC
  E7EA ED5B5003         LD      DE,(350H)
  E7EE 210000           LD      HL,0
  E7F1 225003           LD      (350H),HL
  E7F4 60       REN9    LD      H,B
  E7F5 69               LD      L,C
  E7F6 4E               LD      C,M
  E7F7 23               INC     HL
  E7F8 46               LD      B,M
  E7F9 78               LD      A,B
  E7FA B1               OR      C
  E7FB 28B8             JR      Z,REN3
  E7FD 23               INC     HL
  E7FE 7E               LD      A,M
  E7FF 23               INC     HL
  E800 66               LD      H,M
  E801 6F               LD      L,A
  E802 CD89C6           CALL    0C689H
  E805 2A5003           LD      HL,(350H)
  E808 23               INC     HL
  E809 225003           LD      (350H),HL
  E80C 20E6             JR      NZ,REN9
  E80E 23               INC     HL
  E80F 23               INC     HL
  E810 29               ADD     HL,HL
  E811 23               INC     HL
  E812 ED5BD703         LD      DE,(3D7H)
  E816 19               ADD     HL,DE
  E817 38C0             JR      C,REN6
  E819 CD27C3           CALL    0C327H  ;genug RAM?
  E81C 22D703           LD      (3D7H),HL
  E81F AF               XOR     A
  E820 2B               DEC     HL
  E821 77               LD      M,A
  E822 2B               DEC     HL
  E823 77               LD      M,A
  E824 1B               DEC     DE
  E825 1B               DEC     DE
  E826 EB               EX      DE,HL
  E827 73               LD      M,E
  E828 23               INC     HL
  E829 72               LD      M,D
  E82A 23               INC     HL
  E82B 3D               DEC     A
  E82C 77               LD      M,A
  E82D 23               INC     HL
  E82E 77               LD      M,A
  E82F 23               INC     HL
  E830 EB               EX      DE,HL
  E831 2A5203           LD      HL,(352H)
  E834 224E03           LD      (34EH),HL
  E837 E1       REN10   POP     HL
  E838 4E               LD      C,M
  E839 23               INC     HL
  E83A 46               LD      B,M
  E83B 23               INC     HL
  E83C C5               PUSH    BC
  E83D 014E03           LD      BC,34EH
  E840 7E               LD      A,M
  E841 12               LD      (DE),A
  E842 0A               LD      A,(BC)
  E843 77               LD      M,A
  E844 23               INC     HL
  E845 13               INC     DE
  E846 03               INC     BC
  E847 7E               LD      A,M
  E848 12               LD      (DE),A
  E849 0A               LD      A,(BC)
  E84A 77               LD      M,A
  E84B 13               INC     DE
  E84C 2A4E03           LD      HL,(34EH)
  E84F ED4B5403         LD      BC,(354H)
  E853 09               ADD     HL,BC
  E854 224E03           LD      (34EH),HL
  E857 2A5003           LD      HL,(350H)
  E85A 2B               DEC     HL
  E85B 7C               LD      A,H
  E85C B5               OR      L
  E85D 225003           LD      (350H),HL
  E860 20D5             JR      NZ,REN10
  E862 12               LD      (DE),A
  E863 E1               POP     HL
  E864 2A5F03           LD      HL,(35FH)
  E867 E5               PUSH    HL
  E868 E1       REN11   POP     HL
  E869 4E               LD      C,M
  E86A 23               INC     HL
  E86B 46               LD      B,M
  E86C 23               INC     HL
  E86D C5               PUSH    BC
  E86E 7E               LD      A,M
  E86F 23               INC     HL
  E870 A6               AND     M
  E871 3C               INC     A
  E872 2826             JR      Z,REN15
  E874 23       REN12   INC     HL
  E875 7E       REN13   LD      A,M
  E876 B7               OR      A
  E877 28EF             JR      Z,REN11
  E879 FE88             CP      88H     ;GOTO
  E87B 282A             JR      Z,REN16
  E87D FE8C             CP      8CH     ;GOSUB
  E87F 2826             JR      Z,REN16
  E881 FE8B             CP      8BH     ;RESTORE
  E883 2808             JR      Z,REN14
  E885 FED4             CP      0D4H    ;ELSE
  E887 2804             JR      Z,REN14
  E889 FEA9             CP      0A9H    ;THEN
  E88B 20E7             JR      NZ,REN12
  E88D CD87C9   REN14   CALL    0C987H
  E890 7B               LD      A,E
  E891 B2               OR      D
  E892 C4BBE8           CALL    NZ,RENUP1
  E895 C4EDE8           CALL    NZ,RENUP2
  E898 18DB             JR      REN13
  E89A 2B       REN15   DEC     HL
  E89B 22D703           LD      (3D7H),HL
  E89E 2B               DEC     HL
  E89F 77               LD      M,A
  E8A0 2B               DEC     HL
  E8A1 77               LD      M,A
  E8A2 E1               POP     HL
  E8A3 E1               POP     HL
  E8A4 C38AC4           JP      0C48AH
  E8A7 CD87C9   REN16   CALL    0C987H
  E8AA 7B               LD      A,E
  E8AB B2               OR      D
  E8AC 28C7             JR      Z,REN13
  E8AE CDBBE8           CALL    RENUP1
  E8B1 C4EDE8           CALL    NZ,RENUP2
  E8B4 7E               LD      A,M
  E8B5 FE2C             CP      2CH
  E8B7 20BC             JR      NZ,REN13
  E8B9 18EC             JR      REN16
  E8BB           
  E8BB E5       RENUP1  PUSH    HL
  E8BC D5               PUSH    DE
  E8BD 11FFFF           LD      DE,-1
  E8C0 CDBBC4           CALL    0C4BBH  ;n{chste Zeile
  E8C3 D1               POP     DE
  E8C4 03               INC     BC
  E8C5 03               INC     BC
  E8C6 03               INC     BC
  E8C7 03               INC     BC
  E8C8 60               LD      H,B
  E8C9 69               LD      L,C
  E8CA ED4B5203         LD      BC,(352H)
  E8CE 7E       RUP11   LD      A,M
  E8CF 23               INC     HL
  E8D0 E5               PUSH    HL
  E8D1 B6               OR      M
  E8D2 2816             JR      Z,RUP13
  E8D4 7E               LD      A,M
  E8D5 2B               DEC     HL
  E8D6 6E               LD      L,M
  E8D7 67               LD      H,A
  E8D8 CD89C6           CALL    0C689H
  E8DB 280A             JR      Z,RUP12
  E8DD 2A5403           LD      HL,(354H)
  E8E0 09               ADD     HL,BC
  E8E1 44               LD      B,H
  E8E2 4D               LD      C,L
  E8E3 E1               POP     HL
  E8E4 23               INC     HL
  E8E5 18E7             JR      RUP11
  E8E7 3EFF     RUP12   LD      A,0FFH
  E8E9 B7               OR      A
  E8EA E1       RUP13   POP     HL
  E8EB E1               POP     HL
  E8EC C9               RET
  E8ED           
  E8ED C5       RENUP2  PUSH    BC
  E8EE EB               EX      DE,HL
  E8EF 2AD703           LD      HL,(3D7H)
  E8F2 ED52             SBC     HL,DE
  E8F4 E5               PUSH    HL
  E8F5 C1       RUP21   POP     BC
  E8F6 62               LD      H,D
  E8F7 6B               LD      L,E
  E8F8 1B               DEC     DE
  E8F9 1A               LD      A,(DE)
  E8FA FE2C             CP      ','
  E8FC 280B             JR      Z,RUP22
  E8FE FE3A             CP      ':'
  E900 3007             JR      NC,RUP22
  E902 C5               PUSH    BC
  E903 D5               PUSH    DE
  E904 EDB0             LDIR
  E906 D1               POP     DE
  E907 18EC             JR      RUP21
  E909 EB       RUP22   EX      DE,HL
  E90A D1               POP     DE
  E90B E5               PUSH    HL
  E90C C5               PUSH    BC
  E90D AF               XOR     A
  E90E 0698             LD      B,98H
  E910 CDAED6           CALL    0D6AEH
  E913 CD34D8           CALL    0D834H
  E916 C1               POP     BC
  E917 D1               POP     DE
  E918 23               INC     HL
  E919 13               INC     DE
  E91A 7E       RUP23   LD      A,M
  E91B B7               OR      A
  E91C 280F             JR      Z,RUP24
  E91E C5               PUSH    BC
  E91F E5               PUSH    HL
  E920 EB               EX      DE,HL
  E921 09               ADD     HL,BC
  E922 54               LD      D,H
  E923 5D               LD      E,L
  E924 2B               DEC     HL
  E925 EDB8             LDDR
  E927 E1               POP     HL
  E928 EDA0             LDI
  E92A C1               POP     BC
  E92B 18ED             JR      RUP23
  E92D D5       RUP24   PUSH    DE
  E92E ED5B5F03         LD      DE,(35FH)
  E932 CD93C4           CALL    0C493H
  E935 23       RUP25   INC     HL
  E936 7E               LD      A,M
  E937 23               INC     HL
  E938 B6               OR      M
  E939 20FA             JR      NZ,RUP25
  E93B EB               EX      DE,HL
  E93C 73               LD      M,E
  E93D 23               INC     HL
  E93E 72               LD      M,D
  E93F 13               INC     DE
  E940 13               INC     DE
  E941 ED53D703         LD      (3D7H),DE
  E945 E1               POP     HL
  E946 54               LD      D,H
  E947 5D               LD      E,L
  E948 7E       RUP26   LD      A,M
  E949 B7               OR      A
  E94A 23               INC     HL
  E94B 20FB             JR      NZ,RUP26
  E94D C1               POP     BC
  E94E E3               EX      (SP),HL
  E94F C5               PUSH    BC
  E950 EB               EX      DE,HL
  E951 C9               RET
  E952           
  E952          DELETE  ;Zeile(n) l|schen
  E952 C8               RET     Z
  E953 CD86C9           CALL    0C986H
  E956 CA42C4           JP      Z,0C442H
  E959 CDD6C8           CALL    0C8D6H
  E95C D5               PUSH    DE
  E95D CD86C9           CALL    0C986H
  E960 E1               POP     HL
  E961 C0               RET     NZ
  E962 EB               EX      DE,HL
  E963 E5               PUSH    HL
  E964 CDBBC4           CALL    0C4BBH  ;n{chste Zeile
  E967 D24DC4           JP      NC,0C44DH ;UL-ERROR
  E96A D1               POP     DE
  E96B F5               PUSH    AF
  E96C C5               PUSH    BC
  E96D CDBEC4           CALL    0C4BEH
  E970 D24DC4           JP      NC,0C44DH ;UL-ERROR
  E973 C1               POP     BC
  E974 C350C4           JP      0C450H  ;einsortieren
  E977           
  E977          PAUSE   ;Programm unterbrechen
  E977 CDBEC8           CALL    0C8BEH
  E97A 200F             JR      NZ,PAUS2
  E97C CDD1E3   PAUS1   CALL    KBDS
  E97F 30FB             JR      NC,PAUS1
  E981 FE03             CP      3       ;BRK?
  E983 C8               RET     Z
  E984 FE0A             CP      0AH     ;CUU?
  E986 20F4             JR      NZ,PAUS1
  E988 C3E4DD           JP      0DDE4H  ;Eingabe ASCII
  E98B CD21D4   PAUS2   CALL    0D421H
  E98E 4F               LD      C,A
  E98F 3E10     PAUS3   LD      A,10H   ;96 sek.
  E991 1E14             LD      E,14H   ;WAIT
  E993 CD15F0           CALL    0F015H  ;PV5
  E996 C5               PUSH    BC
  E997 CDD1E3           CALL    KBDS
  E99A C1               POP     BC
  E99B 300A             JR      NC,PAUS4
  E99D FE03             CP      3       ;BRK?
  E99F C8               RET     Z
  E9A0 FE0A             CP      0AH     ;CUU?
  E9A2 2003             JR      NZ,PAUS4
  E9A4 C3E4DD           JP      0DDE4H  ;Eingabe ASCII
  E9A7 0D       PAUS4   DEC     C
  E9A8 20E5             JR      NZ,PAUS3
  E9AA C9               RET
  E9AB           
  E9AB          BBEEP   ;Signalton
  E9AB 0601             LD      B,1     ;ohne Angabe 1x
  E9AD CDBEC8           CALL    0C8BEH
  E9B0 2804             JR      Z,BBP1
  E9B2 CD21D4           CALL    0D421H
  E9B5 47               LD      B,A     ;Anzahl
  E9B6 3E07     BBP1    LD      A,7     ;Beep
  E9B8 1E00             LD      E,0     ;CRT
  E9BA CD15F0           CALL    0F015H  ;PV5
  E9BD 10F7             DJNZ    BBP1
  E9BF C9               RET
  E9C0           
  E9C0          WINDOW  ;Fenster einstellen
  E9C0 CDBEC8           CALL    0C8BEH
  E9C3 2865             JR      Z,WINDOW1
  E9C5 E5               PUSH    HL
  E9C6 CD21D4           CALL    0D421H
  E9C9 E3               EX      (SP),HL     ;in
  E9CA 21F6B9           LD      HL,WNDFN+90 ;Fenster 9
  E9CD CD9EEC           CALL    IRMWR       ;erstellen
  E9D0 23               INC     HL
  E9D1 E3               EX      (SP),HL
  E9D2 CDD6C8           CALL    0C8D6H
  E9D5 CD21D4           CALL    0D421H
  E9D8 E3               EX      (SP),HL
  E9D9 CD9EEC           CALL    IRMWR
  E9DC 23               INC     HL
  E9DD E3               EX      (SP),HL
  E9DE CDD6C8           CALL    0C8D6H
  E9E1 CD21D4           CALL    0D421H
  E9E4 E3               EX      (SP),HL
  E9E5 CD9EEC           CALL    IRMWR
  E9E8 E3               EX      (SP),HL
  E9E9 CDD6C8           CALL    0C8D6H
  E9EC CD21D4           CALL    0D421H
  E9EF E3               EX      (SP),HL
  E9F0 57               LD      D,A
  E9F1 CD18F0           CALL    0F018H  ;IRMON
  E9F4 FE28             CP      28H
  E9F6 304D             JR      NC,WINERR
  E9F8 3AF8B9           LD      A,(WNDFN+92)
  E9FB 5F               LD      E,A
  E9FC FE28             CP      28H
  E9FE 3045             JR      NC,WINERR
  EA00 7A               LD      A,D
  EA01 93               SUB     E
  EA02 3841             JR      C,WINERR
  EA04 3C               INC     A
  EA05 329EB7           LD      (WINLG),A
  EA08 7B               LD      A,E
  EA09 329CB7           LD      (WINON),A
  EA0C 3AF7B9           LD      A,(WNDFN+91)
  EA0F FE20             CP      20H
  EA11 3032             JR      NC,WINERR
  EA13 57               LD      D,A
  EA14 3AF6B9           LD      A,(WNDFN+90)
  EA17 FE20             CP      20H
  EA19 302A             JR      NC,WINERR
  EA1B 5F               LD      E,A
  EA1C 7A               LD      A,D
  EA1D 93               SUB     E
  EA1E 3825             JR      C,WINERR
  EA20 3C               INC     A
  EA21 329FB7           LD      (WINLG+1),A
  EA24 7B               LD      A,E
  EA25 329DB7           LD      (WINON+1),A
  EA28 1810             JR      WINDOW2
  EA2A E5       WINDOW1 PUSH    HL
  EA2B CD18F0           CALL    0F018H  ;IRMON
  EA2E 210001           LD      HL,100H
  EA31 229CB7           LD      (WINON),HL
  EA34 21281E           LD      HL,1E28H
  EA37 229EB7           LD      (WINLG),HL
  EA3A 210000   WINDOW2 LD      HL,0
  EA3D 22A0B7           LD      (CURSO),HL
  EA40 CD1BF0           CALL    0F01BH  ;IRMOF
  EA43 E1               POP     HL
  EA44 C9               RET
  EA45 CD1BF0   WINERR  CALL    0F01BH  ;IRMOF
  EA48 182C             JR      SNERR2
  EA4A           
  EA4A          BLOAD   ;MC-Programm laden
  EA4A E5               PUSH    HL
  EA4B 2181B7           LD      HL,ARGN
  EA4E AF               XOR     A
  EA4F CD9EEC           CALL    IRMWR   ;ARGN=0
  EA52 E1               POP     HL
  EA53 1E10             LD      E,10H   ;LOAD
  EA55 C315F0           JP      0F015H  ;PV5
  EA58           
  EA58 CDE1CD   VPEEK   CALL    0CDE1H
  EA5B E3               EX      (SP),HL
  EA5C 11F3CD           LD      DE,0CDF3H ;RET-Adr.
  EA5F D5               PUSH    DE
  EA60 CD6FC9           CALL    0C96FH
  EA63 E5               PUSH    HL
  EA64 210080           LD      HL,8000H;IRM-Offset
  EA67 19               ADD     HL,DE
  EA68 3803             JR      C,VPK   ;Error
  EA6A 3EBF             LD      A,0BFH
  EA6C BC               CP      H
  EA6D 3807     VPK     JR      C,SNERR2
  EA6F CDA2EC           CALL    IRMRD
  EA72 E1               POP     HL
  EA73 C3E9D3           JP      0D3E9H
  EA76           
  EA76 C348C3   SNERR2  JP      0C348H  ;SN-ERROR
  EA79           
  EA79          VPOKE   ;in IRM schreiben
  EA79 CD6CC9           CALL    0C96CH
  EA7C D5               PUSH    DE
  EA7D CDD6C8           CALL    0C8D6H
  EA80 CD21D4           CALL    0D421H
  EA83 E3               EX      (SP),HL
  EA84 110080           LD      DE,8000H;IRM-Offset
  EA87 19               ADD     HL,DE
  EA88 38E3             JR      C,VPK   ;Error
  EA8A 57               LD      D,A
  EA8B 3EBF             LD      A,0BFH
  EA8D BC               CP      H
  EA8E 38DD             JR      C,VPK   ;nur bis BFFFH
  EA90 7A               LD      A,D
  EA91 CD9EEC           CALL    IRMWR   ;schreiben
  EA94 E1               POP     HL
  EA95 C9               RET
  EA96           
  EA96          BORDER  ;schaltet RAM-8!
  EA96 CD21D4           CALL    0D421H
  EA99 0F               RRCA
  EA9A 0F               RRCA
  EA9B 0F               RRCA
  EA9C E660             AND     60H     ;Auswahl
  EA9E 4F               LD      C,A
  EA9F 069F             LD      B,9FH   ;Maske
  EAA1 C34CF9           JP      TONB
  EAA4           
  EAA4          PRAT    ;PRINT AT
  EAA4 3AFD03           LD      A,(3FDH)
  EAA7 CB4F             BIT     1,A
  EAA9 CBCF             SET     1,A
  EAAB 32FD03           LD      (3FDH),A
  EAAE 20C6             JR      NZ,SNERR2
  EAB0 D5               PUSH    DE
  EAB1 E5               PUSH    HL
  EAB2 CD18F0           CALL    0F018H  ;IRMON
  EAB5 219CB7           LD      HL,WINON
  EAB8 22F6B9           LD      (WNDFN+90),HL
  EABB 11ECB9           LD      DE,WNDFN+80
  EABE 010600           LD      BC,6    ;Fenster
  EAC1 EDB0             LDIR            ;retten
  EAC3 210000           LD      HL,0    ;Fenster gro~
  EAC6 229CB7           LD      (WINON),HL
  EAC9 212820           LD      HL,2028H
  EACC 229EB7           LD      (WINLG),HL
  EACF CD1BF0           CALL    0F01BH  ;IRMOF
  EAD2 E1               POP     HL
  EAD3 CD4FEB           CALL    LOCAT
  EAD6 CDDBC8           CALL    0C8DBH
  EAD9 CDCCC8           CALL    0C8CCH
  EADC 3B               DB      ';'
  EADD 7E               LD      A,M
  EADE FEDF             CP      0DFH
  EAE0 381D             JR      C,PRAT2 ;<DFH
  EAE2 FEE3             CP      0E3H
  EAE4 3019             JR      NC,PRAT2;>E2H
  EAE6 CDE0E6           CALL    PREX4   ;INK,PAPER,COLOR
  EAE9 E5       PRAT1   PUSH    HL
  EAEA CD18F0           CALL    0F018H  ;IRMON
  EAED 21ECB9           LD      HL,WNDFN+80
  EAF0 119CB7           LD      DE,WINON
  EAF3 010600           LD      BC,6    ;Fenster
  EAF6 EDB0             LDIR            ;regenerieren
  EAF8 CD1BF0           CALL    0F01BH  ;IRMOF
  EAFB E1               POP     HL
  EAFC D1               POP     DE
  EAFD C1               POP     BC
  EAFE C9               RET
  EAFF CD03CB   PRAT2   CALL    0CB03H
  EB02 18E5             JR      PRAT1
  EB04           
  EB04 C348C3   SNERR3  JP      0C348H  ;SN-ERROR
  EB07           
  EB07 CD21D4   INK     CALL    0D421H
  EB0A FE20             CP      32
  EB0C 30F6             JR      NC,SNERR3
  EB0E CB27             SLA     A
  EB10 CB27             SLA     A
  EB12 CB27             SLA     A
  EB14 57               LD      D,A
  EB15 E5               PUSH    HL
  EB16 21A3B7           LD      HL,COLOR
  EB19 CDA2EC           CALL    IRMRD
  EB1C E1               POP     HL
  EB1D E607             AND     7
  EB1F 1812             JR      COL2
  EB21           
  EB21 CD21D4   PAPER   CALL    0D421H
  EB24 FE08     COL1    CP      8
  EB26 30DC             JR      NC,SNERR3
  EB28 57               LD      D,A
  EB29 E5               PUSH    HL
  EB2A 21A3B7           LD      HL,COLOR
  EB2D CDA2EC           CALL    IRMRD
  EB30 E1               POP     HL
  EB31 E6F8             AND     0F8H
  EB33 B2       COL2    OR      D
  EB34 E5               PUSH    HL
  EB35 21A3B7           LD      HL,COLOR
  EB38 CD9EEC           CALL    IRMWR   ;neuer Farbwert
  EB3B E1               POP     HL
  EB3C C9               RET
  EB3D           
  EB3D CDBEC8   BCOLOR  CALL    0C8BEH
  EB40 28C2             JR      Z,SNERR3
  EB42 CD07EB           CALL    INK     ;Vordergrund
  EB45 CDBEC8           CALL    0C8BEH
  EB48 C8               RET     Z
  EB49 CDD6C8           CALL    0C8D6H
  EB4C 18D3             JR      PAPER   ;Hintergrund
  EB4E C9               RET
  EB4F           
  EB4F          LOCAT   ;Cursor positionieren
  EB4F CDBDC8           CALL    0C8BDH
  EB52 CDCCC8           CALL    0C8CCH
  EB55 28               DB      '('
  EB56 CD21D4   LOCATE  CALL    0D421H
  EB59 57               LD      D,A
  EB5A E5               PUSH    HL
  EB5B 219FB7           LD      HL,WINLG+1
  EB5E CDA2EC           CALL    IRMRD
  EB61 E1               POP     HL
  EB62 3D               DEC     A
  EB63 BA               CP      D
  EB64 389E             JR      C,SNERR3
  EB66 CDD6C8           CALL    0C8D6H
  EB69 D5               PUSH    DE
  EB6A CD21D4           CALL    0D421H
  EB6D D1               POP     DE
  EB6E 4F               LD      C,A
  EB6F E5               PUSH    HL
  EB70 219EB7           LD      HL,WINLG
  EB73 CDA2EC           CALL    IRMRD
  EB76 E1               POP     HL
  EB77 3D               DEC     A
  EB78 B9               CP      C
  EB79 3889             JR      C,SNERR3
  EB7B 79               LD      A,C
  EB7C E5               PUSH    HL
  EB7D 21A0B7           LD      HL,CURSO
  EB80 CD9EEC           CALL    IRMWR
  EB83 7A               LD      A,D
  EB84 23               INC     HL
  EB85 CD9EEC   LOC1    CALL    IRMWR
  EB88 E1               POP     HL
  EB89 C9               RET
  EB8A           
  EB8A          INKEY   ;Eingabe ein Zeichen
  EB8A E5               PUSH    HL
  EB8B CDD1E3           CALL    KBDS
  EB8E 300F             JR      NC,INKEY3
  EB90 3E01             LD      A,1
  EB92 CD7BD1           CALL    0D17BH
  EB95 CDE4DD           CALL    0DDE4H  ;Eingabe ASCII
  EB98 2AC203   INKEY1  LD      HL,(3C2H)
  EB9B 77               LD      M,A
  EB9C C3A9D1   INKEY2  JP      0D1A9H
  EB9F AF       INKEY3  XOR     A
  EBA0 CD7BD1           CALL    0D17BH
  EBA3 18F7             JR      INKEY2
  EBA5           
  EBA5          SOUND   ;Tonausgabe
  EBA5 CD21D4           CALL    0D421H
  EBA8 E5               PUSH    HL
  EBA9 2182B7           LD      HL,ARG1
  EBAC 0604             LD      B,4     ;4 Argumente
  EBAE CD9EEC   SOUND1  CALL    IRMWR
  EBB1 23               INC     HL
  EBB2 E3               EX      (SP),HL
  EBB3 05               DEC     B
  EBB4 280B             JR      Z,SOUND2
  EBB6 C5               PUSH    BC
  EBB7 CDD6C8           CALL    0C8D6H
  EBBA CD21D4           CALL    0D421H
  EBBD C1               POP     BC
  EBBE E3               EX      (SP),HL
  EBBF 18ED             JR      SOUND1
  EBC1 CDBEC8   SOUND2  CALL    0C8BEH
  EBC4 281C             JR      Z,SOUND3
  EBC6 CDD6C8           CALL    0C8D6H
  EBC9 CD21D4           CALL    0D421H
  EBCC E3               EX      (SP),HL
  EBCD CD9EEC           CALL    IRMWR
  EBD0 23               INC     HL
  EBD1 E3               EX      (SP),HL
  EBD2 CDBEC8           CALL    0C8BEH
  EBD5 280B             JR      Z,SOUND3
  EBD7 CDD6C8           CALL    0C8D6H
  EBDA CD21D4           CALL    0D421H
  EBDD E3               EX      (SP),HL
  EBDE CD9EEC           CALL    IRMWR
  EBE1 E3               EX      (SP),HL
  EBE2 1E35     SOUND3  LD      E,35H   ;TON
  EBE4 C1               POP     BC
  EBE5 C315F0           JP      0F015H  ;PV5
  EBE8           
  EBE8          PSET    ;Punkt setzen
  EBE8 0601             LD      B,1     ;setzen
  EBEA C5       POINT   PUSH    BC
  EBEB CD6CC9           CALL    0C96CH
  EBEE E5               PUSH    HL
  EBEF 7B               LD      A,E
  EBF0 21D3B7           LD      HL,HOR
  EBF3 CD9EEC           CALL    IRMWR
  EBF6 7A               LD      A,D
  EBF7 23               INC     HL
  EBF8 CD9EEC           CALL    IRMWR
  EBFB E3               EX      (SP),HL
  EBFC CDD6C8           CALL    0C8D6H
  EBFF CD21D4           CALL    0D421H
  EC02 E3               EX      (SP),HL
  EC03 23               INC     HL
  EC04 CD9EEC           CALL    IRMWR
  EC07 E1               POP     HL
  EC08 CD1DEC           CALL    GFARB
  EC0B 1E30             LD      E,30H   ;PUSE
  EC0D C1               POP     BC
  EC0E 05               DEC     B
  EC0F 2801             JR      Z,POINT2
  EC11 1D               DEC     E       ;PUDE
  EC12 CD15F0   POINT2  CALL    0F015H  ;PV1
  EC15 DA76EA           JP      C,SNERR2
  EC18 C9               RET
  EC19           
  EC19          PRESET  ;Punkt l|schen
  EC19 0600             LD      B,0     ;l|schen
  EC1B 18CD             JR      POINT
  EC1D           
  EC1D CDBEC8   GFARB   CALL    0C8BEH
  EC20 C8               RET     Z
  EC21 CDD6C8           CALL    0C8D6H
  EC24 CD21D4           CALL    0D421H
  EC27 E5               PUSH    HL
  EC28 21D6B7           LD      HL,FARB ;Grafik-Farbe
  EC2B 17               RLA
  EC2C 17               RLA
  EC2D 17               RLA
  EC2E C385EB           JP      LOC1    ;in IRM schreib.
  EC31           
  EC31          INSTR   ;String1 in String2 suchen
  EC31 CD36CD           CALL    0CD36H  ;Klammer auf?
  EC34 CDD6C8           CALL    0C8D6H
  EC37 E5               PUSH    HL
  EC38 CD30D3           CALL    0D330H
  EC3B 2838             JR      Z,INSTR4
  EC3D 47               LD      B,A
  EC3E 23               INC     HL
  EC3F 23               INC     HL
  EC40 5E               LD      E,M
  EC41 23               INC     HL
  EC42 56               LD      D,M
  EC43 E1               POP     HL
  EC44 D5               PUSH    DE
  EC45 C5               PUSH    BC
  EC46 CD3ACD           CALL    0CD3AH
  EC49 CDDBC8           CALL    0C8DBH  ;Klammer zu?
  EC4C C1               POP     BC
  EC4D D1               POP     DE
  EC4E E5               PUSH    HL
  EC4F D5               PUSH    DE
  EC50 C5               PUSH    BC
  EC51 CD30D3           CALL    0D330H
  EC54 281F             JR      Z,INSTR4
  EC56 23               INC     HL
  EC57 23               INC     HL
  EC58 4E               LD      C,M
  EC59 23               INC     HL
  EC5A 66               LD      H,M
  EC5B 69               LD      L,C
  EC5C C1               POP     BC
  EC5D 4F               LD      C,A
  EC5E D1               POP     DE
  EC5F E5               PUSH    HL
  EC60 C5       INSTR0  PUSH    BC
  EC61 D5               PUSH    DE
  EC62 1A               LD      A,(DE)
  EC63 BE       INSTR1  CP      M
  EC64 2812             JR      Z,INSTR5
  EC66 23               INC     HL
  EC67 0D               DEC     C
  EC68 20F9             JR      NZ,INSTR1
  EC6A AF       INSTR2  XOR     A
  EC6B E1               POP     HL
  EC6C E1               POP     HL
  EC6D E1               POP     HL
  EC6E 11F3CD   INSTR3  LD      DE,0CDF3H ;RET-Adr.
  EC71 D5               PUSH    DE
  EC72 C3C0D0           JP      0D0C0H
  EC75 C367C9   INSTR4  JP      0C967H  ;FC-ERROR
  EC78 23       INSTR5  INC     HL
  EC79 E5               PUSH    HL
  EC7A 2B               DEC     HL
  EC7B 23       INSTR6  INC     HL
  EC7C 0D               DEC     C
  EC7D 280E             JR      Z,INSTR7
  EC7F 13               INC     DE
  EC80 05               DEC     B
  EC81 2811             JR      Z,INSTR8
  EC83 1A               LD      A,(DE)
  EC84 BE               CP      M
  EC85 28F4             JR      Z,INSTR6
  EC87 E1               POP     HL
  EC88 D1               POP     DE
  EC89 C1               POP     BC
  EC8A 0D               DEC     C
  EC8B 18D3             JR      INSTR0
  EC8D 13       INSTR7  INC     DE
  EC8E 05               DEC     B
  EC8F E1               POP     HL
  EC90 20D8             JR      NZ,INSTR2
  EC92 1801             JR      INSTR9
  EC94 E1       INSTR8  POP     HL
  EC95 D1       INSTR9  POP     DE
  EC96 D1               POP     DE
  EC97 D1               POP     DE
  EC98 A7               AND     A
  EC99 ED52             SBC     HL,DE
  EC9B 7D               LD      A,L
  EC9C 18D0             JR      INSTR3
  EC9E           
  EC9E 1E28     IRMWR   LD      E,28H   ;LDMA
  ECA0 1810             JR      JPV5
  ECA2           
  ECA2 1E29     IRMRD   LD      E,29H   ;LDAM
  ECA4 180C             JR      JPV5
  ECA6           
  ECA6          BKEY    ;F-Taste belegen
  ECA6 CD21D4           CALL    0D421H
  ECA9 A7               AND     A
  ECAA 2856             JR      Z,SNERR4
  ECAC FE0D             CP      0DH     ;F1...F12
  ECAE 3052             JR      NC,SNERR4
  ECB0 1E39             LD      E,39H   ;KEY
  ECB2 C315F0   JPV5    JP      0F015H  ;PV5
  ECB5           
  ECB5          KEYLIST ;F-Tasten auflisten
  ECB5 1E3A             LD      E,3AH   ;KEYLI
  ECB7 18F9             JR      JPV5
  ECB9           
  ECB9          SWITCH  ;Module schalten
  ECB9 CD21D4           CALL    0D421H
  ECBC F5               PUSH    AF
  ECBD CDD6C8           CALL    0C8D6H
  ECC0 CD21D4           CALL    0D421H
  ECC3 57               LD      D,A     ;Steuerbyte
  ECC4 F1               POP     AF
  ECC5 E5               PUSH    HL
  ECC6 6F               LD      L,A     ;Steckplatz
  ECC7 3E02             LD      A,2     ;schalten
  ECC9 1E26             LD      E,26H   ;MODU
  ECCB CD15F0           CALL    0F015H  ;PV5
  ECCE E1               POP     HL
  ECCF C9               RET
  ECD0           
  ECD0 CDE1CD   PTEST   CALL    0CDE1H
  ECD3 E3               EX      (SP),HL
  ECD4 11F3CD           LD      DE,0CDF3H ;RET-Adr.
  ECD7 D5               PUSH    DE
  ECD8 CD6FC9           CALL    0C96FH
  ECDB E5               PUSH    HL
  ECDC 7B               LD      A,E     ;nur
  ECDD 21D3B7           LD      HL,HOR  ;X-Koordinate
  ECE0 CD9EEC           CALL    IRMWR   ;eintragen
  ECE3 7A               LD      A,D
  ECE4 23               INC     HL
  ECE5 CD9EEC           CALL    IRMWR
  ECE8 1E2F             LD      E,2FH   ;PUDE
  ECEA CD15F0           CALL    0F015H  ;PV5
  ECED 0600             LD      B,0
  ECEF 280C             JR      Z,PTST1 ;war gel|scht
  ECF1 23               INC     HL
  ECF2 23               INC     HL
  ECF3 CD9EEC           CALL    IRMWR
  ECF6 1E30             LD      E,30H   ;PUSE
  ECF8 CD15F0           CALL    0F015H  ;PV5
  ECFB 0601             LD      B,1
  ECFD 78       PTST1   LD      A,B     ;R}ckgabewert
  ECFE E1               POP     HL
  ECFF C3E9D3           JP      0D3E9H
  ED02           
  ED02 C348C3   SNERR4  JP      0C348H  ;SN-ERROR
  ED05           
  ED05          CLOSE   ;Kanal schlie~en
  ED05 0E00             LD      C,0
  ED07 7E               LD      A,M
  ED08 FE49             CP      'I'     ;Eingabe?
  ED0A 2805             JR      Z,CLOS1
  ED0C 0C               INC     C
  ED0D FE4F             CP      'O'     ;Ausgabe?
  ED0F 20F1             JR      NZ,SNERR4
  ED11 C5       CLOS1   PUSH    BC
  ED12 23               INC     HL
  ED13 7E               LD      A,M
  ED14 FE23             CP      '#'
  ED16 20EA     CLOS2   JR      NZ,SNERR4
  ED18 23               INC     HL
  ED19 CD21D4           CALL    0D421H
  ED1C E603             AND     3       ;4 Ger{te
  ED1E C1               POP     BC
  ED1F C8               RET     Z       ;Console
  ED20 E5               PUSH    HL
  ED21 17               RLA             ;*2
  ED22 81               ADD     C       ;Ein-/Ausgabe
  ED23 F5               PUSH    AF
  ED24 3D               DEC     A
  ED25 0600             LD      B,0
  ED27 37               SCF
  ED28 CB10     CLOS3   RL      B       ;Bit platzieren
  ED2A 3D               DEC     A
  ED2B 20FB             JR      NZ,CLOS3
  ED2D 210703           LD      HL,307H ;E/A-Flag
  ED30 7E               LD      A,M
  ED31 A8               XOR     B       ;negieren
  ED32 77               LD      M,A
  ED33 F1               POP     AF
  ED34 E1               POP     HL
  ED35 CBF7             SET     6,A     ;close
  ED37 D5               PUSH    DE
  ED38 5F               LD      E,A
  ED39 1603             LD      D,3
  ED3B CD0EE0           CALL    BASIO
  ED3E D1               POP     DE
  ED3F C9               RET
  ED40           
  ED40          RANDOM  ;Zufallsgenerator
  ED40 ED5F             LD      A,R
  ED42 321D03           LD      (31DH),A
  ED45 C9               RET
  ED46           
  ED46          OPEN    ;Kanal |ffnen
  ED46 7E               LD      A,M
  ED47 23               INC     HL
  ED48 FE49             CP      'I'     ;Eingabe?
  ED4A 2819             JR      Z,OPEN1
  ED4C FE4F             CP      'O'     ;Ausgabe?
  ED4E 20C6             JR      NZ,CLOS2
  ED50 CD25DE           CALL    0DE25H
  ED53 CDC8DD           CALL    0DDC8H
  ED56 C8               RET     Z
  ED57 3ED5             LD      A,0D5H
  ED59 CDB2DC           CALL    0DCB2H
  ED5C 21EA03           LD      HL,3EAH ;Druckpuffer
  ED5F AF               XOR     A
  ED60 CDD5DD           CALL    0DDD5H
  ED63 E1               POP     HL
  ED64 C9               RET
  ED65 CD5FDE   OPEN1   CALL    0DE5FH  ;Eingabe
  ED68 3A0903           LD      A,(309H);IN-Index
  ED6B E603             AND     3
  ED6D C8               RET     Z
  ED6E 3ED5             LD      A,0D5H
  ED70 CDB2DC           CALL    0DCB2H
  ED73 21EA03           LD      HL,3EAH ;Druckuffer
  ED76 CDE4DD           CALL    0DDE4H  ;Eingabe ASCII
  ED79 E1               POP     HL
  ED7A C9               RET
  ED7B           
  ED7B          BLINE   ;Linie zeichnen
  ED7B 013E04           LD      BC,43EH ;4 Par.,LINE
  ED7E 1803             JR      GRAPH
  ED80           
  ED80          CIRCLE  ;Kreis zeichnen
  ED80 013F03           LD      BC,33FH ;3 Par.,CIRCLE
  ED83 C5       GRAPH   PUSH    BC
  ED84 CD6CC9           CALL    0C96CH
  ED87 C1               POP     BC
  ED88 C5               PUSH    BC
  ED89 E5               PUSH    HL
  ED8A 2182B7           LD      HL,ARG1
  ED8D 7B       GRAPH1  LD      A,E
  ED8E CD9EEC           CALL    IRMWR
  ED91 23               INC     HL
  ED92 7A               LD      A,D
  ED93 CD9EEC           CALL    IRMWR
  ED96 23               INC     HL
  ED97 05               DEC     B
  ED98 280C             JR      Z,GRAPH2
  ED9A E3               EX      (SP),HL
  ED9B C5               PUSH    BC
  ED9C CDD6C8           CALL    0C8D6H
  ED9F CD6CC9           CALL    0C96CH
  EDA2 C1               POP     BC
  EDA3 E3               EX      (SP),HL
  EDA4 18E7             JR      GRAPH1
  EDA6 E1       GRAPH2  POP     HL
  EDA7 CD1DEC           CALL    GFARB
  EDAA D1               POP     DE
  EDAB C315F0           JP      0F015H  ;PV5
  EDAE           
  EDAE          CSRLN   ;akt. Zeile holen
  EDAE CDE1CD           CALL    0CDE1H
  EDB1 E3               EX      (SP),HL
  EDB2 11F3CD           LD      DE,0CDF3H ;RET-Adr.
  EDB5 D5               PUSH    DE
  EDB6 CD24D4           CALL    0D424H
  EDB9 E5               PUSH    HL
  EDBA A7               AND     A
  EDBB 3E00             LD      A,0
  EDBD 2006             JR      NZ,CSRLN1
  EDBF 219DB7           LD      HL,WINON+1
  EDC2 CDA2EC           CALL    IRMRD
  EDC5 47       CSRLN1  LD      B,A
  EDC6 21A1B7           LD      HL,CURSO+1
  EDC9 CDA2EC           CALL    IRMRD
  EDCC 80               ADD     B       ;akt. Zeile
  EDCD E1               POP     HL
  EDCE C3E9D3           JP      0D3E9H
  EDD1           
  EDD1          VGET    ;Zeichen von Bildschirm lesen
  EDD1 E3               EX      (SP),HL
  EDD2 3E01             LD      A,1
  EDD4 CD7BD1           CALL    0D17BH
  EDD7 CD18F0           CALL    0F018H  ;IRMON
  EDDA ED5BA0B7         LD      DE,(CURSO)
  EDDE CD6AE0           CALL    DABR
  EDE1 7E               LD      A,M
  EDE2 CD1BF0           CALL    0F01BH  ;IRMOF
  EDE5 C398EB           JP      INKEY1  ;als Zeichen
  EDE8                                  ;zur}ckgeben
  EDE8          
  EDE8          ; Rest frei
  EDE8 000000           DB      0,0,0
  EDEB 4C5832           DB      'LX2'
  EDEE 00000000         DB      0,0,0,0
  EDF2 03C02099         DB      03H,0C0H,20H,99H,0C1H
  EDF7 0000             DB      0,0
  EDF9 4C5833           DB      'LX3'
  EDFC 000000           DB      0,0,0
  EDFF          
  EDFF 41               DB      41H     ;CAOS-Version
  EE00           
  EE00                  END

Errors: 0
End+1 : EE00 
                
  0200          
  0200          ;**     CAOS 4.1 ROM F          **
  0200           
  0200                  ORG     0F000H
  F000           
  F000 C3BFF0           JP      PWRON
  F003 C347F0           JP      PV1     ;m. DEFB nr
  F006 C363F0           JP      PV2     ;(ARGC)=nr
  F009 C377F0           JP      PV3     ;E=nr
  F00C C333F0           JP      PV4     ;E=nr, IRM e/a
  F00F C3B0F0           JP      RCALL   ;m. DEFW offset
  F012 C3F6F0           JP      BYE     ;JUMP-Einsprung
  F015 C327F0           JP      PV5     ;E=nr
  F018 C37AF0           JP      IRMON
  F01B C399F0           JP      IRMOF
  F01E C5               PUSH    BC      ;PV6, (ARGC)=nr
  F01F CD7AF0           CALL    IRMON
  F022 CD63F0           CALL    PV2
  F025 1807             JR      PV51
  F027           
  F027 C5       PV5     PUSH    BC
  F028 CD7AF0           CALL    IRMON
  F02B CD77F0           CALL    PV3
  F02E CD99F0   PV51    CALL    IRMOF
  F031 C1               POP     BC
  F032 C9               RET
  F033           
  F033 F5       PV4     PUSH    AF
  F034 DB88             IN      A,88H
  F036 CBD7             SET     2,A
  F038 D388             OUT     88H     ;IRM ein
  F03A F1               POP     AF
  F03B CD77F0           CALL    PV3
  F03E F5       SCROFF  PUSH    AF
  F03F DB88             IN      A,88H
  F041 CB97             RES     2,A
  F043 D388             OUT     88H     ;IRM aus
  F045 F1               POP     AF
  F046 C9               RET
  F047           
  F047 F3       PV1     DI
  F048 E5               PUSH    HL
  F049 E1               POP     HL
  F04A E1               POP     HL
  F04B 23               INC     HL      ;nr }bergehen
  F04C E5               PUSH    HL
  F04D 2B               DEC     HL      ;(HL)=nr
  F04E 3B               DEC     SP
  F04F 3B               DEC     SP
  F050 FB               EI
  F051 F5               PUSH    AF
  F052 D5               PUSH    DE
  F053 5E               LD      E,M
  F054 1600     PV22    LD      D,0
  F056 2AB0B7           LD      HL,(SUTAB)
  F059 19               ADD     HL,DE
  F05A 19               ADD     HL,DE   ;HL=Pos. in Tab
  F05B 5E               LD      E,M
  F05C 23               INC     HL
  F05D 56               LD      D,M
  F05E EB               EX      DE,HL
  F05F D1               POP     DE
  F060 F1               POP     AF
  F061 E3       EXRT    EX      (SP),HL
  F062 C9               RET             ;UP anspringen
  F063           
  F063 A7       PV2     AND     A       ;VR: F
  F064 E5       PV21    PUSH    HL
  F065 D5               PUSH    DE
  F066 C5               PUSH    BC
  F067 E5               PUSH    HL
  F068 21BBF0           LD      HL,POPS3;Returnadresse
  F06B E3               EX      (SP),HL ;kellern
  F06C E5               PUSH    HL
  F06D F5               PUSH    AF
  F06E D5               PUSH    DE
  F06F 38E3             JR      C,PV22  ;falls PV3
  F071 3A80B7           LD      A,(ARGC);sonst E
  F074 5F               LD      E,A     ;beschaffen
  F075 18DD             JR      PV22
  F077           
  F077 37       PV3     SCF             ;VR: F
  F078 18EA             JR      PV21
  F07A           
  F07A C1       IRMON   POP     BC      ;R}ckkehradr.
  F07B FDE5             PUSH    IY
  F07D FD210000         LD      IY,0    ;SP in IY merken
  F081 FD39             ADD     IY,SP   ;(kein UP ver-
  F083 F3               DI              ;{ndert IY!)
  F084 DD770B           LD      (IX+11),A
  F087 DB88             IN      A,88H
  F089 CBD7             SET     2,A     ;IRM ein
  F08B CBEF             SET     5,A     ;LED ein
  F08D D388             OUT     88H
  F08F ED7BAEB7         LD      SP,(SYSP)
  F093 FB               EI
  F094 DD7E0B           LD      A,(IX+11)
  F097 C5               PUSH    BC
  F098 C9               RET             ;wie JP (BC)
  F099           
  F099 C1       IRMOF   POP     BC      ;analog IRMON
  F09A DD770B           LD      (IX+11),A
  F09D DB88             IN      A,88H
  F09F CB97             RES     2,A
  F0A1 CBAF             RES     5,A
  F0A3 F3               DI
  F0A4 D388             OUT     88H
  F0A6 FDF9             LD      SP,IY
  F0A8 FB               EI
  F0A9 DD7E0B           LD      A,(IX+11)
  F0AC FDE1             POP     IY
  F0AE C5               PUSH    BC
  F0AF C9               RET
  F0B0           
  F0B0          RCALL   ;Relativer UP-Aufruf
  F0B0 E3               EX      (SP),HL ;VR: DE
  F0B1 5E               LD      E,M
  F0B2 23               INC     HL
  F0B3 56               LD      D,M
  F0B4 23               INC     HL      ;DE=offset
  F0B5 EB               EX      DE,HL
  F0B6 19               ADD     HL,DE   ;zur Absolut-
  F0B7 EB               EX      DE,HL   ;adr. addieren
  F0B8 E3               EX      (SP),HL
  F0B9 D5               PUSH    DE
  F0BA C9               RET             ;wie JP (DE)
  F0BB           
  F0BB C1       POPS3   POP     BC      ;f}r PV2..6
  F0BC D1               POP     DE
  F0BD E1               POP     HL
  F0BE C9               RET
  F0BF           
  F0BF          PWRON   ;Einschalt-Init
  F0BF 31C401           LD      SP,1C4H
  F0C2 AF               XOR     A
  F0C3 47               LD      B,A
  F0C4 4F               LD      C,A
  F0C5 02       PWR1    LD      (BC),A  ;Speicher
  F0C6 0D               DEC     C       ;l|schen
  F0C7 20FC             JR      NZ,PWR1
  F0C9 10FA             DJNZ    PWR1
  F0CB 0E80             LD      C,80H   ;Module
  F0CD ED79     PWR2    OUT     (C),A   ;abschalten
  F0CF 10FC             DJNZ    PWR2
  F0D1 CDD0F3           CALL    SYSI    ;IRM-Init usw.
  F0D4 AF               XOR     A
  F0D5 329BB7   PWR3    LD      (WINNR),A
  F0D8 CD94F6           CALL    WCOPY   ;Fenstervektor-
  F0DB 3C               INC     A       ;speicher init.
  F0DC FE0A             CP      0AH
  F0DE 38F5             JR      C,PWR3
  F0E0 CD85F9           CALL    ESC3
  F0E3 3E0C             LD      A,0CH   ;Zweitbild
  F0E5 CD29E3           CALL    CRT     ;l|schen
  F0E8 CD67F9           CALL    ESC1
  F0EB CD5AFB           CALL    CON     ;M003 suchen,
  F0EE CDBCC6           CALL    V24INI  ;einschalten,
  F0F1 CD66FB           CALL    COFF    ;Tab. kopieren
  F0F4 1806             JR      PWR4
  F0F6           
  F0F6          BYE     ;Tasten-RESET
  F0F6                  ;M003 wird bei RESET nicht
  F0F6                  ;erneut gesucht!
  F0F6           
  F0F6 31C401           LD      SP,1C4H
  F0F9 CDD0F3           CALL    SYSI    ;IRM-Init usw.
  F0FC                  ;
  F0FC 3A00A8   PWR4    LD      A,(0A800H)
  F0FF 3D               DEC     A       ;M003 vorh.?
  F100 200C             JR      NZ,PWR5
  F102 CD5AFB           CALL    CON
  F105 CDB8C2           CALL    V24OC   ;Druckerinit
  F108 CDA6C6           CALL    UMINIT  ;SIO B f}r
  F10B CD66FB           CALL    COFF    ;Interrupt
  F10E 018008   PWR5    LD      BC,880H ;ROM-Modul mit
  F111 ED78             IN      A,(C)   ;Strukturbyte
  F113 3D               DEC     A       ;01?
  F114 201C             JR      NZ,MEN0 ;nein
  F116 3E43             LD      A,43H
  F118 ED79             OUT     (C),A   ;ein ab 4000H
  F11A 3208B8           LD      (0B808H),A
  F11D DD7E04           LD      A,(IX+4)
  F120 E6FC             AND     0FCH    ;RAM4 aus
  F122 DD7704           LD      (IX+4),A
  F125 D386             OUT     86H
  F127 C30040           JP      4000H   ;anspringen
  F12A           
  F12A          ;
  F12A 7F7F             DW      7F7FH
  F12C 4D454E55         DB      'MENU',1
  F131 E1               POP     HL      ;Stack clear
  F132 CDEEF1   MEN0    CALL    OSTR
  F135 0C0A             DB      0CH,0AH ;CLS,CUD
  F137 2A204B43         DB      '* KC-CAOS 4.1 *',0
  F147 2100C0   MENU    LD      HL,0C000H       ;**46**
  F14A 4D               LD      C,L
  F14B 45               LD      B,L     ;BC=0
  F14C CD6BF3   MEN1    CALL    CRLF
  F14F CDEEF1   MEN2    CALL    OSTR
  F152 022500           DB      2,'%',0 ;CLLN,Prompt
  F155 CDE5E3           CALL    BRKT
  F158 3829             JR      C,LOOP3
  F15A DD7E09           LD      A,(IX+9)
  F15D EDB1     MEN3    CPIR
  F15F E283F1           JP      PO,LOOP3;ausgesucht.
  F162 EDA1             CPI             ;2tes Mal?
  F164 20F7             JR      NZ,MEN3
  F166 7E       MEN4    LD      A,M
  F167 FE02             CP      2       ;Epilog?
  F169 38E1             JR      C,MEN1
  F16B FE30             CP      30H     ;<30h?
  F16D 38E0             JR      C,MEN2
  F16F FE60             CP      60H     ;>60h, Kleinb.?
  F171 30DC             JR      NC,MEN2
  F173 CD1DF2           CALL    OCHR
  F176 23               INC     HL
  F177 0B               DEC     BC
  F178 18EC             JR      MEN4
  F17A           
  F17A E1       LOOP1   POP     HL
  F17B CD61F3   LOOP2   CALL    ERRM
  F17E CDEEF1   LOOP    CALL    OSTR            ;**12**
  F181 2500             DB      '%',0   ;Prompt
  F183 CD34F3   LOOP3   CALL    INLIN
  F186 13               INC     DE      ;Zeichen danach
  F187 1A               LD      A,(DE)
  F188 FE20             CP      ' '
  F18A 28F2             JR      Z,LOOP
  F18C A7               AND     A
  F18D 28EF             JR      Z,LOOP
  F18F CDA9F1           CALL    MSUCH
  F192 30E7             JR      NC,LOOP2;nicht gef.
  F194 E5               PUSH    HL
  F195 CDC7F3           CALL    GARG    ;Argumente
  F198 38E0             JR      C,LOOP1
  F19A 217EF1           LD      HL,LOOP ;R}ckkehradr.
  F19D E3               EX      (SP),HL ;kellern
  F19E 7E               LD      A,M     ;A=Epilog
  F19F 23               INC     HL      ;}bergehen
  F1A0 E5               PUSH    HL
  F1A1 A7               AND     A
  F1A2 CDCDF4           CALL    LARG
  F1A5 C0               RET     NZ      ;A=1: nach (HL)
  F1A6 C33EF0           JP      SCROFF  ;A=0: IRM aus,
  F1A9                                  ;dann nach (HL)
  F1A9           
  F1A9 2100C0   MSUCH   LD      HL,0C000H
  F1AC 45               LD      B,L
  F1AD 4D               LD      C,L     ;BC=0
  F1AE DD7E09           LD      A,(IX+9)
  F1B1 EDB1     ZSUCH   CPIR                    ;**1D**
  F1B3 37               SCF
  F1B4 3F               CCF
  F1B5 E0               RET     PO      ;nicht gef.
  F1B6 EDA1             CPI
  F1B8 20F7             JR      NZ,ZSUCH
  F1BA F5               PUSH    AF
  F1BB D5               PUSH    DE
  F1BC 1A       ZS1     LD      A,(DE)
  F1BD FE21             CP      '!'     ;keine SPC und
  F1BF 3809             JR      C,ZS3   ;Steuerzeichen
  F1C1 13               INC     DE
  F1C2 EDA1             CPI
  F1C4 28F6             JR      Z,ZS1
  F1C6 D1       ZS2     POP     DE
  F1C7 F1               POP     AF
  F1C8 18E7             JR      ZSUCH
  F1CA                  ;
  F1CA 7E       ZS3     LD      A,M
  F1CB FE02             CP      2       ;Epilog?
  F1CD 3807             JR      C,ZS4   ;ja
  F1CF FE20             CP      ' '
  F1D1 38F3             JR      C,ZS2
  F1D3 23               INC     HL
  F1D4 18F4             JR      ZS3
  F1D6                  ;
  F1D6 F1       ZS4     POP     AF
  F1D7 F1               POP     AF
  F1D8 37               SCF
  F1D9 C9               RET             ;gefunden
  F1DA           
  F1DA 3D       WTUP    DEC     A       ;UP f}r WAIT
  F1DB C8               RET     Z
  F1DC F5               PUSH    AF
  F1DD F1               POP     AF
  F1DE 18FA             JR      WTUP
  F1E0           
  F1E0 47       WAIT    LD      B,A             ;**14**
  F1E1 AF               XOR     A
  F1E2 CDDAF1   WAIT1   CALL    WTUP
  F1E5 10FB             DJNZ    WAIT1
  F1E7 C9               RET
  F1E8           
  F1E8 DD7E02   BNROST  LD      A,(IX+2);Blocknummer u.
  F1EB CD76F3   AHOSTR  CALL    AHEX    ;RG A und folg.
  F1EE E3       OSTR    EX      (SP),HL ;ZK      **23**
  F1EF 7E       OSTR1   LD      A,M     ;ausgeben
  F1F0 23               INC     HL
  F1F1 A7               AND     A       ;bis 00-Byte
  F1F2 CA61F0           JP      Z,EXRT
  F1F5 CD1DF2           CALL    OCHR
  F1F8 18F5             JR      OSTR1
  F1FA           
  F1FA CD0AF2   NORM    CALL    NIN             ;**13**
  F1FD 2192FB   NOUT    LD      HL,ZEI0         ;**20**
  F200 E5       SOUT    PUSH    HL              ;**1E**
  F201 2AB9B7           LD      HL,(OUTAB)
  F204 E3               EX      (SP),HL
  F205 22B9B7           LD      (OUTAB),HL
  F208 E1               POP     HL
  F209 C9               RET
  F20A           
  F20A 2196FB   NIN     LD      HL,ZEI4         ;**21**
  F20D E5       SIN     PUSH    HL              ;**1F**
  F20E 2ABBB7           LD      HL,(INTAB)
  F211 E3               EX      (SP),HL
  F212 22BBB7           LD      (INTAB),HL
  F215 E1               POP     HL
  F216 C9               RET
  F217           
  F217 E5       INTB    PUSH    HL              ;**16**
  F218 2ABBB7           LD      HL,(INTAB)
  F21B 1804             JR      INTA1
  F21D E5       OCHR    PUSH    HL              ;**24**
  F21E 2AB9B7           LD      HL,(OUTAB)
  F221 D5       INTA1   PUSH    DE      ;s.a. PV1
  F222 C5               PUSH    BC      ;(sehr {hnlich)
  F223 5E               LD      E,M
  F224 1600             LD      D,0
  F226 2AB0B7           LD      HL,(SUTAB)
  F229 F5               PUSH    AF
  F22A 19               ADD     HL,DE
  F22B 19               ADD     HL,DE
  F22C F1               POP     AF
  F22D 5E               LD      E,M
  F22E 23               INC     HL
  F22F 56               LD      D,M
  F230 EB               EX      DE,HL
  F231 CD99E1           CALL    JPHL
  F234 C1       POP3    POP     BC
  F235 D1               POP     DE
  F236 E1               POP     HL
  F237 C9               RET
  F238           
  F238 E5       KBD     PUSH    HL              ;**04**
  F239 D5               PUSH    DE
  F23A C5               PUSH    BC
  F23B DDCB0876 KBD1    BIT     6,(IX+8);F-Taste aktiv?
  F23F 2076             JR      NZ,KBD12;ja
  F241 ED5BA0B7 KBD2    LD      DE,(CURSO)
  F245 CD6AE0           CALL    DABR
  F248 AF               XOR     A       ;Dummy
  F249 BE               CP      M       ;hinter Cursor?
  F24A 2813             JR      Z,KBD5  ;ja-Strich
  F24C 5F               LD      E,A     ;nein-Klotz,E=0
  F24D CD23F7   KBD3    CALL    CUCP    ;blinken
  F250 1C               INC     E       ;Klotz e/a-Merk
  F251 060F             LD      B,0FH   ;F00h mal
  F253 CDDCE3   KBD4    CALL    KBDZ    ;abfragen
  F256 381B             JR      C,KBD7
  F258 0B               DEC     BC
  F259 78               LD      A,B
  F25A B1               OR      C
  F25B 20F6             JR      NZ,KBD4
  F25D 18EE             JR      KBD3    ;dann Cursor
  F25F                                  ;wieder weg usw.
  F25F CD40E0   KBD5    CALL    PADR0
  F262 CBCD             SET     1,L     ;vorletzte
  F264 CBD5             SET     2,L     ;Cursorzeile
  F266 7E               LD      A,M
  F267 47               LD      B,A     ;merken
  F268 EE7F             XOR     7FH
  F26A 77               LD      M,A
  F26B CDDCE3   KBD6    CALL    KBDZ    ;abfragen
  F26E 30FB             JR      NC,KBD6
  F270 70               LD      M,B     ;r}cksetzen
  F271 1805             JR      KBD8
  F273           
  F273 CB43     KBD7    BIT     0,E     ;Klotz gerade
  F275                                  ;ein?
  F275 C423F7           CALL    NZ,CUCP ;ja - wegnehmen
  F278 57       KBD8    LD      D,A
  F279 DDCB086E         BIT     5,(IX+8);Click ein?
  F27D 010703           LD      BC,307H
  F280 C405E3           CALL    NZ,CLIK
  F283 7A               LD      A,D
  F284 21A2B7           LD      HL,STBT
  F287 CB66             BIT     4,M     ;ESC aktiv?
  F289 280D             JR      Z,KBD9
  F28B ED5BA0B7         LD      DE,(CURSO)
  F28F CD63E1           CALL    CRT1
  F292 ED53A0B7         LD      (CURSO),DE
  F296 18A9             JR      KBD2
  F298           
  F298 FE1B     KBD9    CP      1BH     ;ESC?
  F29A 2004             JR      NZ,KBD10
  F29C CBE6             SET     4,M     ;ja
  F29E 18A1             JR      KBD2
  F2A0           
  F2A0 FEF1     KBD10   CP      0F1H    ;F-Taste?
  F2A2 3890             JR      C,POP3
  F2A4 DDCB08F6         SET     6,(IX+8);ja - merken
  F2A8 E60F             AND     0FH
  F2AA 47               LD      B,A
  F2AB 2100B9           LD      HL,0B900H
  F2AE 7E       KBD11   LD      A,M     ;Pos. in F-
  F2AF A7               AND     A       ;Puffer best.
  F2B0 23               INC     HL
  F2B1 20FB             JR      NZ,KBD11
  F2B3 10F9             DJNZ    KBD11
  F2B5 1808             JR      KBD13
  F2B7           
  F2B7 CDE5E3   KBD12   CALL    BRKT    ;Abbruch?
  F2BA 381B             JR      C,KBD15
  F2BC 2AD1B7           LD      HL,(FTAST);Zeiger auf
  F2BF 7E       KBD13   LD      A,M     ;n{chstes CHR
  F2C0 FE1B             CP      1BH     ;ESC?
  F2C2 200C             JR      NZ,KBD14
  F2C4 23               INC     HL
  F2C5 22D1B7           LD      (FTAST),HL
  F2C8 21A2B7           LD      HL,STBT
  F2CB CBE6             SET     4,M     ;ESC merken
  F2CD C33BF2           JP      KBD1
  F2D0           
  F2D0 23       KBD14   INC     HL
  F2D1 22D1B7           LD      (FTAST),HL;Zeiger rett.
  F2D4 A7               AND     A       ;Null-Ende?
  F2D5 2007             JR      NZ,KBD16
  F2D7 DDCB08B6 KBD15   RES     6,(IX+8);ja - inaktiv
  F2DB C341F2           JP      KBD2
  F2DE           
  F2DE 21A2B7   KBD16   LD      HL,STBT
  F2E1 CB66             BIT     4,M
  F2E3 CA34F2           JP      Z,POP3
  F2E6 ED5BA0B7         LD      DE,(CURSO)
  F2EA CD63E1           CALL    CRT1    ;ESC-Seq. ausf.
  F2ED ED53A0B7         LD      (CURSO),DE
  F2F1 C33BF2           JP      KBD1
  F2F4           
  F2F4          KCTAB   ;IRM-Defaulttabelle
  F2F4 22E3             DW      NOT     ;Hardcopy
  F2F6 00               DB      0       ;WINNR
  F2F7 0000             DW      0       ;WINON
  F2F9 2820             DW      2028H   ;WINLG
  F2FB 0000             DW      0       ;CURSO
  F2FD 00               DB      0       ;STBT
  F2FE 39               DB      39H     ;COLOR
  F2FF 60E2             DW      SCRLPG  ;WEND
  F301 00EE             DW      0EE00H  ;CCTL0
  F303 00FE             DW      0FE00H  ;1
  F305 00EE             DW      0EE00H  ;2
  F307 00FE             DW      0FE00H  ;3
  F309 C401             DW      1C4H    ;SYSP
  F30B B0FB             DW      SUTB    ;SUTAB
  F30D D0FC             DW      CRTTAB  ;CTAB
  F30F D388             OUT     88H     ;BJUMP
  F311 C312F0           JP      0F012H
  F314 92FB             DW      ZEI0    ;OUTAB
  F316 96FB             DW      ZEI4    ;INTAB
  F318 C353F3           JP      NOOP    ;UOUT1
  F31B C353F3           JP      NOOP    ;UIN1
  F31E C353F3           JP      NOOP    ;UOUT2
  F321 C353F3           JP      NOOP    ;UIN2
  F324 82DC             DW      0DC82H  ;IOERR (BASIC)
  F326 00B2             DW      0B200H  ;VRAM
  F328           
  F328 9AFB             DW      L3TB    ;L3TAB
  F32A 0B               DB      11      ;L3SIZ
  F32B 05               DB      5       ;COUNT
  F32C 00               DB      0       ;HCPZ
  F32D 01A8             DW      0A801H  ;INTV1
  F32F 08               DB      8       ;INTV1L
  F330 09A8             DW      0A809H  ;INTV2
  F332 09               DB      9       ;INTV2L
  F333 06               DB      6       ;HCPZ2
  F334           
  F334 CD17F2   INLIN   CALL    INTB            ;**17**
  F337 CD1DF2           CALL    OCHR
  F33A FE0D             CP      0DH     ;ENTER?
  F33C 20F6             JR      NZ,INLIN
  F33E CD6BF3           CALL    CRLF    ;PA: A=0
  F341 ED5BA0B7         LD      DE,(CURSO)
  F345 BA               CP      D       ;1 Zeile durch
  F346                                  ;PAGE-Mode?
  F346 2004             JR      NZ,INLI1
  F348 3A9FB7           LD      A,(WINLG+1)
  F34B 57               LD      D,A     ;D korrigieren
  F34C 15       INLI1   DEC     D       ;Zeile dar}ber
  F34D E5               PUSH    HL
  F34E CD6AE0           CALL    DABR
  F351 EB               EX      DE,HL
  F352 E1               POP     HL      ;PA: DE
  F353 C9       NOOP    RET
  F354           
  F354 7C       HLHX    LD      A,H             ;**1A**
  F355 CD76F3           CALL    AHEX
  F358 7D               LD      A,L
  F359 CD76F3   AHSPC   CALL    AHEX
  F35C 3E20     SPACE   LD      A,20H           ;**2B**
  F35E C31DF2   SPAC0   JP      OCHR
  F361           
  F361 CDEEF1   ERRM    CALL    OSTR            ;**17**
  F364 4552524F         DB      'ERROR',7,0
  F36B                  ;
  F36B CDEEF1   CRLF    CALL    OSTR            ;**2C**
  F36E 0D0A00           DB      0DH,0AH,0
  F371 C9               RET
  F372           
  F372 3E10     HOME    LD      A,10H           ;**2D**
  F374 18E8             JR      SPAC0
  F376           
  F376 F5       AHEX    PUSH    AF              ;**1C**
  F377 1F               RRA             ;Tetradentausch
  F378 1F               RRA
  F379 1F               RRA
  F37A 1F               RRA
  F37B CD7FF3           CALL    AHEX0   ;erst rufen,
  F37E F1               POP     AF      ;dann hinein-
  F37F                                  ;laufen
  F37F E60F     AHEX0   AND     0FH     ;Maske
  F381 C690             ADD     90H     ;}bliche
  F383 27               DAA             ;Routine
  F384 CE40             ADC     40H     ;f}r die
  F386 27               DAA             ;Hexausgabe
  F387 18D5             JR      SPAC0
  F389           
  F389 CD8CF3   HLDE    CALL    HLDE1           ;**1B**
  F38C CD54F3   HLDE1   CALL    HLHX
  F38F EB               EX      DE,HL
  F390 C9               RET
  F391           
  F391 13       RHEX0   INC     DE
  F392 1A       RHEX    LD      A,(DE)          ;**18**
  F393 FE20             CP      ' '     ;Trennzeichen
  F395 28FA             JR      Z,RHEX0 ;}bergehen
  F397 AF               XOR     A
  F398 2198B7           LD      HL,NUMVX+1
  F39B 77               LD      M,A
  F39C 2B               DEC     HL      ;HL=NUMVX
  F39D 77               LD      M,A
  F39E 2B               DEC     HL      ;HL=NUMNX
  F39F 77               LD      M,A     ;alles nullsetz.
  F3A0 1A       RH1     LD      A,(DE)
  F3A1 B7               OR      A       ;Ende-Dummy?
  F3A2 C8               RET     Z
  F3A3 FE20             CP      ' '     ;Ende-Space?
  F3A5 C8               RET     Z
  F3A6 D630             SUB     '0'
  F3A8 D8               RET     C
  F3A9 FE0A             CP      0AH
  F3AB 380B             JR      C,RH2
  F3AD D607             SUB     7
  F3AF E6DF             AND     0DFH    ;klein>gro~
  F3B1 FE0A             CP      0AH
  F3B3 D8               RET     C
  F3B4 FE10             CP      10H
  F3B6 3F               CCF
  F3B7 D8               RET     C
  F3B8 13       RH2     INC     DE
  F3B9 34               INC     M       ;NUMNX erh|hen
  F3BA 23               INC     HL
  F3BB ED6F             RLD             ;Tetrade
  F3BD 23               INC     HL      ;durchschieben
  F3BE ED6F             RLD
  F3C0 2B               DEC     HL
  F3C1 2B               DEC     HL      ;RLD setzt auch
  F3C2 28DC             JR      Z,RH1   ;Z-Flag!
  F3C4 1B               DEC     DE      ;Fehler: Zahl
  F3C5 37               SCF             ;zu gro~
  F3C6 C9               RET
  F3C7           
  F3C7 CD5AFB   GARG    CALL    CON             ;**22**
  F3CA CDF1C6           CALL    GARGC
  F3CD C366FB           JP      COFF
  F3D0           
  F3D0 1199B7   SYSI    LD      DE,HCADR
  F3D3 21F4F2           LD      HL,KCTAB
  F3D6 013400           LD      BC,34H
  F3D9 EDB0             LDIR            ;1. St}ck
  F3DB 0E0C             LD      C,0CH
  F3DD 1EDD             LD      E,L3TAB
  F3DF EDB0             LDIR            ;2. St}ck
  F3E1 210301           LD      HL,103H ;RAM0,IRM,
  F3E4 2200B8           LD      (0B800H),HL
  F3E7 210103           LD      HL,301H ;ROMC,RAM8,
  F3EA 2202B8           LD      (0B802H),HL
  F3ED 3E03             LD      A,3     ;RAM4
  F3EF 3204B8           LD      (0B804H),A
  F3F2 3E01             LD      A,1
  F3F4                  ;
  F3F4 21C2FC   SIXD    LD      HL,ISRTAB       ;**31**
  F3F7 32D7B7           LD      (MIXIT),A
  F3FA 1EE2             LD      E,0E2H
  F3FC 57               LD      D,A
  F3FD 010E00           LD      BC,0EH  ;Interrupttab.
  F400 EDB0             LDIR            ;kopieren
  F402          ; bei CAOS 4.2 werden hier die alten Merker
  F402          ; von (IX+1) und (IX+4) mitgenommen
  F402 D5               PUSH    DE
  F403 DDE1             POP     IX
  F405 DD360888         LD      (IX+8),88H
  F409 DD360128         LD      (IX+1),28H;Grundstellung
  F40D DD360463         LD      (IX+4),63H;der Merker
  F411 DD360E42         LD      (IX+14),KTAB;=L(KTAB)
  F415 DD360FFC         LD      (IX+15),0FCH;=H(KTAB)
  F419 DD36097F         LD      (IX+9),7FH
  F41D F5               PUSH    AF
  F41E D5               PUSH    DE
  F41F E5               PUSH    HL
  F420 F3               DI
  F421 ED5E             IM      2
  F423 3AD7B7           LD      A,(MIXIT)
  F426 ED47             LD      I,A
  F428 1609             LD      D,9     ;9 Kan{le
  F42A 214EF4           LD      HL,IOTAB
  F42D CD45F4           CALL    INIME   ;Kan{le init.
  F430 E1               POP     HL
  F431 D1               POP     DE
  F432 F1               POP     AF
  F433                  ;
  F433 DD360500 KPUFF   LD      (IX+5),0
  F437 DD3606B7         LD      (IX+6),0B7H
  F43B C9               RET
  F43C           
  F43C C5       INIEA   PUSH    BC              ;**43**
  F43D 4E               LD      C,M     ;1. Byte=Adr.
  F43E 23               INC     HL
  F43F 46               LD      B,M     ;2. Byte=Anz.
  F440 23               INC     HL
  F441 EDB3             OTIR
  F443 C1               POP     BC
  F444 C9               RET
  F445           
  F445 F3       INIME   DI                      ;**44**
  F446 CD3CF4   INIME1  CALL    INIEA
  F449 15               DEC     D       ;D Kan{le
  F44A 20FA             JR      NZ,INIME1
  F44C FB               EI
  F44D C9               RET
  F44E           
  F44E 8A       IOTAB   DB      8AH     ;PIO-A-Steuerw.
  F44F 01                DB     1
  F450 E4                 DB    0E4H    ;Int-Vektor
  F451 88               DB      88H     ;PIO-A-Daten
  F452 01                DB     1
  F453 0F                 DB    0FH     ;IRM ein...
  F454 8A               DB      8AH     ;PIO-A-Steuerw.
  F455 02                DB     2
  F456 0F                 DB    0FH     ;Byte-Out
  F457 03                 DB    3       ;DI
  F458 8B               DB      8BH     ;PIO-B-Steuerw.
  F459 03                DB     3
  F45A E6                 DB    0E6H    ;Int-Vektor
  F45B 0F                 DB    0FH     ;Byte-Out
  F45C 83                 DB    83H     ;EI
  F45D 89               DB      89H     ;PIO-B-Daten
  F45E 01                DB     1
  F45F FF                 DB    0FFH    ;Daten
  F460 8C               DB      8CH     ;CTC0
  F461 01                DB     1
  F462 E8                 DB    0E8H    ;Int-Vektor CTC
  F463 8E               DB      8EH     ;CTC3
  F464 02                DB     2
  F465 47                 DB    47H     ;DI,Z{hler,Reset
  F466 0C                 DB    0CH     ;Zeitkonst.
  F467 84               DB      84H     ;Port 84H
  F468 01                DB     1
  F469 28                 DB    28H     ;Ausgabebyte
  F46A 86               DB      86H     ;Port 86H
  F46B 01                DB     1
  F46C 63                 DB    63H     ;Ausgabebyte
  F46D          ;
  F46D 7F7F             DW      7F7FH
  F46F 53415645         DB      'SAVE',1
  F474 FE02             CP      2       ;mind. 2 Arg.
  F476 DA61F3           JP      C,ERRM
  F479 CDEEF1           CALL    OSTR
  F47C 4E414D45         DB      'NAME :',0
  F483 CD34F3           CALL    INLIN
  F486 210600           LD      HL,6    ;'NAME :'
  F489 19               ADD     HL,DE   ;}bergehen
  F48A                  ;
  F48A 1100B7   SAVE    LD      DE,0B700H       ;**36**
  F48D 010B00           LD      BC,11   ;Name nach
  F490 EDB0             LDIR            ;Puffer
  F492 AF               XOR     A
  F493 12               LD      (DE),A
  F494 2181B7           LD      HL,ARGN ;alle Arg.
  F497 1E10             LD      E,10H   ;mit ARGN
  F499 0E15             LD      C,15H
  F49B EDB0             LDIR            ;nach Puffer
  F49D CD20E5           CALL    ISRO
  F4A0 2A82B7           LD      HL,(ARG1)
  F4A3 DD7505   SAV1    LD      (IX+5),L
  F4A6 DD7406           LD      (IX+6),H
  F4A9 CDEEF1           CALL    OSTR
  F4AC 0200             DB      2,0     ;CLLN
  F4AE CDE8F1           CALL    BNROST  ;Blocknummer
  F4B1 00               NOP
  F4B2 01A000           LD      BC,0A0H
  F4B5 CDE5E3           CALL    BRKT
  F4B8 DAE6E4           JP      C,CSROI
  F4BB 118000           LD      DE,80H
  F4BE 19               ADD     HL,DE
  F4BF ED5B84B7         LD      DE,(ARG2)
  F4C3 ED52             SBC     HL,DE   ;Ende erreicht?
  F4C5 D2DFE4           JP      NC,CSRO
  F4C8 CD2FE5           CALL    MBO
  F4CB 18D6             JR      SAV1
  F4CD           
  F4CD ED4B86B7 LARG    LD      BC,(ARG3)       ;**15**
  F4D1 ED5B84B7         LD      DE,(ARG2)
  F4D5 2A82B7           LD      HL,(ARG1)
  F4D8 3A81B7           LD      A,(ARGN)
  F4DB C9               RET
  F4DC           
  F4DC          LUP     ;LOAD-Unterprogramm
  F4DC                  ;PA: CY=1 BRK gedr}ckt
  F4DC DD3403           INC     (IX+3)  ;erwarteter Blk
  F4DF CD95E5   LUP1    CALL    MBI
  F4E2 301A             JR      NC,LUP3
  F4E4 CDEEF1           CALL    OSTR
  F4E7 09090920         DB      9,9,9,' ',0
  F4EC CDE8F1           CALL    BNROST
  F4EF 203F             DB      ' ?'    ;fehlerhaft
  F4F1 1E00             DB      1EH,0
  F4F3 CD38F2           CALL    KBD
  F4F6 FE03             CP      3       ;BRK?
  F4F8 37               SCF
  F4F9 C8               RET     Z
  F4FA FE0A             CP      0AH     ;CUD?
  F4FC 20E1             JR      NZ,LUP1
  F4FE DD7E02   LUP3    LD      A,(IX+2)
  F501 DD4603           LD      B,(IX+3)
  F504 05               DEC     B       ;Block 01 erw.?
  F505 2817             JR      Z,LUP6
  F507 04               INC     B       ;richtige
  F508 B8               CP      B       ;Blocknummer?
  F509 280B             JR      Z,LUP5
  F50B 3C               INC     A       ;Block FF?
  F50C 2808             JR      Z,LUP5
  F50E CDE8F1   LUP4    CALL    BNROST
  F511 2A               DB      '*'     ;falsche Nr.
  F512 1900             DB      19H,0
  F514 18C9             JR      LUP1
  F516           
  F516 CDE8F1   LUP5    CALL    BNROST
  F519 3E20             DB      '> '    ;korrekter Blk.
  F51B 1900             DB      19H,0
  F51D C9               RET
  F51E           
  F51E 3D       LUP6    DEC     A       ;Block 01?
  F51F 20ED             JR      NZ,LUP4
  F521 CD6BF3           CALL    CRLF    ;keine Nr,
  F524 2100B7           LD      HL,0B700H
  F527 060B             LD      B,11    ;sondern Name
  F529 7E       LUP7    LD      A,M     ;ausgeben
  F52A 23               INC     HL
  F52B CD29E3           CALL    CRT
  F52E 10F9             DJNZ    LUP7
  F530 C35CF3           JP      SPACE
  F533           
  F533 CDEEF1   NOMC    CALL    OSTR    ;wenn kein MC
  F536 3F3F3F00         DB      '???',0
  F53A C3E6E4   CLJP    JP      CSROI
  F53D          ;
  F53D 7F7F             DW      7F7FH
  F53F 56455249         DB      'VERIFY',1
  F546 DDCB0786 VERIF   RES     0,(IX+7)        ;**11**
  F54A 180B             JR      LOAD0
  F54C          ;
  F54C 7F7F             DW      7F7FH
  F54E 4C4F4144         DB      'LOAD',1
  F553 DDCB07C6 LOAD    SET     0,(IX+7)        ;**10**
  F557 CDBBE4   LOAD0   CALL    ISRI1
  F55A DD360300         LD      (IX+3),0
  F55E CDDCF4           CALL    LUP
  F561 38D7     LOAD1   JR      C,CLJP  ;bei BRK
  F563 DDCB0746         BIT     0,(IX+7)
  F567 2849             JR      Z,LOAD4
  F569 2E10             LD      L,10H
  F56B DD7E07           LD      A,(IX+7)
  F56E E6E3             AND     0E3H    ;Bits 2..4 Null
  F570 DD7707           LD      (IX+7),A
  F573 7E               LD      A,M     ;Anzahl Arg.
  F574 CB27             SLA     A
  F576 CB27             SLA     A       ;2* schieben
  F578 E61C             AND     1CH     ;Maske
  F57A DDB607           OR      (IX+7)
  F57D DD7707           LD      (IX+7),A
  F580 7E               LD      A,M     ;Anzahl Arg.
  F581 D602             SUB     2
  F583 FE09             CP      9       ;MC?
  F585 30AC             JR      NC,NOMC
  F587 ED4B15B7         LD      BC,(0B715H)
  F58B ED5B13B7         LD      DE,(0B713H)
  F58F 2A11B7           LD      HL,(0B711H)
  F592 3A81B7           LD      A,(ARGN)
  F595 A7               AND     A       ;Ladeoffset?
  F596 2813             JR      Z,LOAD3
  F598 C5               PUSH    BC
  F599 ED4B82B7         LD      BC,(ARG1)
  F59D 09               ADD     HL,BC   ;AAdr,EAdr
  F59E EB               EX      DE,HL   ;umrechnen
  F59F 09               ADD     HL,BC
  F5A0 EB               EX      DE,HL
  F5A1 DDCB0766         BIT     4,(IX+7);4..7 Args?
  F5A5 2003             JR      NZ,LOAD2;nein
  F5A7 E3               EX      (SP),HL
  F5A8 09               ADD     HL,BC   ;SAdr umrechnen
  F5A9 E3               EX      (SP),HL
  F5AA C1       LOAD2   POP     BC      ;SAdr merken
  F5AB ED4384B7 LOAD3   LD      (ARG2),BC
  F5AF CD89F3           CALL    HLDE    ;AAdr,EAdr ausg.
  F5B2 CD6BF3   LOAD4   CALL    CRLF
  F5B5 EB               EX      DE,HL
  F5B6                  ;Load-Hauptschleife
  F5B6 CDDCF4   LOAD5   CALL    LUP
  F5B9 38A6             JR      C,LOAD1 ;BRK
  F5BB DDCB0746         BIT     0,(IX+7)
  F5BF 2812             JR      Z,LOAD7 ;wenn VERIFY
  F5C1 E5               PUSH    HL
  F5C2 ED52             SBC     HL,DE
  F5C4 018000           LD      BC,80H
  F5C7 ED42             SBC     HL,BC
  F5C9 09               ADD     HL,BC
  F5CA 3001             JR      NC,LOAD6
  F5CC 4D               LD      C,L
  F5CD 2100B7   LOAD6   LD      HL,0B700H ;MC in Spei-
  F5D0 EDB0             LDIR            ;cher kopieren
  F5D2 E1               POP     HL
  F5D3 DD3402   LOAD7   INC     (IX+2)  ;Blocknr. FF?
  F5D6 20DE             JR      NZ,LOAD5
  F5D8                  ;
  F5D8 CDE6E4   CSRI    CALL    CSROI           ;**0B**
  F5DB DD7E07           LD      A,(IX+7);kein Autostart
  F5DE E603             AND     3       ;wenn VERIFY,
  F5E0 3D               DEC     A       ;Bit 1(IX+7)=1
  F5E1 C0               RET     NZ
  F5E2 DD7E07           LD      A,(IX+7)
  F5E5 E61C             AND     1CH     ;und bei nur 2
  F5E7 FE0C             CP      0CH     ;Ladeargumenten
  F5E9 DD360700         LD      (IX+7),0
  F5ED D8               RET     C
  F5EE 2A84B7           LD      HL,(ARG2)
  F5F1 E9               JP      (HL)    ;Ansprung
  F5F2          ;
  F5F2 7F7F             DW      7F7FH
  F5F4 434F4C4F         DB      'COLOR',1
  F5FA CD5AFB   COLR    CALL    CON             ;**0F**
  F5FD CD63C1           CALL    COLRC
  F600 C366FB           JP      COFF
  F603           
  F603 E5       CSTBT   PUSH    HL              ;**42**
  F604 21A2B7           LD      HL,STBT
  F607 CBDE             SET     3,M
  F609 CD1DF2           CALL    OCHR    ;Steuerzeichen
  F60C CB9E             RES     3,M     ;darstellen
  F60E E1               POP     HL
  F60F C9               RET
  F610          ;
  F610 7F7F             DW      7F7FH
  F612 44495350         DB      'DISPLAY',1
  F61A CD5AFB   DISP    CALL    CON             ;**3B**
  F61D CD25C2           CALL    DISPC
  F620 C366FB   JCOFF   JP      COFF
  F623          ;
  F623 CD66FB   LDAME   CALL    COFF    ;f}r DISPC
  F626 7E               LD      A,M     ;und MODIC
  F627 C35AFB   JCON    JP      CON
  F62A          ;
  F62A 7F7F             DW      7F7FH
  F62C 4D4F4449         DB      'MODIFY',1
  F633 CD5AFB   MODI    CALL    CON             ;**2E**
  F636 CD4DC2           CALL    MODIC
  F639 18E5             JR      JCOFF
  F63B           
  F63B CD66FB   LDMAE   CALL    COFF    ;f}r MODIC
  F63E 77               LD      M,A
  F63F 18E6             JR      JCON
  F641          ;
  F641 7F7F             DW      7F7FH
  F643 57494E44         DB      'WINDOW',1
  F64A A7               AND     A       ;kein Arg?
  F64B 282A             JR      Z,WINAK ;Aufruf No. 0
  F64D 3D               DEC     A       ;1 Arg?
  F64E 2826             JR      Z,WINAKI;Aufruf No.L
  F650 65               LD      H,L
  F651 69               LD      L,C
  F652 53               LD      D,E
  F653 3A88B7           LD      A,(ARG49)
  F656 5F               LD      E,A
  F657 3A81B7           LD      A,(ARGN)
  F65A FE04             CP      4
  F65C 2005             JR      NZ,WIN1 ;PROGRAMMFEHLER
  F65E 3809             JR      C,WIN3  ;(vertauscht!)
  F660 AF               XOR     A
  F661 1803             JR      WIN2
  F663           
  F663 3A8AB7   WIN1    LD      A,(ARG49+2);Init No. A
  F666 CD6DF6   WIN2    CALL    WININ
  F669 DA61F3   WIN3    JP      C,ERRM
  F66C C9               RET
  F66D           
  F66D CD5AFB   WININ   CALL    CON             ;**3C**
  F670 CD2DC1           CALL    WININC
  F673 C366FB           JP      COFF
  F676           
  F676 7D       WINAKI  LD      A,L
  F677 CD94F6   WINAK   CALL    WCOPY           ;**3D**
  F67A 329BB7           LD      (WINNR),A
  F67D CD87F6           CALL    PART    ;Fenster A
  F680 D8               RET     C       ;holen
  F681 EB               EX      DE,HL
  F682 119CB7           LD      DE,WINON
  F685 181B             JR      WCOP1
  F687           
  F687          PART    ;Testet A und berechnet Pos.
  F687                  ;in Fenstervektorspeicher =DE
  F687 C6F6             ADD     0F6H
  F689 D8               RET     C
  F68A 87               ADD     A
  F68B 5F               LD      E,A
  F68C 87               ADD     A
  F68D 87               ADD     A
  F68E 83               ADD     E
  F68F 5F               LD      E,A
  F690 16B9             LD      D,0B9H
  F692 A7               AND     A
  F693 C9               RET
  F694           
  F694          WCOPY   ;Fenster (WINNR) retten
  F694 F5               PUSH    AF
  F695 3A9BB7           LD      A,(WINNR)
  F698 CD87F6           CALL    PART
  F69B DA5BE0           JP      C,IAD2
  F69E F1               POP     AF
  F69F 219CB7           LD      HL,WINON
  F6A2 010A00   WCOP1   LD      BC,10
  F6A5 EDB0             LDIR
  F6A7 C9               RET
  F6A8           
  F6A8 23       NCHAR   INC     HL
  F6A9 7E       KEYUP   LD      A,M     ;f}r KEY
  F6AA A7               AND     A
  F6AB C8               RET     Z
  F6AC CD03F6           CALL    CSTBT
  F6AF 18F7             JR      NCHAR
  F6B1          ;
  F6B1 7F7F             DW      7F7FH
  F6B3 4B455901         DB      'KEY',1
  F6B7 FE01             CP      1
  F6B9 C0               RET     NZ
  F6BA 7D               LD      A,L
  F6BB A7       KEY     AND     A               ;**3A**
  F6BC C8               RET     Z       ;KEY 0
  F6BD FE0D             CP      0DH     ;nur F-Tasten
  F6BF D0               RET     NC      ;1..C
  F6C0 47               LD      B,A
  F6C1 2100B9           LD      HL,0B900H
  F6C4 7E       KEY1    LD      A,M     ;B Dummys
  F6C5 23               INC     HL      ;ausz{hlen
  F6C6 A7               AND     A
  F6C7 20FB             JR      NZ,KEY1
  F6C9 10F9             DJNZ    KEY1
  F6CB CDA9F6           CALL    KEYUP   ;Belegung
  F6CE                                  ;pr{sentieren
  F6CE F5       KEY2    PUSH    AF
  F6CF 2B               DEC     HL
  F6D0 F1       KEY3    POP     AF
  F6D1 CD17F2   KEY4    CALL    INTB
  F6D4 FE13             CP      13H     ;STOP-Ende
  F6D6 CA6BF3           JP      Z,CRLF
  F6D9 F5               PUSH    AF
  F6DA 0600             LD      B,0
  F6DC 3D               DEC     A       ;CLR?
  F6DD 2015             JR      NZ,KEY5
  F6DF 7E               LD      A,M     ;Leere Belegung
  F6E0 A7               AND     A       ;nicht l|schen!
  F6E1 28ED             JR      Z,KEY3
  F6E3 54               LD      D,H     ;DE=Ziel
  F6E4 5D               LD      E,L
  F6E5 E5               PUSH    HL
  F6E6 23               INC     HL      ;HL=Quelle
  F6E7 3E9C             LD      A,9CH
  F6E9 95               SUB     L       ;L{nge, B=0
  F6EA 4F               LD      C,A     ;Pufferrest
  F6EB EDB0             LDIR            ;vorziehen
  F6ED E1               POP     HL
  F6EE F1               POP     AF
  F6EF CD1DF2           CALL    OCHR
  F6F2 18DA             JR      KEY2
  F6F4           
  F6F4 3A9AB9   KEY5    LD      A,(0B99AH);vorletztes
  F6F7 A7               AND     A       ;Zeichen<>0?
  F6F8 20D6             JR      NZ,KEY3 ;ja-voll!
  F6FA E5               PUSH    HL
  F6FB 3E9B             LD      A,9BH
  F6FD 95               SUB     L
  F6FE 4F               LD      C,A     ;L{nge, B=0
  F6FF 219AB9           LD      HL,0B99AH
  F702 119BB9           LD      DE,0B99BH;Pufferrest
  F705 EDB8             LDDR            ;hinterziehen
  F707 E1               POP     HL
  F708 F1               POP     AF
  F709 23               INC     HL      ;Zeichen
  F70A 77               LD      M,A     ;eintragen
  F70B CD03F6           CALL    CSTBT   ;& anzeigen
  F70E 18C1             JR      KEY4
  F710          ;
  F710 7F7F             DW      7F7FH
  F712 4B45594C         DB      'KEYLIST',1
  F71A CD5AFB   KEYLI   CALL    CON             ;**39**
  F71D CDF7C0           CALL    KEYLIC
  F720 C366FB           JP      COFF
  F723           
  F723          CUCP    ;nur f}r Klotz-Cursor!
  F723 D5               PUSH    DE              ;**25**
  F724 F5               PUSH    AF
  F725 E5               PUSH    HL
  F726 CD40E0           CALL    PADR0
  F729 380A             JR      C,CUCP2 ;au~erhalb
  F72B C5               PUSH    BC
  F72C 0608             LD      B,8
  F72E 7E       CUCP1   LD      A,M
  F72F 2F               CPL             ;compeln
  F730 77               LD      M,A
  F731 2C               INC     L
  F732 10FA             DJNZ    CUCP1   ;8*
  F734 C1               POP     BC
  F735 E1       CUCP2   POP     HL
  F736 F1               POP     AF
  F737 D1               POP     DE
  F738 C9               RET
  F739           
  F739 2AD3B7   BITT    LD      HL,(HOR)
  F73C 7D               LD      A,L
  F73D E607             AND     7       ;Bit-Nr.
  F73F CB3C             SRL     H
  F741 CB1D             RR      L
  F743 CB3C             SRL     H
  F745 CB1D             RR      L
  F747 CB3C             SRL     H
  F749 CB1D             RR      L       ;/8
  F74B C6F8             ADD     0F8H    ;Position in
  F74D 4F               LD      C,A     ;BITTAB
  F74E 06FD             LD      B,0FDH
  F750 3AD5B7           LD      A,(VERT)
  F753 2F               CPL             ;unten =0/0
  F754 67               LD      H,A
  F755 0A               LD      A,(BC)  ;Bit
  F756 C34EE0           JP      PADR
  F759           
  F759 E5       PUSE    PUSH    HL              ;**30**
  F75A D5               PUSH    DE
  F75B C5               PUSH    BC
  F75C A7               AND     A       ;Kennung CY=0
  F75D F5               PUSH    AF
  F75E 2AD3B7           LD      HL,(HOR)
  F761 ED5BD5B7         LD      DE,(VERT)
  F765 1600             LD      D,0
  F767 C3E5F7           JP      PUSEJ
  F76A           
  F76A CD5AFB   PUDE    CALL    CON             ;**2F**
  F76D CDE4C1           CALL    PUDEC
  F770 C366FB           JP      COFF
  F773           
  F773 3A86B7   CIRCL   LD      A,(ARG3)        ;**3F**
  F776 4F               LD      C,A
  F777 69               LD      L,C
  F778 AF               XOR     A
  F779 0608             LD      B,8
  F77B CB1D             RR      L
  F77D 3001     CIR1    JR      NC,CIR2
  F77F 81               ADD     C
  F780 1F       CIR2    RRA
  F781 CB1D             RR      L
  F783 10F8             DJNZ    CIR1
  F785 67               LD      H,A     ;Achtelkreis
  F786 54               LD      D,H     ;berechnen
  F787 5D               LD      E,L
  F788 79               LD      A,C
  F789 CB21             SLA     C
  F78B CB10             RL      B
  F78D 0B               DEC     BC
  F78E C5               PUSH    BC
  F78F 010100           LD      BC,1
  F792 D9               EXX
  F793 4F               LD      C,A
  F794 0600             LD      B,0
  F796 CDBBF7   CIR3    CALL    CIRUP   ;und jeweils 8
  F799 CDBBF7           CALL    CIRUP   ;Punkte setzen
  F79C D9               EXX
  F79D A7               AND     A
  F79E ED42             SBC     HL,BC
  F7A0 03               INC     BC
  F7A1 03               INC     BC
  F7A2 ED52             SBC     HL,DE
  F7A4 19               ADD     HL,DE
  F7A5 D9               EXX
  F7A6 300C             JR      NC,CIR4
  F7A8 D9               EXX
  F7A9 E3               EX      (SP),HL
  F7AA EB               EX      DE,HL
  F7AB A7               AND     A
  F7AC ED52             SBC     HL,DE
  F7AE 1B               DEC     DE
  F7AF 1B               DEC     DE
  F7B0 EB               EX      DE,HL
  F7B1 E3               EX      (SP),HL
  F7B2 D9               EXX
  F7B3 0D               DEC     C
  F7B4 04       CIR4    INC     B
  F7B5 79               LD      A,C
  F7B6 B8               CP      B
  F7B7 30DD             JR      NC,CIR3
  F7B9 F1               POP     AF
  F7BA C9               RET
  F7BB           
  F7BB          CIRUP   ;setzt 4 Punkte um Mittelpunkt
  F7BB 78               LD      A,B
  F7BC 41               LD      B,C
  F7BD 4F               LD      C,A
  F7BE AF               XOR     A
  F7BF 57               LD      D,A
  F7C0 2A84B7           LD      HL,(ARG2)
  F7C3 E5               PUSH    HL
  F7C4 59               LD      E,C
  F7C5 19               ADD     HL,DE
  F7C6 CDCFF7           CALL    CIRUP1
  F7C9 AF               XOR     A
  F7CA 57               LD      D,A
  F7CB E1               POP     HL
  F7CC 59               LD      E,C
  F7CD ED52             SBC     HL,DE
  F7CF E5       CIRUP1  PUSH    HL
  F7D0 2A82B7           LD      HL,(ARG1)
  F7D3 58               LD      E,B
  F7D4 A7               AND     A
  F7D5 ED52             SBC     HL,DE
  F7D7 D1               POP     DE
  F7D8 CDE1F7           CALL    SHLDE
  F7DB D5               PUSH    DE
  F7DC 57               LD      D,A
  F7DD 58               LD      E,B
  F7DE 19               ADD     HL,DE
  F7DF 19               ADD     HL,DE
  F7E0 D1               POP     DE
  F7E1                  ;
  F7E1          SHLDE   ;setzt Punkt auf Pos. HL/DE
  F7E1                  ;(wenn m|glich)
  F7E1 E5               PUSH    HL
  F7E2 D5               PUSH    DE
  F7E3 C5               PUSH    BC
  F7E4 F5               PUSH    AF
  F7E5 7D       PUSEJ   LD      A,L
  F7E6 E607             AND     7       ;Bitnummer
  F7E8 C6F8             ADD     BITTAB%256
  F7EA 4F               LD      C,A
  F7EB 06FD             LD      B,BITTAB/256
  F7ED 7D               LD      A,L
  F7EE CB3C             SRL     H
  F7F0 1F               RRA
  F7F1 CB3C             SRL     H       ;Bytespalte
  F7F3 1F               RRA
  F7F4 CB3C             SRL     H
  F7F6 1F               RRA
  F7F7 FE28             CP      28H     ;HL>13Fh?
  F7F9 3031             JR      NC,POP4
  F7FB F680             OR      80H
  F7FD 67               LD      H,A     ;im IRM
  F7FE 3EFF             LD      A,0FFH
  F800 82               ADD     D       ;D>0? (DE>0FFh?)
  F801 3829             JR      C,POP4
  F803 AB               XOR     E
  F804 6F               LD      L,A
  F805 3AD6B7           LD      A,(FARB)
  F808 57               LD      D,A
  F809 0A               LD      A,(BC)  ;BITTAB lesen
  F80A DDCB015E         BIT     3,(IX+1);HiRes?
  F80E 282D             JR      Z,HIRES
  F810 CB4A             BIT     1,D     ;L|schen?
  F812 2023             JR      NZ,CPLA
  F814 CB42             BIT     0,D     ;XOR?
  F816 2019             JR      NZ,XORA
  F818 B6               OR      M       ;Punkt setzen
  F819 77       XORJ    LD      M,A
  F81A DD7E01           LD      A,(IX+1)
  F81D 5F               LD      E,A
  F81E EE02             XOR     2       ;Farbebene
  F820 F3               DI
  F821 D384             OUT     84H
  F823 7E               LD      A,M
  F824 E607             AND     7
  F826 B2               OR      D
  F827 77               LD      M,A     ;neue vFarbe
  F828 7B               LD      A,E
  F829 D384             OUT     84H
  F82B FB               EI
  F82C F1       POP4    POP     AF
  F82D C1               POP     BC
  F82E D1               POP     DE
  F82F E1               POP     HL
  F830 C9               RET
  F831           
  F831 AE       XORA    XOR     M       ;Punkt setzen/
  F832 CB82             RES     0,D     ;r}cksetzen
  F834 C319F8           JP      XORJ
  F837           
  F837 2F       CPLA    CPL
  F838 A6               AND     M       ;Punkt l|schen
  F839 77               LD      M,A
  F83A C32CF8           JP      POP4
  F83D           
  F83D 47       HIRES   LD      B,A
  F83E B6               OR      M       ;Bit setzen
  F83F CB5A             BIT     3,D     ;Farbe rot?
  F841 2001             JR      NZ,HIR1
  F843 A8               XOR     B       ;wieder l|schen
  F844 77       HIR1    LD      M,A
  F845 DD7E01           LD      A,(IX+1)
  F848 4F               LD      C,A
  F849 EE02             XOR     2
  F84B F3               DI
  F84C D384             OUT     84H     ;Farbebene
  F84E 78               LD      A,B
  F84F B6               OR      M       ;setzen
  F850 CB62             BIT     4,D     ;Farbe t}rkis?
  F852 2001             JR      NZ,HIR2
  F854 A8               XOR     B       ;l|schen
  F855 77       HIR2    LD      M,A
  F856 79               LD      A,C
  F857 D384             OUT     84H
  F859 FB               EI
  F85A 18D0             JR      POP4
  F85C           
  F85C CD5AFB   LINE    CALL    CON             ;**3E**
  F85F CD7CC1           CALL    LINEC
  F862 1828             JR      COF
  F864           
  F864 CD5AFB   MBOUT   CALL    CON             ;**38**
  F867 CD0EC7           CALL    MBOC
  F86A 1820             JR      COF
  F86C           
  F86C CD5AFB   MBIN    CALL    CON             ;**37**
  F86F CD8AC7           CALL    MBIC
  F872 1818             JR      COF
  F874           
  F874 CD66FB   JIOERR  CALL    COFF    ;Sprung zu
  F877 2AC9B7           LD      HL,(IOERR);?IO ERROR
  F87A CD99F0           CALL    IRMOF
  F87D E9               JP      (HL)
  F87E          ;
  F87E 7F7F             DW      7F7FH
  F880 4D4F4455         DB      'MODUL',1
  F886 CD5AFB   ESC5    CALL    CON
  F889 CD00C0           CALL    MODULC
  F88C C366FB   COF     JP      COFF
  F88F          ;
  F88F 7F7F             DW      7F7FH
  F891 53595354         DB      'SYSTEM',1
  F898 CD5AFB   ESC6    CALL    CON
  F89B CD28C0           CALL    SYSTC
  F89E 18EC             JR      COF
  F8A0           
  F8A0          BASPV   ;BASIC-I/O-Verteiler
  F8A0 E5               PUSH    HL
  F8A1 C5               PUSH    BC
  F8A2 CD7AF0           CALL    IRMON
  F8A5 CB6B             BIT     5,E
  F8A7 D5               PUSH    DE
  F8A8 2053             JR      NZ,BASBYE
  F8AA 23               INC     HL
  F8AB 23               INC     HL
  F8AC CB7B             BIT     7,E
  F8AE 2042             JR      NZ,BSA2
  F8B0 E5               PUSH    HL
  F8B1 7B               LD      A,E
  F8B2 E607             AND     7
  F8B4 21EAF8           LD      HL,BUPTAB
  F8B7 85               ADD     L
  F8B8 6F               LD      L,A
  F8B9 7A               LD      A,D
  F8BA 53               LD      D,E
  F8BB 5E               LD      E,M     ;UP-Nummer
  F8BC E1               POP     HL
  F8BD CD77F0           CALL    PV3     ;Aufruf
  F8C0 D1               POP     DE
  F8C1 57               LD      D,A
  F8C2 7B               LD      A,E
  F8C3 E64F             AND     4FH
  F8C5 EE43             XOR     43H
  F8C7 2018             JR      NZ,BSA1
  F8C9 CDEEF1           CALL    OSTR
  F8CC 56455249         DB      'VERIFY ?(Y):',0
  F8D9 CD38F2           CALL    KBD
  F8DC FE59             CP      'Y'
  F8DE CC46F5           CALL    Z,VERIF
  F8E1 7A       BSA1    LD      A,D
  F8E2 CB9B             RES     3,E
  F8E4 CD99F0           CALL    IRMOF
  F8E7 C1               POP     BC
  F8E8 E1               POP     HL
  F8E9 C9               RET
  F8EA           
  F8EA          BUPTAB  ;H-Teil konstant!
  F8EA 16               DB      16H     ;INTB
  F8EB 24               DB      24H     ;OCHR
  F8EC 37               DB      37H     ;MBIN
  F8ED 38               DB      38H     ;MBOUT
  F8EE 06               DB      06H     ;USIN1
  F8EF 02               DB      02H     ;UOT1
  F8F0 07               DB      07H     ;USIN2
  F8F1 03               DB      03H     ;UOT2
  F8F2           
  F8F2 CDD1E3   BSA2    CALL    KBDS
  F8F5 D1               POP     DE
  F8F6 57               LD      D,A
  F8F7 30E8             JR      NC,BSA1
  F8F9 CBBB             RES     7,E
  F8FB 18E4             JR      BSA1
  F8FD           
  F8FD 3A5E03   BASBYE  LD      A,(35EH)
  F900 A7               AND     A
  F901 2809             JR      Z,BASB1
  F903 CD99F0           CALL    IRMOF
  F906 CD41C6           CALL    0C641H  ;NEW
  F909 CD7AF0           CALL    IRMON
  F90C DB88     BASB1   IN      A,88H
  F90E E65F             AND     5FH     ;BASIC-ROM aus
  F910 D388             OUT     88H
  F912 C37EF1           JP      LOOP
  F915           
  F915 7E       ZKOUT   LD      A,M             ;**45**
  F916 23               INC     HL
  F917 A7               AND     A
  F918 C8               RET     Z
  F919 CD1DF2           CALL    OCHR
  F91C 18F7             JR      ZKOUT
  F91E           
  F91E CDCDF4   TON     CALL    LARG            ;**35**
  F921 DDCB084E TON1    BIT     1,(IX+8);noch alter Ton?
  F925 20FA             JR      NZ,TON1
  F927 C5       TON2    PUSH    BC
  F928 0E8C             LD      C,8CH   ;CTC0
  F92A CD53F9           CALL    TON4    ;starten
  F92D 0C               INC     C       ;CTC1
  F92E EB               EX      DE,HL
  F92F CD53F9           CALL    TON4    ;starten
  F932 C1               POP     BC
  F933 79               LD      A,C     ;Lautst{rke
  F934 EE1F             XOR     1FH     ;da nullaktiv
  F936 F681             OR      81H     ;maskieren
  F938 4F               LD      C,A
  F939 78               LD      A,B
  F93A A7               AND     A       ;Dauerton?
  F93B 280D             JR      Z,TON3
  F93D CBB9             RES     7,C     ;nein, Blink aus
  F93F DDCB08CE         SET     1,(IX+8);neuer Ton
  F943 3EC7             LD      A,0C7H  ;EI,Z{hler 50Hz
  F945 D38E             OUT     8EH     ;CTC2
  F947 78               LD      A,B     ;Dauer
  F948 D38E             OUT     8EH
  F94A 0660     TON3    LD      B,60H
  F94C DB89     TONB    IN      A,89H
  F94E A0               AND     B       ;RAM8 maskieren
  F94F B1               OR      C
  F950 D389             OUT     89H     ;Ltst. ausgeben
  F952 C9               RET
  F953           
  F953          TON4    ;CTC (C) programmieren mit HL
  F953 7D               LD      A,L
  F954 A7               AND     A       ;Ton?
  F955 2E03             LD      L,3
  F957 280B             JR      Z,TON6  ;kein Ton
  F959 6F               LD      L,A
  F95A 3E07             LD      A,7     ;Zeitgeber
  F95C CB44             BIT     0,H     ;Vorteiler?
  F95E 2802             JR      Z,TON5  ;nein
  F960 F620             OR      20H
  F962 ED79     TON5    OUT     (C),A
  F964 ED69     TON6    OUT     (C),L
  F966 C9               RET
  F967           
  F967 2100B2   ESC1    LD      HL,0B200H
  F96A 22CBB7           LD      (VRAM),HL
  F96D DD7E01           LD      A,(IX+1)
  F970 E6F8             AND     0F8H
  F972 D384     OUT84   OUT     84H,A
  F974 DD7701           LD      (IX+1),A
  F977 C9               RET
  F978           
  F978 2100AD   ESC2    LD      HL,0AD00H
  F97B 22CBB7           LD      (VRAM),HL
  F97E DD7E01           LD      A,(IX+1)
  F981 F605             OR      5
  F983 18ED             JR      OUT84
  F985           
  F985 2100AD   ESC3    LD      HL,0AD00H
  F988 22CBB7           LD      (VRAM),HL
  F98B DD7E01           LD      A,(IX+1)
  F98E E6FE             AND     0FEH
  F990 F604             OR      4
  F992 18DE             JR      OUT84
  F994           
  F994 2100B2   ESC4    LD      HL,0B200H
  F997 22CBB7           LD      (VRAM),HL
  F99A DD7E01           LD      A,(IX+1)
  F99D F601             OR      1
  F99F E6F9             AND     0F9H
  F9A1 18CF             JR      OUT84
  F9A3           
  F9A3 21A2B7   ESC7    LD      HL,STBT
  F9A6 7E               LD      A,M
  F9A7 EE04             XOR     4
  F9A9 77               LD      M,A
  F9AA C9               RET
  F9AB           
  F9AB DD7E01   ESC9    LD      A,(IX+1)
  F9AE EE02             XOR     2
  F9B0 18C0             JR      OUT84
  F9B2           
  F9B2 DB89     ESCA    IN      A,89H
  F9B4 EE80             XOR     80H     ;Blinkbit
  F9B6 D389             OUT     89H
  F9B8 DD7E01           LD      A,(IX+1)
  F9BB EE08             XOR     8
  F9BD 18B3             JR      OUT84
  F9BF          ;
  F9BF 7F7F             DW      7F7FH
  F9C1 5632344F         DB      'V24OUT',1
  F9C8          V24OUT  ;Druckerroutine          **47**
  F9C8 CD5AFB           CALL    CON
  F9CB CDB8C2           CALL    V24OC
  F9CE 3E0D             LD      A,0DH
  F9D0 CDFFF9           CALL    PRINT
  F9D3 3E0A             LD      A,0AH
  F9D5 CDFFF9           CALL    PRINT
  F9D8 C366FB   COF1    JP      COFF
  F9DB           
  F9DB          HCPGM   ;Reaktion auf ShCLR
  F9DB CD5AFB           CALL    CON
  F9DE CDB2C3           CALL    HCPGMC
  F9E1 18F5             JR      COF1
  F9E3          ;
  F9E3 7F7F             DW      7F7FH
  F9E5 56323444         DB      'V24DUP',1
  F9EC          V24DUP  ;Duplexroutine           **48**
  F9EC CD5AFB           CALL    CON
  F9EF CD32C6           CALL    V24DC
  F9F2 18E4             JR      COF1
  F9F4           
  F9F4 CD5AFB   INIDUP  CALL    CON
  F9F7 CDA6C6           CALL    UMINIT
  F9FA 18DC             JR      COF1
  F9FC           
  F9FC CD29E3   ECHO    CALL    CRT
  F9FF F5       PRINT   PUSH    AF
  FA00 3AE1B7           LD      A,(HCPZ)
  FA03 CB7F             BIT     7,A
  FA05 2829             JR      Z,PRINT3;Drucker
  FA07 E6F0             AND     0F0H    ;Schreibmasch.
  FA09 FE90             CP      90H
  FA0B 2817             JR      Z,S3004 ;S3004
  FA0D FEA0             CP      0A0H
  FA0F 201F             JR      NZ,PRINT3
  FA11 F1               POP     AF      ;S6005
  FA12 E5               PUSH    HL
  FA13 C5               PUSH    BC
  FA14 2184FB           LD      HL,ZIBM
  FA17 010700           LD      BC,7
  FA1A EDB1             CPIR
  FA1C 200F             JR      NZ,PRINT2
  FA1E 0E06             LD      C,6
  FA20 09               ADD     HL,BC
  FA21 7E               LD      A,M
  FA22 1809             JR      PRINT2
  FA24           
  FA24 F1       S3004   POP     AF
  FA25 FE7E             CP      7EH
  FA27 2008             JR      NZ,PRINT4
  FA29 3E83             LD      A,83H   ;~
  FA2B 1804             JR      PRINT4
  FA2D           
  FA2D C1       PRINT2  POP     BC
  FA2E E1               POP     HL
  FA2F F5               PUSH    AF
  FA30 F1       PRINT3  POP     AF
  FA31 FE09     PRINT4  CP      9       ;CUR
  FA33 280D             JR      Z,PRINT5
  FA35 F5               PUSH    AF
  FA36 3AA2B7           LD      A,(STBT)
  FA39 CB5F             BIT     3,A     ;Steuerzeichen
  FA3B 280E             JR      Z,OFILT1;ausf}hren
  FA3D F1               POP     AF
  FA3E FE7F             CP      7FH     ;und 7Fh
  FA40 2002             JR      NZ,PRINT6
  FA42 3E20     PRINT5  LD      A,20H   ;nach SPC
  FA44 FE20     PRINT6  CP      20H     ;und Steuerzei-
  FA46 3002             JR      NC,OFILT;chen nach '_'
  FA48 3E5F             LD      A,'_'   ;wandeln
  FA4A          OFILT   ;Ausgabe ohne Filter
  FA4A F5               PUSH    AF
  FA4B C5       OFILT1  PUSH    BC
  FA4C CDCDFA           CALL    LCSO
  FA4F 3E0A             LD      A,0AH
  FA51 81               ADD     C
  FA52 4F               LD      C,A
  FA53 ED78             IN      A,(C)
  FA55 CB57             BIT     2,A     ;bereit?
  FA57 2008             JR      NZ,OFILT2
  FA59 3E01             LD      A,1
  FA5B CDE0F1           CALL    WAIT    ;nein, warten
  FA5E C1               POP     BC
  FA5F 18EA             JR      OFILT1
  FA61                  ;
  FA61 C1       OFILT2  POP     BC
  FA62 F1               POP     AF
  FA63 C5               PUSH    BC
  FA64 F5               PUSH    AF
  FA65 CDCDFA           CALL    LCSO
  FA68 3E08     V24PUT  LD      A,8
  FA6A 81               ADD     C
  FA6B 4F               LD      C,A
  FA6C F1               POP     AF
  FA6D ED79             OUT     (C),A   ;Ausgabe
  FA6F C1               POP     BC
  FA70 C9               RET
  FA71           
  FA71 F5       V24OT   PUSH    AF      ;wie OFILT
  FA72 C5       V24OT1  PUSH    BC
  FA73 CDDAFA           CALL    LCSOD
  FA76 3E0A             LD      A,0AH
  FA78 81               ADD     C
  FA79 4F               LD      C,A
  FA7A ED78             IN      A,(C)
  FA7C CB57             BIT     2,A
  FA7E 2008             JR      NZ,V24OT2
  FA80 3E01             LD      A,1
  FA82 CDE0F1           CALL    WAIT
  FA85 C1               POP     BC
  FA86 18EA             JR      V24OT1
  FA88 C1       V24OT2  POP     BC
  FA89 F1               POP     AF
  FA8A C5               PUSH    BC
  FA8B F5               PUSH    AF
  FA8C CDDAFA           CALL    LCSOD
  FA8F 18D7             JR      V24PUT
  FA91           
  FA91 C5       V24IN   PUSH    BC
  FA92 E5               PUSH    HL
  FA93 D5               PUSH    DE
  FA94 CDDAFA           CALL    LCSOD
  FA97 C5               PUSH    BC
  FA98 3E0A             LD      A,0AH
  FA9A 81               ADD     C
  FA9B 4F               LD      C,A
  FA9C ED78             IN      A,(C)
  FA9E CB47             BIT     0,A     ;bereit?
  FAA0 2015             JR      NZ,V24IN2
  FAA2 3E05             LD      A,5     ;WR5
  FAA4 ED79             OUT     (C),A
  FAA6 3EEA             LD      A,0EAH  ;DTR on, 8 bit,
  FAA8 ED79             OUT     (C),A   ;Senderfreigabe
  FAAA ED78     V24IN1  IN      A,(C)
  FAAC CB47             BIT     0,A     ;jetzt bereit?
  FAAE 2007             JR      NZ,V24IN2
  FAB0 CDE5E3           CALL    BRKT
  FAB3 3815             JR      C,V24IN4
  FAB5 18F3             JR      V24IN1
  FAB7           
  FAB7 3E05     V24IN2  LD      A,5     ;WR5
  FAB9 ED79             OUT     (C),A
  FABB 3E6A             LD      A,6AH   ;DTR off usw.
  FABD ED79             OUT     (C),A
  FABF C1               POP     BC
  FAC0 3E08             LD      A,8
  FAC2 81               ADD     C
  FAC3 4F               LD      C,A
  FAC4 ED78             IN      A,(C)   ;einlesen
  FAC6 D1       V24IN3  POP     DE
  FAC7 E1               POP     HL
  FAC8 C1               POP     BC
  FAC9 C9               RET
  FACA           
  FACA C1       V24IN4  POP     BC
  FACB 18F9             JR      V24IN3
  FACD           
  FACD          LCSO    ;Lade C mit SIO-Offset f.V24OUT
  FACD F5               PUSH    AF
  FACE 3AE1B7           LD      A,(HCPZ)
  FAD1 CB57     LCSO1   BIT     2,A
  FAD3 0E01             LD      C,1
  FAD5 2001             JR      NZ,LCSOE
  FAD7 0D               DEC     C
  FAD8 F1       LCSOE   POP     AF
  FAD9 C9               RET
  FADA           
  FADA          LCSOD   ;dto. f}r V24DUP
  FADA F5               PUSH    AF
  FADB 3AE8B7           LD      A,(HCPZ2)
  FADE 18F1             JR      LCSO1
  FAE0           
  FAE0          ISRSB   ;ISR SIO B f}r MC-Load oder
  FAE0                  ;Fremd-Tastatur
  FAE0 E5               PUSH    HL
  FAE1 D5               PUSH    DE
  FAE2 C5               PUSH    BC
  FAE3 F5               PUSH    AF
  FAE4 DB09             IN      A,9     ;1. Zeichen
  FAE6 F5               PUSH    AF
  FAE7 3E18             LD      A,18H   ;Kanal
  FAE9 D30B             OUT     0BH     ;r}cksetzen
  FAEB 3E05             LD      A,5     ;WR5
  FAED D30B             OUT     0BH
  FAEF 3E6A             LD      A,6AH   ;DTR off
  FAF1 D30B             OUT     0BH
  FAF3 F1               POP     AF
  FAF4 CD50FB           CALL    ISB8
  FAF7 FE0D             CP      0DH     ;Fremdtastatur?
  FAF9 280A             JR      Z,ISB3
  FAFB FE1B             CP      1BH     ;MC-Programm?
  FAFD 2813             JR      Z,ISB4
  FAFF CDF4F9   ISB1    CALL    INIDUP  ;nichts
  FB02 C324E1   ISB2    JP      POPS4
  FB05           
  FB05 CDF4F9   ISB3    CALL    INIDUP
  FB08 2153FB           LD      HL,ISB9 ;neue ISR
  FB0B F3               DI
  FB0C 22E201           LD      (1E2H),HL
  FB0F FB               EI
  FB10 18F0             JR      ISB2
  FB12           
  FB12 3E06     ISB4    LD      A,6
  FB14 32E8B7           LD      (HCPZ2),A
  FB17 CDECF9           CALL    V24DUP
  FB1A CD91FA           CALL    V24IN
  FB1D FE54             CP      'T'
  FB1F 2806             JR      Z,ISB5  ;MC laden
  FB21 FE55             CP      'U'
  FB23 281E             JR      Z,ISB7  ;MC starten
  FB25 18D8             JR      ISB1
  FB27           
  FB27 CD91FA   ISB5    CALL    V24IN
  FB2A 6F               LD      L,A
  FB2B CD91FA           CALL    V24IN
  FB2E 67               LD      H,A
  FB2F CD91FA           CALL    V24IN
  FB32 4F               LD      C,A
  FB33 CD91FA           CALL    V24IN
  FB36 47               LD      B,A
  FB37 CD91FA   ISB6    CALL    V24IN
  FB3A 77               LD      M,A
  FB3B 23               INC     HL
  FB3C 0B               DEC     BC
  FB3D 79               LD      A,C
  FB3E B0               OR      B
  FB3F 20F6             JR      NZ,ISB6
  FB41 18BC             JR      ISB1
  FB43           
  FB43 CD91FA   ISB7    CALL    V24IN
  FB46 6F               LD      L,A
  FB47 CD91FA           CALL    V24IN
  FB4A 67               LD      H,A
  FB4B CD99E1           CALL    JPHL
  FB4E 18AF             JR      ISB1
  FB50           
  FB50 FB       ISB8    EI
  FB51 ED4D             RETI
  FB53           
  FB53          ISB9    ;ISR SIO-B f}r Fremdtastatur
  FB53 FB               EI
  FB54 F5               PUSH    AF
  FB55 DB09             IN      A,9
  FB57 C3BCE3           JP      TST7
  FB5A           
  FB5A          CON     ;CAOS-ROM C on
  FB5A F5               PUSH    AF
  FB5B DDCB04FE         SET     7,(IX+4)
  FB5F DD7E04           LD      A,(IX+4)
  FB62 D386             OUT     86H
  FB64 F1               POP     AF
  FB65 C9               RET
  FB66           
  FB66          COFF    ;CAOS-ROM C off
  FB66 F5               PUSH    AF
  FB67 DDCB04BE         RES     7,(IX+4)
  FB6B DD7E04           LD      A,(IX+4)
  FB6E D386             OUT     86H
  FB70 F1               POP     AF
  FB71 C9               RET
  FB72           
  FB72          MULT    ;BA=D*C                  **41**
  FB72 CD5AFB           CALL    CON
  FB75 CD1CC1           CALL    MULC
  FB78 C366FB           JP      COFF
  FB7B           
  FB7B CD5AFB   SQR     CALL    CON             ;**40**
  FB7E CD0EC1           CALL    SQRC
  FB81 C366FB           JP      COFF
  FB84           
  FB84 7B7C7D7E ZIBM    DEFM    '{|}~'
  FB88 5B5C5D           DEFM    '[\]'
  FB8B 84               DEFB    84H
  FB8C 94               DEFB    94H
  FB8D 81               DEFB    81H
  FB8E E1               DEFB    0E1H
  FB8F 8E               DEFB    8EH
  FB90 99               DEFB    99H
  FB91 9A               DEFB    9AH
  FB92           
  FB92 00       ZEI0    DEFB    0       ;CRT
  FB93 38               DEFB    38H     ;MBOUT
  FB94 02       ZEI2    DEFB    2       ;UOUT1
  FB95 03       ZEI3    DEFB    3       ;UOUT2
  FB96 04       ZEI4    DEFB    4       ;KBD
  FB97 37               DEFB    37H     ;MBIN
  FB98 06               DEFB    6       ;UIN1
  FB99 07               DEFB    7       ;UIN2
  FB9A           
  FB9A          L3TB    ;ESC-Tabelle
  FB9A 9AE1             DEFW    ESC0    ;Tabulator
  FB9C 67F9             DEFW    ESC1    ;Anz0   Zugr0
  FB9E 78F9             DEFW    ESC2    ;1      1
  FBA0 85F9             DEFW    ESC3    ;0      1
  FBA2 94F9             DEFW    ESC4    ;1      0
  FBA4 86F8             DEFW    ESC5    ;MODUL
  FBA6 98F8             DEFW    ESC6    ;SYSTEM
  FBA8 A3F9             DEFW    ESC7    ;PixInvers o/o
  FBAA 0FE3             DEFW    ESC8    ;ColSwap o/o
  FBAC ABF9             DEFW    ESC9    ;Farbebene o/o
  FBAE B2F9             DEFW    ESCA    ;HiRes o/o
  FBB0           
  FBB0 29E3     SUTB    DEFW    CRT     ;00
  FBB2 2FE5             DEFW    MBO     ;01
  FBB4 BDB7             DEFW    UOUT1   ;02
  FBB6 C3B7             DEFW    UOUT2   ;03
  FBB8 38F2             DEFW    KBD     ;04
  FBBA 95E5             DEFW    MBI     ;05
  FBBC C0B7             DEFW    UIN1    ;06
  FBBE C6B7             DEFW    UIN2    ;07
  FBC0 20E5             DEFW    ISRO    ;08
  FBC2 DFE4             DEFW    CSRO    ;09
  FBC4 92E5             DEFW    ISRI    ;0A
  FBC6 D8F5             DEFW    CSRI    ;0B
  FBC8 D1E3             DEFW    KBDS    ;0C
  FBCA F6F0             DEFW    BYE     ;0D
  FBCC DCE3             DEFW    KBDZ    ;0E
  FBCE FAF5             DEFW    COLR    ;0F
  FBD0 53F5             DEFW    LOAD    ;10
  FBD2 46F5             DEFW    VERIF   ;11
  FBD4 7EF1             DEFW    LOOP    ;12
  FBD6 FAF1             DEFW    NORM    ;13
  FBD8 E0F1             DEFW    WAIT    ;14
  FBDA CDF4             DEFW    LARG    ;15
  FBDC 17F2             DEFW    INTB    ;16
  FBDE 34F3             DEFW    INLIN   ;17
  FBE0 92F3             DEFW    RHEX    ;18
  FBE2 61F3             DEFW    ERRM    ;19
  FBE4 54F3             DEFW    HLHX    ;1A
  FBE6 89F3             DEFW    HLDE    ;1B
  FBE8 76F3             DEFW    AHEX    ;1C
  FBEA B1F1             DEFW    ZSUCH   ;1D
  FBEC 00F2             DEFW    SOUT    ;1E
  FBEE 0DF2             DEFW    SIN     ;1F
  FBF0 FDF1             DEFW    NOUT    ;20
  FBF2 0AF2             DEFW    NIN     ;21
  FBF4 C7F3             DEFW    GARG    ;22
  FBF6 EEF1             DEFW    OSTR    ;23
  FBF8 1DF2             DEFW    OCHR    ;24
  FBFA 23F7             DEFW    CUCP    ;25
  FBFC 0BE4             DEFW    MODU    ;26
  FBFE A2E4             DEFW    JUMP    ;27
  FC00 16E3             DEFW    LDMA    ;28
  FC02 18E3             DEFW    LDAM    ;29
  FC04 E5E3             DEFW    BRKT    ;2A
  FC06 5CF3             DEFW    SPACE   ;2B
  FC08 6BF3             DEFW    CRLF    ;2C
  FC0A 72F3             DEFW    HOME    ;2D
  FC0C 33F6             DEFW    MODI    ;2E
  FC0E 6AF7             DEFW    PUDE    ;2F
  FC10 59F7             DEFW    PUSE    ;30
  FC12 F4F3             DEFW    SIXD    ;31
  FC14 6AE0             DEFW    DABR    ;32
  FC16 5EE0             DEFW    TCIF    ;33
  FC18 4EE0             DEFW    PADR    ;34
  FC1A 1EF9             DEFW    TON     ;35
  FC1C 8AF4             DEFW    SAVE    ;36
  FC1E 6CF8             DEFW    MBIN    ;37
  FC20 64F8             DEFW    MBOUT   ;38
  FC22 BBF6             DEFW    KEY     ;39
  FC24 1AF7             DEFW    KEYLI   ;3A
  FC26 1AF6             DEFW    DISP    ;3B
  FC28 6DF6             DEFW    WININ   ;3C
  FC2A 77F6             DEFW    WINAK   ;3D
  FC2C 5CF8             DEFW    LINE    ;3E
  FC2E 73F7             DEFW    CIRCL   ;3F
  FC30 7BFB             DEFW    SQR     ;40
  FC32 72FB             DEFW    MULT    ;41
  FC34 03F6             DEFW    CSTBT   ;42
  FC36 3CF4             DEFW    INIEA   ;43
  FC38 45F4             DEFW    INIME   ;44
  FC3A 15F9             DEFW    ZKOUT   ;45
  FC3C 47F1             DEFW    MENU    ;46
  FC3E C8F9             DEFW    V24OUT  ;47
  FC40 ECF9             DEFW    V24DUP  ;48
  FC42           
  FC42 5777     KTAB    DEFM    'Ww'
  FC44 4161             DEFM    'Aa'
  FC46 3222             DEFM    '2"'
  FC48 0819             DEFW    1908H
  FC4A 100C             DEFW    0C10H
  FC4C 2D3D             DEFM    '-='
  FC4E F2F8             DEFW    0F8F2H
  FC50 5979             DEFM    'Yy'
  FC52 4565             DEFM    'Ee'
  FC54 5373             DEFM    'Ss'
  FC56 3323             DEFM    '3#'
  FC58 5E5D             DEFM    '^]'
  FC5A 010F             DEFW    0F01H
  FC5C 3A2A             DEFM    ':*'
  FC5E F3F9             DEFW    0F9F3H
  FC60 5878             DEFM    'Xx'
  FC62 5474             DEFM    'Tt'
  FC64 4666             DEFM    'Ff'
  FC66 3525             DEFM    '5%'
  FC68 5070             DEFM    'Pp'
  FC6A 1F02             DEFW    21FH
  FC6C 30               DEFM    '0'
  FC6D 40               DEFB    40H
  FC6E F5FB             DEFW    0FBF5H
  FC70 5676             DEFM    'Vv'
  FC72 5575             DEFM    'Uu'
  FC74 4868             DEFM    'Hh'
  FC76 37               DEFM    '7'
  FC77 27               DEFB    27H
  FC78 4F6F             DEFM    'Oo'
  FC7A 1A14             DEFW    141AH
  FC7C 3929             DEFM    '9)'
  FC7E 0303             DEFW    0303H
  FC80 4E6E             DEFM    'Nn'
  FC82 4969             DEFM    'Ii'
  FC84 4A6A             DEFM    'Jj'
  FC86 3828             DEFM    '8('
  FC88 205B             DEFM    ' ['
  FC8A 4B6B             DEFM    'Kk'
  FC8C 2C3C             DEFM    ',<'
  FC8E 131B             DEFW    1B13H
  FC90 4D6D             DEFM    'Mm'
  FC92 5A7A             DEFM    'Zz'
  FC94 4767             DEFM    'Gg'
  FC96 3626             DEFM    '6&'
  FC98 0000             DEFW    0
  FC9A 4C6C             DEFM    'Ll'
  FC9C 2E3E             DEFM    '.>'
  FC9E F6FC             DEFW    0FCF6H
  FCA0 4262             DEFM    'Bb'
  FCA2 5272             DEFM    'Rr'
  FCA4 4464             DEFM    'Dd'
  FCA6 3424             DEFM    '4$'
  FCA8 5F5C             DEFM    '_\'
  FCAA 2B3B             DEFM    '+;'
  FCAC 2F3F             DEFM    '/?'
  FCAE F4FA             DEFW    0FAF4H
  FCB0 4363             DEFM    'Cc'
  FCB2 5171             DEFM    'Qq'
  FCB4 1616             DEFW    1616H
  FCB6 3121             DEFM    '1!'
  FCB8 0A12             DEFW    120AH
  FCBA 0B11             DEFW    110BH
  FCBC 0918             DEFW    1809H
  FCBE F1F7             DEFW    0F7F1H
  FCC0 0D0D             DEFW    0D0DH
  FCC2           
  FCC2 E0FA     ISRTAB  DEFW    ISRSB   ;SIO B:Empfang
  FCC4 0CE6             DEFW    ISRPA   ;PIO A:Kassette
  FCC6 49E3             DEFW    ISRPB   ;PIO B:Tastatur
  FCC8 1BE6             DEFW    INTEND  ;CTC 0:frei
  FCCA FCE5             DEFW    ISRC1   ;CTC 1:Kassette
  FCCC 53E6             DEFW    ISRC2   ;CTC 2:Tondauer
  FCCE 39E3             DEFW    ISRC3   ;CTC 3:Tastatur
  FCD0           
  FCD0 22E3     CRTTAB  DEFW    NOT     ;00
  FCD2 E2E1             DEFW    CLR     ;01
  FCD4 AAE2             DEFW    CLLN    ;02
  FCD6 22E3             DEFW    NOT     ;03=BRK
  FCD8 22E3             DEFW    NOT     ;04
  FCDA 22E3             DEFW    NOT     ;05
  FCDC 22E3             DEFW    NOT     ;06
  FCDE EBE2             DEFW    BEEP    ;07
  FCE0 C1E1             DEFW    CUL     ;08
  FCE2 AAE1             DEFW    CUR     ;09
  FCE4 B3E1             DEFW    CUD     ;0A
  FCE6 D1E1             DEFW    CUU     ;0B
  FCE8 37E2             DEFW    CLS     ;0C
  FCEA 47E2             DEFW    CBL     ;0D
  FCEC 22E3             DEFW    NOT     ;0E
  FCEE BDE1             DEFW    HCOPY   ;0F
  FCF0 45E2             DEFW    HOMEPG  ;10
  FCF2 D6E1             DEFW    PAGE    ;11
  FCF4 DDE1             DEFW    SCROL   ;12
  FCF6 22E3             DEFW    NOT     ;13=STOP
  FCF8 1AE3             DEFW    CLICK   ;14
  FCFA 22E3             DEFW    NOT     ;15
  FCFC 22E3             DEFW    NOT     ;16=CAPS
  FCFE 22E3             DEFW    NOT     ;17
  FD00 CBE1             DEFW    CEL     ;18
  FD02 47E2             DEFW    CBL     ;19
  FD04 18E2             DEFW    INS     ;1A
  FD06 23E3             DEFW    ESC     ;1B
  FD08 22E3             DEFW    NOT     ;1C=LIST
  FD0A 22E3             DEFW    NOT     ;1D=RUN
  FD0C B1E1             DEFW    NL      ;1E=CONT
  FD0E E5E1             DEFW    DEL     ;1F
  FD10           
  FD10          TOKTAB  ;BASIC-Token
  FD10                  ;(Bit 7 im 1. Byte gesetzt!)
  FD10 C94E4B45         DB      '?NKEY$'
  FD16 CA4F5953         DB      '?OYST'
  FD1B D3545249         DB      '?TRING$'
  FD22 C94E5354         DB      '?NSTR'
  FD27 D2454E55         DB      '?ENUMBER'
  FD2F C4454C45         DB      '?ELETE'
  FD35 D0415553         DB      '?AUSE'
  FD3A C2454550         DB      '?EEP'
  FD3E D7494E44         DB      '?INDOW'
  FD44 C24F5244         DB      '?ORDER'
  FD4A C94E4B           DB      '?NK'
  FD4D D0415045         DB      '?APER'
  FD52 C154             DB      '?T'
  FD54 C34F4C4F         DB      '?OLOR'
  FD59 D34F554E         DB      '?OUND'
  FD5E D0534554         DB      '?SET'
  FD62 D0524553         DB      '?RESET'
  FD68 C24C4F41         DB      '?LOAD'
  FD6D D6504545         DB      '?PEEK'
  FD72 D6504F4B         DB      '?POKE'
  FD77 CC4F4341         DB      '?OCATE'
  FD7D CB45594C         DB      '?EYLIST'
  FD84 CB4559           DB      '?EY'
  FD87 D3574954         DB      '?WITCH'
  FD8D D0544553         DB      '?TEST'
  FD92 C34C4F53         DB      '?LOSE'
  FD97 CF50454E         DB      '?PEN'
  FD9B D2414E44         DB      '?ANDOMIZE'
  FDA4 D6474554         DB      '?GET$'
  FDA9 CC494E45         DB      '?INE'
  FDAD C3495243         DB      '?IRCLE'
  FDB3 C353524C         DB      '?SRLIN'
  FDB9 80               DB      80H
  FDBA           
  FDBA          TOKJP   ;Sprungtabelle f}r BASIC-Token
  FDBA 8AEB             DW      INKEY
  FDBC FD02             DW      02FDH   ;JOYST
  FDBE 13E7             DW      STRING
  FDC0 31EC             DW      INSTR
  FDC2 72E7             DW      RENUM
  FDC4 52E9             DW      DELETE
  FDC6 77E9             DW      PAUSE
  FDC8 ABE9             DW      BBEEP
  FDCA C0E9             DW      WINDOW
  FDCC 96EA             DW      BORDER
  FDCE 07EB             DW      INK
  FDD0 21EB             DW      PAPER
  FDD2 48C3             DW      0C348H  ;AT
  FDD4 3DEB             DW      BCOLOR
  FDD6 A5EB             DW      SOUND
  FDD8 E8EB             DW      PSET
  FDDA 19EC             DW      PRESET
  FDDC 4AEA             DW      BLOAD
  FDDE 48C3             DW      0C348H  ;VPEEK
  FDE0 79EA             DW      VPOKE
  FDE2 56EB             DW      LOCATE
  FDE4 B5EC             DW      KEYLIST
  FDE6 A6EC             DW      BKEY
  FDE8 B9EC             DW      SWITCH
  FDEA 48C3             DW      0C348H  ;PTEST
  FDEC 05ED             DW      CLOSE
  FDEE 46ED             DW      OPEN
  FDF0 40ED             DW      RANDOM
  FDF2 48C3             DW      0C348H  ;VGET
  FDF4 7BED             DW      BLINE
  FDF6 80ED             DW      CIRCLE
  FDF8           
  FDF8 80       BITTAB  DEFB    80H
  FDF9 40               DEFB    40H
  FDFA 20               DEFB    20H
  FDFB 10               DEFB    10H
  FDFC 08               DEFB    8
  FDFD 04               DEFB    4
  FDFE 02               DEFB    2
  FDFF 01               DEFB    1
  FE00           
  FE00                  END

Errors: 0
End+1 : FE00 
